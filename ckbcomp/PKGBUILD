# Maintainer: aur.chaotic.cx

_pkgname="ckbcomp"
pkgname="$_pkgname"
pkgver=1.233
pkgrel=1
pkgdesc="Compile a XKB keyboard description to a keymap suitable for loadkeys or kbdcontrol"
url="https://salsa.debian.org/installer-team/console-setup"
license=('GPL-2.0-or-later')
arch=('any')

makedepends=('git')

_pkgsrc="console-setup"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  _tag=$(git tag | grep -Ev '[A-Za-z]{2}' | sort -rV | head -1)
  git -c advice.detachedHead=false checkout -f "$_tag"
}

pkgver() {
  cd "$_pkgsrc"
  git describe --tags
}

package() {
  depends+=('perl')

  cd "$_pkgsrc"
  install -Dm755 Keyboard/ckbcomp -t "$pkgdir/usr/bin/"
}
