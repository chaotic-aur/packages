# Maintainer: Runnytu < runnytu at gmail dot com >
# OldMaintainer: FreeK <stephan@confidr.me>
# Contributor: olav-st <olav.s.th@gmail.com>
# Contributor: David Manouchehri <manouchehri@riseup.net>

### BUILD OPTIONS
# Set to y to enable nomachine service autostart
_autoservice=n
# Set to y to enable firewall autorules
_autofirewall=n
### END BUILD OPTIONS

pkgname=nomachine
pkgver=8.12.12
_pkgvermain=8.12
_pkgrel_i686=4
_pkgrel_x86_64=4
_pkgrel_armv6h=3
_pkgrel_armv7h=3
_pkgrel_armv8h=3
_pkgrel_aarch64=${_pkgrel_armv8h}
_pkgrel_pentium4=${_pkgrel_i686}
pkgrel=1
pkgdesc="Remote desktop application"
groups=('network')
url="http://www.nomachine.com"
license=('custom:"NoMachine EULA"')
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'armv8h' 'aarch64' 'pentium4')
options=('!strip')
conflicts=('nxmanager' 'nxwebplayer' 'nxserver' 'nxnode' 'nxclient')
depends=('bash' 'openssh' 'nawk' 'polkit' 'rpm-tools' 'dkms')
optdepends=('xorg-xauth: allows logging into a headless machine')
sha512sums_x86_64=('b5610c0d41a7a6b5e770a5b968fb589a1f3f73a5e00df1643af9cac5a5834dc5eb710e7e5ebaa046a9f210c9c70a70b54bfc727bc7bf8ed65ab48a1e1df87331')
sha512sums_i686=('fa8c0dcb0f2d6e83d12ab192b88fc58b6df2d6512a7125eaaff19921aa0b14240bccb0e776d6b1672d7a7acea7db46d5e671c54c495544b1c4043fe1887096e3')
sha512sums_armv6h=('d59ca26521d85fec68894d72a286869deb21816115378ec4d4a91aad31759b89b285308efe97e5ee4f22f6c4bc50a0ed330962eb9550c76dbe7a22248d8b6d27')
sha512sums_armv7h=('d9ca137cd150825007147d59b8019ce9d7ab2602772720d43a1f211dc47062a21afe43a133885a87563a19bfaf7b10ad9ef322aa9909e51eefc17bf0bccf6f71')
sha512sums_armv8h=('eb7bfdbee35202fa9c48f9b6296d8f912bf2b8d35d8712ec1bb2455d93315c8bdb6c035f1ef70d7f1fd7a3bb9bf8f9ff5a839098723312656f25bc5af11a6d2d')
sha512sums_aarch64=('eb7bfdbee35202fa9c48f9b6296d8f912bf2b8d35d8712ec1bb2455d93315c8bdb6c035f1ef70d7f1fd7a3bb9bf8f9ff5a839098723312656f25bc5af11a6d2d')
sha512sums_pentium4=('fa8c0dcb0f2d6e83d12ab192b88fc58b6df2d6512a7125eaaff19921aa0b14240bccb0e776d6b1672d7a7acea7db46d5e671c54c495544b1c4043fe1887096e3')
source_x86_64=("http://download.nomachine.com/download/${_pkgvermain}/Linux/${pkgname}_${pkgver}_${_pkgrel_x86_64}_x86_64.tar.gz")
source_i686=("http://download.nomachine.com/download/${_pkgvermain}/Linux/${pkgname}_${pkgver}_${_pkgrel_i686}_i686.tar.gz")
source_armv6h=("http://download.nomachine.com/download/${_pkgvermain}/Raspberry/${pkgname}_${pkgver}_${_pkgrel_armv6h}_armv6hl.tar.gz")
source_armv7h=("http://download.nomachine.com/download/${_pkgvermain}/Raspberry/${pkgname}_${pkgver}_${_pkgrel_armv7h}_armv7hl.tar.gz")
source_armv8h=("http://download.nomachine.com/download/${_pkgvermain}/Raspberry/${pkgname}_${pkgver}_${_pkgrel_armv8h}_aarch64.tar.gz")
source_aarch64=("${source_armv8h[@]}")
source_pentium4=("${source_i686[@]}")
install=nomachine.install

prepare() {
  #Fix Fedora Version Var And Libstdc++ Rename
  tar -zxf "$srcdir/NX/etc/NX/server/packages/nxrunner.tar.gz" NX/scripts/setup/nxrunner
  sed -i 's/    majorFedoraVersion.*/    majorFedoraVersion=23/' "$srcdir/NX/scripts/setup/nxrunner"
  tar -zxf "$srcdir/NX/etc/NX/server/packages/nxrunner.tar.gz" "NX/lib/"
  for _libstdc in "$srcdir/NX/lib/"libstdc++.*; do
    mv "${_libstdc}" ${_libstdc}.nomachine
  done
  gzip -d "$srcdir/NX/etc/NX/server/packages/nxrunner.tar.gz"
  tar -rf "$srcdir/NX/etc/NX/server/packages/nxrunner.tar" NX/scripts/setup/nxrunner -C "$srcdir/NX/scripts/setup/nxrunner"
  tar --delete -f "$srcdir/NX/etc/NX/server/packages/nxrunner.tar" "NX/lib/"
  tar -rf "$srcdir/NX/etc/NX/server/packages/nxrunner.tar" "NX/lib/" -C "$srcdir/NX/lib/"
  gzip "$srcdir/NX/etc/NX/server/packages/nxrunner.tar"
  rm -fr "$srcdir/NX/scripts"*
  rm -fr "$srcdir/NX/lib"*
  #Change Automatic Service Start And/Or Firewall Automatic Rules If Apply
  if [ $_autoservice = y ] && [ $_autofirewall = y ]; then
    echo "##################################################################"
    echo "#Enabling Automatic Service Start And Firewall Automatic Rules#"
    echo "##################################################################"
  elif [ $_autoservice = y ] && [ $_autofirewall = n ]; then
    echo "#####################################"
    echo "#Enabling Automatic Service Start#"
    echo "#####################################"
    tar -zxf "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz" NX/etc/server-fedora.cfg.sample
    sed -i 's/#EnableFirewallConfiguration 1/EnableFirewallConfiguration 0/' NX/etc/server-fedora.cfg.sample
    gzip -d "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz"
    tar -rf "$srcdir/NX/etc/NX/server/packages/nxserver.tar" NX/etc/server-fedora.cfg.sample -C "$srcdir/NX/etc/server-fedora.cfg.sample"
    gzip "$srcdir/NX/etc/NX/server/packages/nxserver.tar"
    rm -fr "$srcdir/NX/etc/server-fedora.cfg.sample"
  elif [ $_autoservice = n ] && [ $_autofirewall = y ]; then
    echo "######################################"
    echo "#Enabling Firewall Automatic Rules#"
    echo "######################################"
    tar -zxf "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz" NX/etc/server-fedora.cfg.sample
    sed -i 's/#StartNXDaemon Automatic/StartNXDaemon Manual/' NX/etc/server-fedora.cfg.sample
    gzip -d "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz"
    tar -rf "$srcdir/NX/etc/NX/server/packages/nxserver.tar" NX/etc/server-fedora.cfg.sample -C "$srcdir/NX/etc/server-fedora.cfg.sample"
    gzip "$srcdir/NX/etc/NX/server/packages/nxserver.tar"
    rm -fr "$srcdir/NX/etc/server-fedora.cfg.sample"
  elif [ $_autoservice = n ] && [ $_autofirewall = n ]; then
    echo "###################################################################"
    echo "#Disabling Automatic Service Start And Firewall Automatic Rules#"
    echo "###################################################################"
    tar -zxf "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz" NX/etc/server-fedora.cfg.sample
    sed -i 's/#EnableFirewallConfiguration 1/EnableFirewallConfiguration 0/' NX/etc/server-fedora.cfg.sample
    sed -i 's/#StartNXDaemon Automatic/StartNXDaemon Manual/' NX/etc/server-fedora.cfg.sample
    gzip -d "$srcdir/NX/etc/NX/server/packages/nxserver.tar.gz"
    tar -rf "$srcdir/NX/etc/NX/server/packages/nxserver.tar" NX/etc/server-fedora.cfg.sample -C "$srcdir/NX/etc/server-fedora.cfg.sample"
    gzip "$srcdir/NX/etc/NX/server/packages/nxserver.tar"
    rm -fr "$srcdir/NX/etc/server-fedora.cfg.sample"
  fi
}

package() {
  cd "$srcdir"
  mkdir "$srcdir/NX/etc" -p
  install -d "$pkgdir/usr/"
  cp -a NX "$pkgdir/usr/NX"
}
