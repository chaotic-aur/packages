# Maintainer: imnotpolar
# Contributor: Ã©clairevoyant
# Contributor: ThatOneCalculator <kainoa at t1c dot dev>

_pkgname=hyprpicker
pkgname="$_pkgname-git"
pkgver=0.4.1.r1.4411a6d
pkgrel=1
pkgdesc="A wlroots-compatible Wayland color picker that does not suck"
arch=(x86_64)
url="https://github.com/hyprwm/$_pkgname"
license=('BSD-3-Clause')
depends=(cairo gcc-libs glibc libxkbcommon wayland)
makedepends=(
  cmake
  gdb
  git
  libgl
  libjpeg-turbo
  meson
  ninja
  pango
  wayland-protocols
  xorgproto
  pkg-config
)
optdepends=('wl-clipboard: --autocopy'
  'hyprutils: choose either hyprutils or hyprutils-git'
  'hyprutils-git: choose either hyprutils or hyprutils-git')
source=("git+$url.git")
provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")
b2sums=('SKIP')

pkgver() {
  git -C $_pkgname describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname
  cmake --build build
}

package() {
  install -vDm755 build/hyprpicker -t "$pkgdir/usr/bin/"

  cd $_pkgname
  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm644 doc/$_pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm644 README.md -t "$pkgdir/usr/share/doc/$_pkgname/"
}
