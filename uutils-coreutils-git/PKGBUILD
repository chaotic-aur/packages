# Maintainer: Sam <dev at samarthj dot com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

# shellcheck disable=2034,2148,2154

_pkgname='uutils-coreutils'
pkgname="${_pkgname}-git"
pkgver=r5937.e9d63519d
pkgrel=1
pkgdesc="GNU Coreutils rewritten in Rust"
arch=('x86_64')
url='https://github.com/uutils/coreutils'
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust' 'cargo' 'cmake' 'python-sphinx')
conflicts=('uutils-coreutils')
source=("git+https://github.com/uutils/coreutils.git")
sha512sums=('SKIP')

pkgver() {
  cd coreutils || exit 1
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd coreutils || exit 1
  make PROFILE=release
}

check() {
  cd coreutils || exit 1
  # TODO: Fix the failing tests. These 4 seem to fail for a few benign reasons.
  make test \
    TEST_NO_FAIL_FAST="--no-fail-fast -- --skip test_chown::test_chown_only_group --skip test_chown::test_chown_only_group_id --skip test_pinky::test_no_flag --skip test_who::test_lookup"
}

package() {
  cd coreutils || exit 1
  make install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    MANDIR=/share/man/man1 \
    PROG_PREFIX=uu- \
    PROFILE=release
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
