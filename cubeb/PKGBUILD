# Maintainer: Marco Rubin <marco.rubin@protonmail.com>

pkgbase=cubeb
pkgname=('cubeb' 'cubeb-docs')
pkgver=20241024
_commit=78b2bce70e0d1c21d3c175b72f322c50801b2e94
pkgrel=1
pkgdesc="Cross platform audio library"
arch=('aarch64' 'armv7h' 'i486' 'i686' 'pentium4' 'x86_64')
url="https://github.com/mozilla/cubeb"
license=('ISC')
makedepends=('alsa-lib' 'cmake' 'doxygen' 'git' 'jack' 'libpulse' 'sndio' 'speexdsp')
conflicts=("$pkgname-git")
source=(git+$url#commit=$_commit)
b2sums=('6b5aaa17848ecef649dde231bc7eba69a895116114343c57d9e60f0d5f9ab1ced409548fc860f1650a57b49f4c0334324d37cf8eb80b7b9ecc2ca429350e3161')

pkgver() {
  cd "$srcdir/$pkgname"
  git log -n1 --pretty='format:%cd' --date=format:'%Y%m%d' $_commit
}

prepare() {
  cd "$srcdir/$pkgname"
  # https://github.com/mozilla/cubeb/issues/736
  sed -i '/cubeb_add_test(logging)/d' CMakeLists.txt
}

build() {
  cd "$srcdir"
  cmake -B build -S "$pkgname" \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTS=OFF \
    -DBUNDLE_SPEEX=OFF \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SANITIZERS=OFF \
    -Wno-dev
  cmake --build build
}

package_cubeb() {
  optdepends+=(
    'alsa-lib: for ALSA backend'
    'jack: for JACK backend'
    'libpulse: for PulseAudio backend'
    'sndio: for sndio backend')
  depends+=('libspeexdsp.so' 'glibc' 'gcc-libs')
  provides+=("$pkgname=$pkgver" 'libcubeb.so')

  cd "$srcdir"
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -rf "$pkgdir/usr/share/doc"
}
package_cubeb-docs() {
  arch=('any')
  cd "$srcdir"
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir/cubeb/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -rf "$pkgdir/usr/lib" "$pkgdir/usr/bin" "$pkgdir/usr/include"
}
