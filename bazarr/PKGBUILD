# Maintainer: Donald Webster <fryfrog@gmail.com>
# Maintainer: txtsd <aur.archlinux@ihavea.quest>
# Contributor: Pieter Goetschalckx <3.14.e.ter@gmail.com>

pkgname=bazarr
pkgver=1.5.2
pkgrel=2
pkgdesc="Subtitle management and download automation for Sonarr and Radarr."
arch=('any')
url="https://www.bazarr.media"
license=('GPL-3.0-or-later')
depends=(
  bash
  ffmpeg
  gdk-pixbuf2
  python
  python-gevent
  python-gevent-websocket
  python-greenlet
  python-lxml
  python-magic
  python-numpy
  python-pillow
  python-platformdirs
  python-setuptools
  python-sqlalchemy
  python-tomli
  python-typing_extensions
  python-ujson
  python-webrtcvad
  sqlite
  unrar
)
makedepends=('unzip')
options=('!debug')
source=(
  "${pkgname}-${pkgver}.zip::https://github.com/morpheus65535/bazarr/releases/download/v${pkgver}/bazarr.zip"
  bazarr.service
  bazarr.sysusers
  bazarr.tmpfiles
  imghdr.py
)
noextract=("${pkgname}-${pkgver}.zip")
sha256sums=('63519d9855e5b84c947b18d72fa36dfa9341a040879d1079bfde2fabfe8ab30e'
  '5921db5ff3d3501cc8ff4a137d161b525fb91cce02369d5cc39ef6eb7b3a79fc'
  '73a60121fd2b7b8f5bad75ec4b0f92552fcae0e29a4b9e6aaf15f86a825a88a3'
  'e7055260d0f3554e8b628d9560d8e12a40f720d76542048df0dfc838db88357b'
  'ff4b65f15e63c66747d6ede9b9dd3d1fe3c9216e33ecfe5c6889c0a5f3e87165')

prepare() {
  unzip -qq -o -d "${pkgname}-${pkgver}" "${pkgname}-${pkgver}.zip"
}

package() {
  local _pyver="$(python -c 'import sys; print("%i.%i" % sys.version_info[:2])')"
  # local _pdir="${pkgdir}/usr/lib/python${_pyver}/site-packages/${pkgname}"
  local _pdir="${pkgdir}/opt/${pkgname}"

  mkdir -pm755 "${_pdir}"
  cp -dpr --no-preserve=ownership "${srcdir}/${pkgname}-${pkgver}/"* "${_pdir}"

  install -Dm644 "${srcdir}/imghdr.py" "${_pdir}/custom_libs/imghdr.py"

  rm -r "${_pdir}"/libs/sqlalchemy
  rm -r "${_pdir}"/libs/SQLAlchemy-*.dist-info
  rm -r "${_pdir}"/libs/typing_extensions.py
  rm -r "${_pdir}"/libs/typing_extensions-*.dist-info

  python -m compileall "${pkgdir}"

  install -Dm644 "${srcdir}/bazarr.service" "${pkgdir}/usr/lib/systemd/system/bazarr.service"
  install -Dm644 "${srcdir}/bazarr.sysusers" "${pkgdir}/usr/lib/sysusers.d/bazarr.conf"
  install -Dm644 "${srcdir}/bazarr.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/bazarr.conf"
}
