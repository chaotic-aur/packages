# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
# Contributor: Kevin Puertas <kevinpr@jkanetwork.com>
# Contributor: Eric Engestrom <arch.aur@engestrom.ch>
# Helper: paulequilibrio
pkgname=gdevelop-bin
_pkgname=GDevelop
pkgver=5.4.204
_electronversion=18
pkgrel=1
pkgdesc="A full-featured, no-code, open-source game development software."
arch=(
  'aarch64'
  'x86_64'
)
url='https://gdevelop.io/'
_ghurl="https://github.com/4ian/GDevelop"
license=('MIT')
provides=("${pkgname%-bin}=${pkgver}")
conflicts=("${pkgname%-bin}")
depends=(
  "electron${_electronversion}"
  'java-runtime'
)
makedepends=(
  'fuse2'
  'asar'
)
options=('!strip')
source=(
  "LICENSE-${pkgver}.md::https://raw.githubusercontent.com/4ian/GDevelop/v${pkgver}/LICENSE.md"
  "${pkgname%-bin}.sh"
)
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.AppImage::${_ghurl}/releases/download/v${pkgver}/${_pkgname}-5-${pkgver}-arm64.AppImage")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.AppImage::${_ghurl}/releases/download/v${pkgver}/${_pkgname}-5-${pkgver}.AppImage")
sha256sums=('0620d885ddbc88e952f99090d767de08671b6a81e5c10900ef5b949531460b92'
  'dc0c5ca385ad81a08315a91655c7c064b5bf110eada55e61265633ae198b39f8')
sha256sums_aarch64=('c3b5306925c29e2763707132c16318f6798d1f772a8aece6c3ee2076836de6f8')
sha256sums_x86_64=('e7f707158e4cbd68d52d149c525e675adab05100ab9fe0fc4a490d52c8fc8caa')
build() {
  sed -e "s|@electronversion@|${_electronversion}|g" \
    -e "s|@appname@|${pkgname%-bin}|g" \
    -e "s|@runname@|app.asar|g" \
    -e "s|@cfgdirname@|${_pkgname} 5|g" \
    -e "s|@options@||g" \
    -i "${srcdir}/${pkgname%-bin}.sh"
  chmod a+x "${srcdir}/${pkgname%-bin}-${pkgver}-${CARCH}.AppImage"
  "${srcdir}/${pkgname%-bin}-${pkgver}-${CARCH}.AppImage" --appimage-extract > /dev/null
  sed "s|AppRun --no-sandbox|${pkgname%-bin}|g" -i "${srcdir}/squashfs-root/${pkgname%-bin}.desktop"
  asar e "${srcdir}/squashfs-root/resources/app.asar" "${srcdir}/app.asar.unpacked"
  sed "s|if (isDev)|if (!isDev)|g;s|isDev,|\/\/isDev,|g;s|devTools,|\/\/devTools,|g" -i "${srcdir}/app.asar.unpacked/main.js"
  asar p "${srcdir}/app.asar.unpacked" "${srcdir}/app.asar"
  find "${srcdir}/squashfs-root/resources" -type d -exec chmod 755 {} \;
}
package() {
  install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
  install -Dm644 "${srcdir}/app.asar" -t "${pkgdir}/usr/lib/${pkgname%-bin}"
  cp -r "${srcdir}/squashfs-root/resources/"{app.asar.unpacked,GDJS,preview_node_modules} "${pkgdir}/usr/lib/${pkgname%-bin}"
  install -Dm644 "${srcdir}/squashfs-root/swiftshader/"* -t "${pkgdir}/usr/lib/${pkgname%-bin}/swiftshader"
  install -Dm644 "${srcdir}/squashfs-root/usr/lib/"* -t "${pkgdir}/usr/lib/${pkgname%-bin}/lib"
  for _icons in 16x16 32x32 48x48 64x64 128x128 256x256 512x512 1024x1024; do
    install -Dm644 "${srcdir}/squashfs-root/usr/share/icons/hicolor/${_icons}/apps/${pkgname%-bin}.png" \
      -t "${pkgdir}/usr/share/icons/hicolor/${_icons}/apps"
  done
  install -Dm644 "${srcdir}/squashfs-root/${pkgname%-bin}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/squashfs-root/LICENSE"* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
