# Maintainer: aur.chaotic.cx

_pkgname="lsfg-vk"
pkgname="$_pkgname-git"
pkgver=1.0.0.r124.g6d2ed64
pkgrel=1
pkgdesc="Lossless Scaling Frame Generation on Linux via Vulkan"
url="https://github.com/PancakeTAS/lsfg-vk"
license=('GPL-3.0-or-later')
arch=('x86_64')

depends=(
  'hicolor-icon-theme'
  'qt6-base'
  'qt6-declarative'
  'vulkan-icd-loader'
)
makedepends=(
  'cmake'
  'git'
  'ninja'
  'vulkan-headers'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

options=('!lto' '!strip' '!debug')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -Wno-dev

    -DLSFGVK_BUILD_VK_LAYER=ON
    -DLSFGVK_BUILD_UI=ON
    -DLSFGVK_BUILD_CLI=ON
    -DLSFGVK_INSTALL_DEVELOP=OFF
    -DLSFGVK_INSTALL_XDG_FILES=OFF
    -DLSFGVK_LAYER_LIBRARY_PATH="/usr/lib/liblsfg-vk-layer.so"
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_pkgsrc/lsfg-vk-ui/rsc/gay.pancake.lsfg-vk-ui.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/lsfg-vk-ui.png"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/lsfg-vk-ui.desktop" << END
[Desktop Entry]
Type=Application
Name=lsfg-vk Configuration
Comment=Configuration editor for lsfg-vk
Exec=lsfg-vk-ui %U
Icon=lsfg-vk-ui
Terminal=false
Categories=Settings;
Keywords=gaming;graphics;configuration;
StartupNotify=true
StartupWMClass=gay.pancake.lsfg-vk-ui
MimeType=application/x-lsfg-profile;
END
}
