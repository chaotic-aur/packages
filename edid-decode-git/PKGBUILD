# Maintainer: aur.chaotic.cx

## TODO: Drop when included in stable v4l-utils

# last functional commit
: ${_commit:=5332a3b76080e17137db15fd31e2d3949cef5f84}

_pkgname="edid-decode"
pkgname="$_pkgname-git"
pkgver=r720.5332a3b
pkgrel=2
pkgdesc="EDID decoder and conformance tester"
url="https://git.linuxtv.org/edid-decode.git"
license=('MIT')
arch=('x86_64')

depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'git'
  'meson'
)

provides=("$_pkgname")
conflicts=(
  "$_pkgname"
  'v4l-utils-git'
)

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  arch-meson "$_pkgsrc" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
