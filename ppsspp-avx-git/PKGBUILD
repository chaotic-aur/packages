# Maintainer: aur.chaotic.cx

## links:
# https://www.ppsspp.org/
# https://github.com/hrydgard/ppsspp

## options
: ${_build_ffmpeg:=true}

: ${_build_avx:=false}
: ${_build_git:=true}

unset _pkgtype
[[ "${_build_avx::1}" == "t" ]] && _pkgtype+='-avx'
[[ "${_build_git::1}" == "t" ]] && _pkgtype+='-git'

## basic info
_pkgname="ppsspp"
pkgname="${_pkgname}${_pkgtype:-}"
pkgver=1.18.1.r742.g5431bb3
pkgrel=1
pkgdesc='PSP emulator written in C++'
url="https://github.com/hrydgard/ppsspp"
arch=('x86_64')
license=('GPL-2.0-or-later')

depends=(
  glew
  libzip
  sdl2
  sdl2_ttf
  snappy
)
makedepends=(
  clang
  cmake
  git
  lld
  ninja
  python
)

provides=(
  "ppsspp=${pkgver%%.r*}"
  "ppsspp-assets=${pkgver%%.r*}"
)
conflicts=(
  "ppsspp"
  "ppsspp-assets"
)

options=(!lto)

_source_main() {
  _pkgsrc="ppsspp"
  source=(
    "git+https://github.com/hrydgard/ppsspp.git"
  )
  sha256sums=(
    'SKIP'
  )
}

_source_ppsspp() {
  source+=(
    'bylaws.libadrenotools'::'git+https://github.com/bylaws/libadrenotools.git'
    'discordapp.discord-rpc'::'git+https://github.com/discordapp/discord-rpc.git'
    'erkkah.naett'::'git+https://github.com/erkkah/naett.git'
    'facebook.zstd'::'git+https://github.com/facebook/zstd.git'
    'google.cpu_features'::'git+https://github.com/google/cpu_features.git'
    'hrydgard.minidx9'::'git+https://github.com/hrydgard/minidx9.git'
    'hrydgard.ppsspp-ffmpeg'::'git+https://github.com/hrydgard/ppsspp-ffmpeg.git'
    'hrydgard.ppsspp-freetype'::'git+https://github.com/hrydgard/ppsspp-freetype.git'
    'hrydgard.ppsspp-lua'::'git+https://github.com/hrydgard/ppsspp-lua.git'
    'hrydgard.ppsspp-mac-sdl'::'git+https://github.com/hrydgard/ppsspp-mac-sdl.git'
    'hrydgard.ppsspp-moltenvk'::'git+https://github.com/hrydgard/ppsspp-moltenvk.git'
    'hrydgard.pspautotests'::'git+https://github.com/hrydgard/pspautotests.git'
    'khronosgroup.glslang'::'git+https://github.com/KhronosGroup/glslang.git'
    'khronosgroup.openxr-sdk'::'git+https://github.com/KhronosGroup/OpenXR-SDK.git'
    'khronosgroup.spirv-cross'::'git+https://github.com/KhronosGroup/SPIRV-Cross.git'
    'kingcom.armips'::'git+https://github.com/Kingcom/armips.git'
    'libretro.libretro-common'::'git+https://github.com/libretro/libretro-common.git'
    'miniupnp'::'git+https://github.com/miniupnp/miniupnp.git'
    'retroachievements.rcheevos'::'git+https://github.com/RetroAchievements/rcheevos.git'
    'rtissera.libchdr'::'git+https://github.com/rtissera/libchdr.git'
    'tencent.rapidjson'::'git+https://github.com/Tencent/rapidjson.git'
    'unknownbrackets.ppsspp-debugger'::'git+https://github.com/unknownbrackets/ppsspp-debugger.git'
  )
  sha256sums+=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
  )

  if [[ "${_build_ffmpeg::1}" == "t" ]]; then
    source+=(
      'hrydgard.ppsspp-ffmpeg'::'git+https://github.com/hrydgard/ppsspp-ffmpeg.git'
    )
    sha256sums+=(
      'SKIP'
    )
  fi

  _prepare_ppsspp() (
    cd "$srcdir/$_pkgsrc"
    local _submodules=(
      'bylaws.libadrenotools'::'ext/libadrenotools'
      'discordapp.discord-rpc'::'ext/discord-rpc'
      'erkkah.naett'::'ext/naett'
      'facebook.zstd'::'ext/zstd'
      'google.cpu_features'::'ext/cpu_features'
      'hrydgard.minidx9'::'dx9sdk'
      'hrydgard.ppsspp-ffmpeg'::'ffmpeg'
      'hrydgard.ppsspp-freetype'::'ext/native/tools/prebuilt'
      'hrydgard.ppsspp-lua'::'ext/lua'
      'hrydgard.ppsspp-mac-sdl'::'SDL/macOS'
      'hrydgard.ppsspp-moltenvk'::'ios/MoltenVK'
      'hrydgard.pspautotests'::'pspautotests'
      'khronosgroup.glslang'::'ext/glslang'
      'khronosgroup.openxr-sdk'::'ext/OpenXR-SDK'
      'khronosgroup.spirv-cross'::'ext/SPIRV-Cross'
      'kingcom.armips'::'ext/armips'
      'libretro.libretro-common'::'libretro/libretro-common'
      'miniupnp'::'ext/miniupnp'
      'retroachievements.rcheevos'::'ext/rcheevos'
      'rtissera.libchdr'::'ext/libchdr'
      'tencent.rapidjson'::'ext/rapidjson'
      'unknownbrackets.ppsspp-debugger'::'assets/debugger'
    )

    if [[ "${_build_ffmpeg::1}" == "t" ]]; then
      _submodules+=(
        'hrydgard.ppsspp-ffmpeg'::'ffmpeg'
      )
    fi

    _submodule_update
  )
}

_source_bylaws_libadrenotools() {
  source+=(
    'bylaws.liblinkernsbypass'::'git+https://github.com/bylaws/liblinkernsbypass.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_bylaws_libadrenotools() (
    cd "$srcdir/$_pkgsrc"
    cd 'ext/libadrenotools'
    local _submodules=(
      'bylaws.liblinkernsbypass'::'lib/linkernsbypass'
    )
    _submodule_update
  )
}

_source_hrydgard_ppsspp_ffmpeg() {
  if [ "${_build_ffmpeg::1}" != "t" ]; then
    depends+=(ffmpeg)

    _prepare_hrydgard_ppsspp_ffmpeg() {
      :
    }

    return
  fi

  source+=(
    'ffmpeg.gas-preprocessor'::'git+https://github.com/FFmpeg/gas-preprocessor.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_hrydgard_ppsspp_ffmpeg() (
    cd "$srcdir/$_pkgsrc"
    cd 'ffmpeg'
    local _submodules=(
      'ffmpeg.gas-preprocessor'::'gas-preprocessor'
    )
    _submodule_update
  )
}

_source_kingcom_armips() {
  source+=(
    'kingcom.filesystem'::'git+https://github.com/Kingcom/filesystem.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_kingcom_armips() (
    cd "$srcdir/$_pkgsrc"
    cd 'ext/armips'
    local _submodules=(
      'kingcom.filesystem'::'ext/filesystem'
    )
    _submodule_update
  )
}

_source_tencent_rapidjson() {
  source+=(
    'google.googletest'::'git+https://github.com/google/googletest.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_tencent_rapidjson() (
    cd "$srcdir/$_pkgsrc"
    cd 'ext/rapidjson'
    local _submodules=(
      'google.googletest'::'thirdparty/gtest'
    )
    _submodule_update
  )
}

_source_main
_source_ppsspp

_source_bylaws_libadrenotools
_source_hrydgard_ppsspp_ffmpeg
_source_kingcom_armips
_source_tencent_rapidjson

pkgver() {
  cd ppsspp
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  _submodule_update() {
    local _module
    for _module in "${_submodules[@]}"; do
      git submodule init "${_module##*::}"
      git submodule set-url "${_module##*::}" "$srcdir/${_module%::*}"
      git -c protocol.file.allow=always submodule update "${_module##*::}"
    done
  }

  _prepare_ppsspp

  _prepare_bylaws_libadrenotools
  _prepare_hrydgard_ppsspp_ffmpeg
  _prepare_kingcom_armips
  _prepare_tencent_rapidjson
}

build() {
  if [[ "${_build_avx::1}" == "t" ]]; then
    export CFLAGS="$(echo "$CFLAGS" | sed -E 's@(\s*-(march|mtune)=\S+\s*)@ @g;s@\s*-O[0-9]\s*@ @g;s@\s+@ @g') -march=x86-64-v3 -mtune=generic -O3"
    export CXXFLAGS="$(echo "$CXXFLAGS" | sed -E 's@(\s*-(march|mtune)=\S+\s*)@ @g;s@\s*-O[0-9]\s*@ @g;s@\s+@ @g') -march=x86-64-v3 -mtune=generic -O3"
  fi

  export CC=clang
  export CXX=clang++
  export LDFLAGS+=" -Wl,-fuse-ld=lld"

  local _cmake_options=(
    -S "$_pkgsrc"
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_SKIP_RPATH=ON
    -DOpenGL_GL_PREFERENCE=GLVND
    -DUSE_SYSTEM_MINIUPNPC=OFF
    -DUSE_SYSTEM_LIBZIP=ON
    -DUSE_SYSTEM_SNAPPY=ON
    -DUSE_SYSTEM_ZSTD=ON

    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  if [[ "${_build_ffmpeg::1}" == "t" ]]; then
    _cmake_options+=(-DUSE_SYSTEM_FFMPEG=OFF)

    # Rebuild ffmpeg locally
    (
      cd ppsspp/ffmpeg
      ./linux_x86-64.sh
    )
  else
    _cmake_options+=(-DUSE_SYSTEM_FFMPEG=ON)
  fi

  local _cmake_sdl=(
    -B build-sdl
    -DHEADLESS=ON
    -DUSING_QT_UI=OFF
  )

  cmake "${_cmake_options[@]}" "${_cmake_sdl[@]}"
  cmake --build build-sdl
}

package() {
  depends+=(
    hicolor-icon-theme
  )

  local _assets="build-sdl"
  install -Dm755 "$_assets/PPSSPPSDL" -t "$pkgdir/usr/bin/"
  install -Dm755 "$_assets/PPSSPPHeadless" -t "$pkgdir/usr/bin/"

  ln -sf PPSSPPSDL "$pkgdir/usr/bin/ppsspp-sdl"
  ln -sf PPSSPPHeadless "$pkgdir/usr/bin/ppsspp-headless"

  install -dm755 "$pkgdir/usr/share/ppsspp"
  cp --reflink=auto -a "$_assets/assets" "$pkgdir/usr/share/ppsspp/"

  install -dm755 "$pkgdir/usr/share/icons"
  cp --reflink=auto -a "ppsspp/icons/hicolor" "$pkgdir/usr/share/icons/"

  install -Dm644 ppsspp/icons/icon-512.svg "$pkgdir/usr/share/pixmaps/ppsspp.svg"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/ppsspp-sdl.desktop" << END
[Desktop Entry]
Type=Application
Name=PPSSPP (SDL)
Comment=$pkgdesc
Exec=PPSSPPSDL %f
Icon=ppsspp
Terminal=false
StartupNotify=true
Categories=Game;
END

  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
