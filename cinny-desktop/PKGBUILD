# Maintainer: Jef Roosens

pkgname='cinny-desktop'
pkgver='4.2.3'
pkgrel='1'
pkgdesc='Matrix client focusing primarily on a simple, elegant and secure interface.'
arch=('x86_64')

url='https://cinny.in/'
license=('MIT')

# https://tauri.studio/v1/guides/getting-started/prerequisites#1-system-dependencies
depends=(
  'webkit2gtk'
  'openssl'
  'gtk3'
  'appmenu-gtk-module'
  'libappindicator-gtk3'
  'librsvg'
  'libvips'
  'sqlite'
  'gst-plugins-good'
)
makedepends=('rust' 'nodejs' 'npm')

# This makes sure dirty builds still pull down the newest version
source=("${pkgname}-${pkgver}.zip::https://github.com/cinnyapp/cinny-desktop/releases/download/v${pkgver}/cinny-desktop-v${pkgver}.zip")
sha256sums=('657a43e6cc9522d28b98e4f56ec4074cab4ddfdace2ef669956d1bba739b28d2')
sha512sums=('e86a6632335ba3f7ddb65d264f5d84bd4c7c6994cf326df2152d2f40701b117e51616b7e25d2a794827e08decc70a4f03c48f80e337f3bac04632661a29dd013')

install="${pkgname}.install"

build() {
  cd "${pkgname}"

  msg2 'Installing Node dependencies...'
  cd 'cinny' && npm --legacy-peer-deps ci
  cd .. && npm ci

  msg2 'Running Tauri build...'
  # With the default memory limit, the build fails due to having insufficient
  # memory available. 4GB seems to be enough for now.
  NODE_OPTIONS=--max_old_space_size=4096 npm run tauri build -- --bundles 'deb'
}

package() {
  ar x "${pkgname}/src-tauri/target/release/bundle/deb/cinny_${pkgver}_amd64.deb" 'data.tar.gz'
  tar xzf 'data.tar.gz' -C "${pkgdir}"
}

# vim: ft=bash
