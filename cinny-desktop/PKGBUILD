# Maintainer: Jef Roosens

pkgname='cinny-desktop'
pkgver='4.1.0'
pkgrel='1'
pkgdesc='Matrix client focusing primarily on a simple, elegant and secure interface.'
arch=('x86_64')

url='https://cinny.in/'
license=('MIT')

# https://tauri.studio/v1/guides/getting-started/prerequisites#1-system-dependencies
depends=(
  'webkit2gtk'
  'openssl'
  'gtk3'
  'appmenu-gtk-module'
  'libappindicator-gtk3'
  'librsvg'
  'libvips'
  'sqlite'
  'gst-plugins-good'
)
makedepends=('rust' 'nodejs' 'npm')

# This makes sure dirty builds still pull down the newest version
source=("${pkgname}-${pkgver}.zip::https://github.com/cinnyapp/cinny-desktop/releases/download/v${pkgver}/cinny-desktop-v${pkgver}.zip")
sha256sums=('422651d2b4097c362749b560ecbe4b13c833777eb8cdfe495b1ccc9f0be4dc43')
sha512sums=('8c1ab579dfcd088bec73ae0b34d3f7bb58c016c801d7720f61c7a7823b6d3f2aa950cc1e053b5ad151bac880ae9b95dc2ea164fe46c089f696754d75e9371150')

install="${pkgname}.install"

build() {
  cd "${pkgname}"

  msg2 'Installing Node dependencies...'
  cd 'cinny' && npm --legacy-peer-deps ci
  cd .. && npm ci

  msg2 'Running Tauri build...'
  # With the default memory limit, the build fails due to having insufficient
  # memory available. 4GB seems to be enough for now.
  NODE_OPTIONS=--max_old_space_size=4096 npm run tauri build -- --bundles 'deb'
}

package() {
  ar x "${pkgname}/src-tauri/target/release/bundle/deb/cinny_${pkgver}_amd64.deb" 'data.tar.gz'
  tar xzf 'data.tar.gz' -C "${pkgdir}"
}

# vim: ft=bash
