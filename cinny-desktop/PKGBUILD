# Maintainer: Jef Roosens

pkgname='cinny-desktop'
pkgver='4.2.0'
pkgrel='1'
pkgdesc='Matrix client focusing primarily on a simple, elegant and secure interface.'
arch=('x86_64')

url='https://cinny.in/'
license=('MIT')

# https://tauri.studio/v1/guides/getting-started/prerequisites#1-system-dependencies
depends=(
  'webkit2gtk'
  'openssl'
  'gtk3'
  'appmenu-gtk-module'
  'libappindicator-gtk3'
  'librsvg'
  'libvips'
  'sqlite'
  'gst-plugins-good'
)
makedepends=('rust' 'nodejs' 'npm')

# This makes sure dirty builds still pull down the newest version
source=("${pkgname}-${pkgver}.zip::https://github.com/cinnyapp/cinny-desktop/releases/download/v${pkgver}/cinny-desktop-v${pkgver}.zip")
sha256sums=('830a699e9bc7974592a4efe83636cd377906c29cf14e45e1606d4b9198ca9316')
sha512sums=('4cb1fbdac764b3869875933248698067e277ebe58e5c36d7924cf2c7d1db74524261047ca6d42d3df8bc782a8b44e3aeafe04d8be0ba5927f9acdb93fa313e8a')

install="${pkgname}.install"

build() {
  cd "${pkgname}"

  msg2 'Installing Node dependencies...'
  cd 'cinny' && npm --legacy-peer-deps ci
  cd .. && npm ci

  msg2 'Running Tauri build...'
  # With the default memory limit, the build fails due to having insufficient
  # memory available. 4GB seems to be enough for now.
  NODE_OPTIONS=--max_old_space_size=4096 npm run tauri build -- --bundles 'deb'
}

package() {
  ar x "${pkgname}/src-tauri/target/release/bundle/deb/cinny_${pkgver}_amd64.deb" 'data.tar.gz'
  tar xzf 'data.tar.gz' -C "${pkgdir}"
}

# vim: ft=bash
