# Maintainer: Jef Roosens

pkgname='cinny-desktop'
pkgver='4.0.3'
pkgrel='1'
pkgdesc='Matrix client focusing primarily on a simple, elegant and secure interface.'
arch=('x86_64')

url='https://cinny.in/'
license=('MIT')

# https://tauri.studio/v1/guides/getting-started/prerequisites#1-system-dependencies
depends=(
  'webkit2gtk'
  'openssl'
  'gtk3'
  'appmenu-gtk-module'
  'libappindicator-gtk3'
  'librsvg'
  'libvips'
  'sqlite'
  'gst-plugins-good'
)
makedepends=('rust' 'nodejs' 'npm')

# This makes sure dirty builds still pull down the newest version
source=("${pkgname}-${pkgver}.zip::https://github.com/cinnyapp/cinny-desktop/releases/download/v${pkgver}/cinny-desktop-v${pkgver}.zip")
sha256sums=('a80222cd95899724280f2d7b12d42a239a46f0eb53b82c28dca780aae89c9f37')
sha512sums=('8e757c9e68ac233e2369f03f64939af4680bd58efd9cf6c17e5a0568296ef805043de6b94cf4f54eec7a261ebd0fa36a8e57010966242d3946f1aec2723c7d4d')

install="${pkgname}.install"

build() {
  cd "${pkgname}"

  msg2 'Installing Node dependencies...'
  cd 'cinny' && npm --legacy-peer-deps ci
  cd .. && npm ci

  msg2 'Running Tauri build...'
  # With the default memory limit, the build fails due to having insufficient
  # memory available. 4GB seems to be enough for now.
  NODE_OPTIONS=--max_old_space_size=4096 npm run tauri build -- --bundles 'deb'
}

package() {
  ar x "${pkgname}/src-tauri/target/release/bundle/deb/cinny_${pkgver}_amd64.deb" 'data.tar.gz'
  tar xzf 'data.tar.gz' -C "${pkgdir}"
}

# vim: ft=bash
