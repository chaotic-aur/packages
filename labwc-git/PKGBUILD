# Maintainer: Lex Black <autumn-wind@web.de>

_pkgname=labwc
pkgname=labwc-git
pkgver=0.8.4.r55.gaff7cb92
pkgrel=1
pkgdesc='stacking wayland compositor with look and feel from openbox (git version)'
url="https://github.com/labwc/labwc"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libpng' 'librsvg' 'libsfdo' 'pango' 'seatd' 'ttf-font' 'wayland' 'xorg-xwayland')
depends+=('libdisplay-info.so' 'libinput' 'libliftoff.so' 'liblcms2.so' 'libvulkan.so' 'xcb-util-errors' 'xcb-util-renderutil' 'xcb-util-wm')
makedepends=('git' 'meson' 'scdoc' 'wayland-protocols')
makedepends+=('glslang' 'libxkbcommon' 'vulkan-headers' 'xorgproto') # temp deps for static wlroots
optdepends=("bemenu: default launcher via Alt+F3")
conflicts=(labwc)
provides=(labwc)
source=("git+https://github.com/labwc/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  meson subprojects download wlroots --sourcedir="$_pkgname"
}

build() {
  arch-meson -Dman-pages=enabled "$_pkgname" build --force-fallback-for=wlroots
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir" --skip-subprojects
}
