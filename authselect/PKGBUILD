# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>

pkgname=authselect
pkgver=1.5.0
pkgrel=1
pkgdesc="Tool to select system authentication and identity sources from a list of supported profiles."
arch=('i686' 'x86_64')
url="https://github.com/authselect/authselect"
license=('GPL3')
depends=('popt' 'asciidoc' 'libselinux')
provides=('authconfig')
conflicts=('authconfig')
makedepends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('11e8f1c3ecc4a3c30668a0cb644601f55c0473c47bbcd1e0d0e73d92721f19b0')

_srcdir="${pkgname}-${pkgver}"

prepare() {
  cd "${_srcdir}"
  autoreconf -i
}

build() {
  cd "${_srcdir}"
  ./configure --prefix=/usr --localstatedir=/var --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd "${_srcdir}"
  make DESTDIR="${pkgdir}" install
  install -Dpm644 'COPYING' -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
