# Maintainer: archcrack <johndoe.arch@outlook.com>

pkgname=clifm-git
_pkgname=clifm
pkgver=1.1.r346.gec633a5
pkgrel=1
pkgdesc="The command line file manager (development version)"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'aarch64' 'armv7h')
url="https://github.com/leo-arch/clifm"
license=(GPL2)
provides=('clifm')
conflicts=('clifm')
depends=('libcap' 'readline' 'acl' 'file')
makedepends=('git')
optdepends=(
  'archivemount: Archives mount'
  'atool: Archives/compression support'
  'p7zip: ISO 9660 support'
  'cdrtools: ISO 9660 support'
  'fzf: fzf mode for TAB completion'
  'udevil: (un)mount storage devices'
  'udisks2: (un)mount storage devices'
)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}" MANDIR=/usr/share/man PREFIX=/usr install
}
