# Maintainer: dawfukfr <dawfukfr@gmail.com>

_pkgname="aquamarine"
pkgname="${_pkgname}-git"
pkgver=0.3.0_r170.g131ed05
pkgrel=1
pkgdesc="Aquamarine is a very light linux rendering backend library "
arch=(any)
url="https://github.com/hyprwm/aquamarine"
license=('BSD-3-Clause')
depends=('pugixml' 'seatd' 'wayland-protocols' 'pixman' 'libdisplay-info' 'opengl-driver' 'hyprutils-git' 'libinput')
makedepends=('git' 'cmake' 'gcc' 'hyprwayland-scanner-git')
source=("${_pkgname}::git+https://github.com/hyprwm/aquamarine.git")
provides=("aquamarine")
conflicts=("aquamarine")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  _ver="$(cat VERSION)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake --no-warn-unused-cli -DCMAKE_BUILD_TYPE:STRING=Release -DCMAKE_INSTALL_PREFIX:PATH=/usr -S . -B ./build
  cmake --build ./build --config Release --target all -j$(nproc 2> /dev/null || getconf _NPROCESSORS_CONF)
}

package() {
  cd "${srcdir}/${_pkgname}"
  DESTDIR="${pkgdir}" cmake --install build

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
