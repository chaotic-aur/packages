# Maintainer: Kevin Stolp <kevinstolp@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Iacopo Isimbaldi <isiachi@rhye.it>

pkgname=zfs-dkms
pkgver=2.3.2
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
arch=('any')
url="https://zfsonlinux.org/"
license=('CDDL')
provides=("ZFS-MODULE=${pkgver}" "SPL-MODULE=${pkgver}")
# ambiguous, provided for backwards compat, pls don't use
provides+=('zfs')
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"{,.asc}
  "0001-only-build-the-module-in-dkms.conf.patch")
sha256sums=('80da628a9543ec3355bf410617450e167706948ceb287541455a1b8d87b8758a'
  'SKIP'
  '3039da5ecb6fe5f7dbaab8492f73d59bcafb8b22bd267f9d72a3ce5e70f82113')
b2sums=('8a89c62cbbeaf410db4011821cdd9959abef1782be7427b81ac47565407384fa3a381bef041dae73e97c2b2cefca62933180851901b3b1b86974ed33ad178a61'
  'SKIP'
  '26f28e841aafab9855791f2de45525ce26edd11a83d7e7bd7cc2f699bdba5dd8fd810827fba58365f655fdacd8a0f4f1febb1b339c17a7c4b3d498fb333ef6ac')
validpgpkeys=('4F3BA9AB6D1F8D683DC2DFB56AD860EED4598027' # Tony Hutter (GPG key for signing ZFS releases) <hutter2@llnl.gov>
  'C33DF142657ED1F7C328A2960AB9E991C6AF658B')            # Brian Behlendorf <behlendorf1@llnl.gov>

prepare() {
  cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

  patch -p1 -i ../0001-only-build-the-module-in-dkms.conf.patch

  # remove unneeded sections from module build
  sed -ri "/AC_CONFIG_FILES/,/]\)/{
/AC_CONFIG_FILES/n
/]\)/n
/^\s*(module\/.*|${pkgname%-dkms}.release|Makefile)/!d
}" configure.ac

  autoreconf -fi
}

build() {
  cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

  ./scripts/dkms.mkconf -n ${pkgname%-dkms} -v ${pkgver} -f dkms.conf
  ./scripts/make_gitrev.sh include/zfs_gitrev.h
}

package() {
  depends=("zfs-utils=${pkgver}" 'dkms')

  cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

  dkmsdir="${pkgdir}/usr/src/${pkgname%-dkms}-${pkgver}"
  install -d "${dkmsdir}"/{config,scripts}
  cp -a configure dkms.conf Makefile.in META ${pkgname%-dkms}_config.h.in ${pkgname%-dkms}.release.in include/ module/ "${dkmsdir}"/
  cp config/compile config/config.* config/missing config/*sh "${dkmsdir}"/config/
  cp scripts/dkms.postbuild "${dkmsdir}"/scripts/
}
