# Contributor: Michał Przybyś <michal@przybys.eu>
# Maintainer: Thomas Bork <sudoBash418@gmail.com>
pkgname=strawberry-qt5
pkgver=1.0.23
pkgrel=1
pkgdesc="A music player aimed at audio enthusiasts and music collectors (Qt5 version)"
arch=(x86_64)
url="https://strawberrymusicplayer.org/"
license=(GPL3)

depends=(qt5-base
  qt5-x11extras
  chromaprint
  protobuf
  gst-plugins-base
  gst-plugins-good
  udisks2
  libcdio
  libgpod
  libmtp
  fftw)
makedepends=(cmake
  boost
  qt5-tools)
optdepends=('gst-libav: additional codecs'
  'gst-plugins-bad: additional codecs'
  'gst-plugins-ugly: additional codecs')

provides=(strawberry)
conflicts=(strawberry)

source=("https://github.com/strawberrymusicplayer/strawberry/releases/download/${pkgver}/strawberry-${pkgver}.tar.xz")
sha256sums=('24f63064caf62c72330e9fa0808a1ca938339831d0a4e8385986280601a54f10')

build() {
  cmake -B build -S "strawberry-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT5=on \
    -DENABLE_VLC=off \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -DQT_NO_DEBUG_OUTPUT"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
