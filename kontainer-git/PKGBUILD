# Maintainer: aur.chaotic.cx

_pkgname="kontainer"
pkgname="$_pkgname-git"
pkgver=1.0.1.r139.g655cb82
pkgrel=1
pkgdesc="A KDE-based GUI to manage Distrobox containers"
url="https://invent.kde.org/system/kontainer"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'kcoreaddons'
  'ki18n'
  'kio'
)
makedepends=(
  'cmake'
  'extra-cmake-modules'
  'git'
  'ninja'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
