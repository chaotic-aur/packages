# Maintainer: aur.chaotic.cx

: ${_jdkver:=24}
: ${_branch:=archive_2_x}

_pkgname="recaf"
pkgname="${_pkgname}2-git"
pkgver=2.21.14.r1.g1d16a27
pkgrel=1
pkgdesc="A modern Java bytecode editor"
url="https://github.com/Col-E/Recaf"
license=("MIT")
arch=("any")

makedepends=(
  "java-environment=$_jdkver"
  "git"
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgsrc"
  ./mvnw clean package -Dmaven.test.skip -Dcheckstyle.skip
}

package() {
  depends=(
    "java-runtime"
    "ttf-font"
  )

  cd "$_pkgsrc"
  local _files=(target/$_pkgname-*-with-dependencies.jar)
  install -Dm755 "${_files[-1]}" "$pkgdir/usr/share/java/$_pkgname/$_pkgname.jar"

  install -Dm644 "src/main/resources/icons/logo.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$_pkgname" << END
#!/usr/bin/env bash
exec java -jar "/usr/share/java/$_pkgname/$_pkgname.jar" "\$@"
END

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" << END
[Desktop Entry]
Type=Application
Name=${_pkgname^}
Comment=$pkgdesc
Exec=$_pkgname
Icon=$_pkgname
Terminal=false
MimeType=application/java-archive
Categories=Development;Java
END
}
