# Maintainer: aur.chaotic.cx

_pkgname="freecad"
pkgname="$_pkgname-git"
pkgver=1.0.2.r5104.g280addd
pkgrel=1
pkgdesc="A general purpose 3D CAD modeler"
url="https://github.com/FreeCAD/FreeCAD"
license=('LGPL-2.1-or-later')
arch=('x86_64')

depends=(
  'coin'
  'hicolor-icon-theme'
  'libGL.so'                    # libglvnd
  'libboost_program_options.so' # boost-libs
  'libboost_thread.so'          # boost-libs
  'libfmt.so'                   # fmt
  'libfreetype.so'              # freetype2
  'libspnav'
  'libyaml-cpp.so' # yaml-cpp
  'libz.so'        # zlib
  'med'
  'opencascade'
  'pyside6'
  'python'
  'python-importlib-metadata'
  'python-lazy-loader'
  'python-matplotlib'
  'python-numpy'
  'python-packaging'
  'python-pip'
  'python-pivy'
  'python-ply'
  'python-requests'
  'python-typing_extensions'
  'python-yaml'
  'qt6-base'
  'qt6-svg'
  'qt6-tools'
  'shiboken6'
  'vtk'
  'xerces-c'
)
makedepends=(
  'boost'
  'cmake'
  'eigen'
  'git'
  'glew'
  'ninja'
  'nlohmann-json'
  'pybind11'
)
optdepends=(
  'openscad: OpenSCAD support'
  'graphviz: dependency graph support'
  'python-pip: support installing python dependencies for addons'
  'calculix-ccx: FEM solver backend'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  git submodule update --init --recursive --depth=1

  # remove cmake version check
  sed -E -e '/FATAL_ERROR/d' \
    -e '1i cmake_minimum_required(VERSION 4.0)' \
    -i CMakeLists.txt
}

pkgver() {
  cd "$_pkgsrc"
  local _tag _version _revision _hash
  _tag=$(git tag | grep -Ev '[A-Za-z]{2}' | sort -rV | head -1)
  _version="${_tag:?}"
  _revision=$(git rev-list --count --cherry-pick "$_tag"...HEAD)
  _hash=$(git rev-parse --short=7 HEAD)
  printf '%s.r%s.g%s' "${_version:?}" "${_revision:?}" "${_hash:?}"
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr/lib/freecad
    -DCMAKE_INSTALL_DATADIR=/usr/share/freecad
    -DCMAKE_INSTALL_DATAROOTDIR=/usr/share
    -DCMAKE_INSTALL_DOCDIR=/usr/share/freecad/doc
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
    -DBUILD_DESIGNER_PLUGIN=ON
    -DBUILD_FLAT_MESH=ON
    -DFREECAD_QT_VERSION=6
    -DFREECAD_USE_EXTERNAL_PIVY=ON
    -DFREECAD_USE_PCL=OFF
    -DFREECAD_USE_QT_FILEDIALOG=ON
    -DINSTALL_TO_SITEPACKAGES=ON
    -DENABLE_DEVELOPER_TESTS=OFF
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # tools
  install -Dm755 "$_pkgsrc"/src/Tools/fcinfo -t "$pkgdir/usr/bin/"

  # symlinks
  install -d "$pkgdir/usr/bin"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/freecad"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/FreeCAD"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/freecadcmd"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/FreeCADCmd"
}
