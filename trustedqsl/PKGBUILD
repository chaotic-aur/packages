# Maintainer: aur.chaotic.cx

_pkgname="trustedqsl"
pkgname="$_pkgname"
pkgver=2.7.4
pkgrel=2
pkgdesc="QSO log signing data for the ARRL Logbook of The World (LoTW)"
url="https://www.arrl.org/tqsl"
license=('LicenseRef-ARRL')
arch=('x86_64')

depends=(
  'wxgtk3'
)
makedepends=(
  'cmake'
  'desktop-file-utils'
  'libxxf86vm'
  'ninja'
)
optdepends=(
  'hamradio-menus'
)

_pkgsrc="tqsl-$pkgver"
_pkgext="tar.gz"
source=("$_pkgsrc.$_pkgext"::"http://www.arrl.org/tqsl/$_pkgsrc.$_pkgext")
sha256sums=('d9fb7226c82b804cfed927c8843515c2374fe3f34bbef02c61dc053413e84f82')

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config'
    -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc'
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$_pkgsrc/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  desktop-file-edit --set-key=KEY --set-value=VALUE --set-name=TrustedQSL "$pkgdir/usr/share/applications/org.arrl.trustedqsl.desktop"
}
