# Maintainer: aur.chaotic.cx

_pkgname="libadwaita-without-adwaita"
pkgname="$_pkgname"
pkgver=1.8.4
pkgrel=1
pkgdesc="Building blocks for modern adaptive GNOME applications - patched to respect system theme"
url="https://gitlab.gnome.org/GNOME/libadwaita"
arch=('x86_64')
license=('LGPL-2.1-or-later')

depends=(
  'appstream'
  'gtk4'
)
makedepends=(
  'git'
  'glib2-devel'
  'gobject-introspection'
  'meson'
  'sassc'
  'vala'
)

provides=(
  "libadwaita=1:$pkgver"
  "libadwaita-1.so"
)
conflicts=('libadwaita')

_pkgsrc="libadwaita"
source=(
  "$_pkgsrc"::"git+https://gitlab.gnome.org/GNOME/libadwaita.git#tag=$pkgver"
  '0001-respect_system_theme.patch'
)
sha256sums=(
  'SKIP'
  '74aaf5455f7b9990c53f68a968570a4555567d2c1a7fb58f79d71de6d74b3889'
)

prepare() {
  cd "$_pkgsrc"
  patch -Np1 -F100 -i ../0001-respect_system_theme.patch
}

build() {
  local meson_options=(
    -Dexamples=false
    -Dtests=false
  )

  arch-meson "$_pkgsrc" build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
