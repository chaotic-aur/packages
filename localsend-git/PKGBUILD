# Maintainer: aur.chaotic.cx

## options
: ${_install_path:=usr/lib}

: ${CARGO_HOME:=$SRCDEST/cargo-home}
: ${FVM_CACHE_PATH:=$SRCDEST/fvm-cache}
: ${RUSTUP_HOME:=$SRCDEST/rust-home}
export CARGO_HOME FVM_CACHE_PATH RUSTUP_HOME

#: ${_commit=574a54a} # last working

_pkgname="localsend"
pkgname="$_pkgname-git"
pkgver=1.17.0.r84.gc2792af
pkgrel=1
pkgdesc="An open source cross-platform alternative to AirDrop"
url="https://github.com/localsend/localsend"
license=('MIT')
arch=('x86_64')

depends=(
  'libayatana-appindicator'
)
makedepends=(
  'clang'
  'cmake'
  'fvm'
  'git'
  'lld'
  'llvm'
  'ninja'
  'patchelf'
  'rustup'
)

conflicts=("$_pkgname")
provides=("$_pkgname")

options=('!lto')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git${commit:+#commit=$_commit}")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  #sed -E 's&^(channel) = .*$&\1 = "stable"&' -i app/rust-toolchain.toml

  git -c user.name="x" -c user.email="a@b.c" \
    revert f9aa78366988a56baf1acb6083cf4e5807fe9e8b --no-edit
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() (
  export CXXFLAGS+=' -Wno-error=deprecated-declarations'

  cd "$_pkgsrc/app"
  fvm install

  fvm flutter --disable-analytics
  #fvm flutter pub upgrade --major-versions
  fvm flutter --no-version-check pub get
  fvm flutter build linux --no-pub --release
)

package() {
  cd "$_pkgsrc/app/build/linux/x64/release/bundle"

  # files
  install -Dm755 "localsend_app" "$pkgdir/$_install_path/$_pkgname/$_pkgname"
  cp -r lib/ "$pkgdir/$_install_path/$_pkgname/"
  cp -r data/ "$pkgdir/$_install_path/$_pkgname/"

  # runpath
  patchelf --set-rpath '$ORIGIN/lib' "$pkgdir/$_install_path/$_pkgname/$_pkgname"
  for i in "$pkgdir/$_install_path/$_pkgname/lib"/*.so; do
    [ -z "$(patchelf --print-rpath "$i")" ] && continue
    patchelf --set-rpath '$ORIGIN' "$i"
  done

  # symlink
  install -dm755 "${pkgdir}/usr/bin"
  ln -sfr "$pkgdir/$_install_path/$_pkgname/$_pkgname" "$pkgdir/usr/bin/${_pkgname}"

  # icon
  install -Dm644 "$srcdir/$_pkgsrc/app/build/flutter_assets/assets/img/logo-512.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  # launcher
  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" << END
[Desktop Entry]
Type=Application
Name=LocalSend
Comment=$pkgdesc
Exec=$_pkgname
Icon=$_pkgname
Terminal=false
Categories=Utility;Network;
END

  # license
  install -Dm644 "$srcdir/$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

  # permissions
  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
