# Maintainer:

## options

: ${_install_path:=opt}

# basic info
_pkgname="localsend"
pkgname="$_pkgname-git"
pkgver=1.14.0.r29.g4f040e6
pkgrel=1
pkgdesc='An open source cross-platform alternative to AirDrop'
url="https://github.com/localsend/localsend"
license=('MIT')
arch=('x86_64')

depends=(
  'libayatana-appindicator'
)
makedepends=(
  'clang'
  'cmake'
  'flutter-bin'
  'git'
  'lld'
  'llvm'
  'ninja'
)

conflicts=('localsend')
provides=('localsend')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgsrc/app"

  flutter pub add path:^1.9.0
  flutter pub add wechat_assets_picker:^9.0.3
  # flutter upgrade
  flutter pub run build_runner build --delete-conflicting-outputs
  flutter pub get
  flutter build linux
}

package() {
  cd "$_pkgsrc/app/build/linux/x64/release/bundle"

  # app files
  install -Dm755 "localsend_app" "$pkgdir/$_install_path/$_pkgname/$_pkgname"
  cp --reflink=auto -r lib/ "$pkgdir/$_install_path/$_pkgname/"
  cp --reflink=auto -r data/ "$pkgdir/$_install_path/$_pkgname/"

  # symlink
  install -dm755 "${pkgdir}/usr/bin"
  ln -sfr "$pkgdir/$_install_path/$_pkgname/$_pkgname" "$pkgdir/usr/bin/${_pkgname}"

  # icon
  install -Dm644 "$srcdir/$_pkgname/app/build/flutter_assets/assets/img/logo-512.png" "$pkgdir/usr/share/pixmaps/localsend.png"

  # .desktop file
  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" <<END
[Desktop Entry]
Type=Application
Comment=An open source cross-platform alternative to AirDrop 
Name=localsend
Exec=localsend
Icon=localsend
Terminal=false
Hidden=false
END

  #license
  install -Dm644 "$srcdir/$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/"
}
