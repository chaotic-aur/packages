# SPDX-License-Identifier: 0BSD
# Maintainer: Maciej Szeptuch <neverous at neverous dot info>

pkgbase=efibooteditor
pkgname=(efibooteditor efibooteditor-qt5)
pkgver=1.5.2
pkgrel=3
pkgdesc="Boot Editor for (U)EFI based systems."
arch=(x86_64 aarch64 armv7h)
url="https://github.com/Neverous/efibooteditor"
license=(LGPL3)
depends=(efivar zlib qt6-base qt5-base)
makedepends=(cmake git qt6-tools qt5-tools)
source=("${pkgbase}::git+${url}.git?signed#tag=v${pkgver}")
b2sums=("ecb60d4f3bfa7d45204b4c0f9c768b001d316c1b1b701743949d86c35dfdda413aaaf84ea7ac1ff7282468c5e5074d3ef6521e7cf5878ae92ccf831c420d4c58")
validpgpkeys=(
  "9E76A390AAF9772C8448DCCEE4F948F71FEE7375" # Maciej Szeptuch <neverous at neverous dot info>
)

build() {
  cd ${pkgbase} || exit

  export BUILD_VERSION=${pkgver}-aur.${pkgrel}

  # Build Qt6 version
  cmake -B build-qt6 . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6

  cmake --build build-qt6 --config Release

  # Build Qt5 version
  cmake -B build-qt5 . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=5

  cmake --build build-qt5 --config Release
}

package_efibooteditor() {
  pkgdesc="Boot Editor for (U)EFI based systems."
  depends=(efivar zlib qt6-base)

  cd ${pkgbase} || exit

  DESTDIR="${pkgdir}" cmake --install build-qt6
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgbase}/LICENSE
}

package_efibooteditor-qt5() {
  pkgdesc="Boot Editor for (U)EFI based systems (Qt5 version)."
  depends=(efivar zlib qt5-base)
  provides=(efibooteditor)
  conflicts=(efibooteditor)

  cd ${pkgbase} || exit

  DESTDIR="${pkgdir}" cmake --install build-qt5
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgbase}/LICENSE
}
