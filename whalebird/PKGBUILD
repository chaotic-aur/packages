# Maintainer: Akira Fukushima <h3.poteto@gmail.com>
pkgname=whalebird
pkgver=6.2.2
pkgrel=1
pkgdesc="Single-column Fediverse client for desktop "
arch=('x86_64')
url="https://whalebird.social"
license=('GPL-3.0')
depends=('electron34>=34.0.0' 'electron34<35.0.0')
makedepends=('yarn' 'tar' 'nodejs>=22.0.0' 'node-gyp' 'git')
provides=('whalebird')
conflicts=('whalebird-bin' 'whalebird-desktop-git')
source=("https://github.com/h3poteto/whalebird-desktop/archive/refs/tags/v$pkgver.tar.gz"
  whalebird.desktop
  electron-builder.yml)
md5sums=('91f0ea79e4e52f62adfb8e58fc93d87d'
  '6ab525c54c6e91871d0d6f05a76f5289'
  '480286196dc0614fd14b0d906c00b228')

prepare() {
  cd "whalebird-desktop-${pkgver}"
  rm -f electron-builder.yml
  rm .tool-versions
  _electronVersion="$(< /usr/lib/electron34/version)"
  yarn up "electron@$_electronVersion"
}

build() {
  cp electron-builder.yml "whalebird-desktop-${pkgver}"/electron-builder.yml
  cd "whalebird-desktop-${pkgver}"
  yarn exec nextron build --no-pack
  yarn exec electron-builder --linux --dir --config electron-builder.yml -c.electronDist=/usr/lib/electron34 -c.electronVersion="$_electronVersion"
}

package() {
  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share"
  mv "whalebird-desktop-${pkgver}/dist/linux-unpacked" "${pkgdir}/usr/share/${pkgname}"
  ln -sf "/usr/share/${pkgname}/whalebird" "${pkgdir}/usr/bin/whalebird"

  for i in 16 32 128 256 512; do
    install -Dm 644 "whalebird-desktop-${pkgver}/resources/icons/icon.iconset/icon_${i}x${i}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/whalebird.png"
  done
  install -Dm 644 "whalebird-desktop-${pkgver}/resources/icons/icon.iconset/icon_32x32@2x.png" \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/whalebird.png"

  install -Dm644 -t "${pkgdir}/usr/share/applications" whalebird.desktop
  install -Dm 644 "whalebird-desktop-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
