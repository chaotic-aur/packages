# Maintainer: mooow <9mvnyapkt@mozmail.com>
# Thanks: Darren Wu <$(base64 --decode <<<'ZGFycmVuMTk5NzA4MTBAZ21haWwuY29tCg==')>
# Thanks: Jared Casper <jaredcasper@gmail.com>
# Thanks: Kyle Keen <keenerd@gmail.com>
# Thanks: Markus Koch <CClassicVideos@aol.com>
# Thanks: Thomas Dziedzic < gostrc at gmail >

pkgname=gtkwave-gtk3-tcl
pkgver=3.3.126
pkgrel=1
pkgdesc="A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files (GTK+ 3 version, with Tcl/Tk support)"
arch=('x86_64')
url="http://gtkwave.sourceforge.net"
license=('GPL-2.0-or-later AND MIT')
depends=('bzip2' 'xz' 'gtk3' 'dconf' # Original gtkwave dependencies
  'tcl' 'tk'
)
makedepends=('gperf')
conflicts=(gtkwave)
provides=(gtkwave)
install='gtkwave.install'
source=("${url}/gtkwave-gtk3-${pkgver}.tar.gz")
sha256sums=('965019d74829746b5d1d382a9cf1e272246c91101d0e131f0da512c94ba5816a')

build() {
  cd "gtkwave-gtk3-${pkgver}/"
  ./configure \
    --prefix=/usr \
    --enable-tcl \
    --disable-mime-update \
    --with-gsettings \
    --enable-gtk3

  make
}

package() {
  cd "gtkwave-gtk3-${pkgver}/"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.TXT -t "${pkgdir}/usr/share/licenses/gtkwave/"
}
