# Maintainer: aur.chaotic.cx

_pkgname="zenergy"
pkgname="$_pkgname-dkms-git"
pkgver=r31.f77293f
pkgrel=3
pkgdesc="Linux kernel driver for reading RAPL registers for AMD Zen CPUs"
url="https://github.com/boukehaarsma23/zenergy"
license=('GPL-2.0-only')
arch=('x86_64')

depends=(
  'dkms'
)
makedepends=(
  'git'
)

provides=("$_pkgname-dkms")
conflicts=("$_pkgname-dkms")

_pkgsrc="$_pkgname"
source=(
  "$_pkgsrc"::"git+$url.git"
  "PR18-linux-6.16.patch"::"https://github.com/BoukeHaarsma23/zenergy/pull/18.patch"
)
sha256sums=(
  'SKIP'
  '693d75019a890fbe6149803319869cc47658e72a25292b4aa546d411e1f1e09e'
)

pkgver() {
  cd "$_pkgsrc"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "$_pkgsrc"
  patch -Np1 -F100 -i "$srcdir/PR18-linux-6.16.patch"
  sed -e "s/@VERSION@/$pkgver/" -i dkms.conf
}

package() {
  install -Dm644 "$_pkgsrc"/{dkms.conf,Makefile,zenergy.c} -t "$pkgdir/usr/src/$_pkgname-$pkgver/"
}
