# Maintainer: Nixuge
# Co-Maintainer: Antti <antti@antti.codes>

pkgname=lunar-client
_pkgname=lunarclient
pkgver=3.3.4
pkgrel=1
pkgdesc='PvP modpack for all modern versions of Minecraft'
url=https://lunarclient.com
arch=(x86_64)
license=(unknown)
depends=(fuse xorg-xrandr)
options=(!strip !debug)
# For some reason lunar added "ow" to the filename, may or may not change, check on the next update.
_appimage="Lunar%20Client-${pkgver}-ow.AppImage"
source=("https://launcherupdates.lunarclientcdn.com/${_appimage}")
sha256sums=('a5ba8010a26fe9357aceefa32cb2a6386d309e1ab7565967965b14406eb27e86')

prepare() {
  chmod +x "${_appimage}"
  ./"${_appimage}" --appimage-extract
}

build() {
  # Adjust .desktop so it will work outside of AppImage container
  sed -i -E \
    "s|Exec=AppRun|Exec=env DESKTOPINTEGRATION=false /usr/bin/${_pkgname}|" \
    "squashfs-root/lunarclient.desktop"
  # Fix permissions; .AppImage permissions are 700 for all directories
  chmod -R a-x+rX squashfs-root/usr
}

package() {
  # AppImage
  install -Dm755 \
    "${srcdir}/${_appimage}" \
    "${pkgdir}/opt/${_pkgname}/${_pkgname}.AppImage"

  # Desktop file
  install -Dm644 \
    "${srcdir}/squashfs-root/lunarclient.desktop" \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

  # Icon images
  install -dm755 "${pkgdir}/usr/share/"
  cp -a \
    "${srcdir}/squashfs-root/usr/share/icons" \
    "${pkgdir}/usr/share/"

  # Symlink executable
  install -dm755 "${pkgdir}/usr/bin"
  ln -s \
    "/opt/${_pkgname}/${_pkgname}.AppImage" \
    "${pkgdir}/usr/bin/${_pkgname}"
}
# vim:set ts=4 sw=4 noet:
