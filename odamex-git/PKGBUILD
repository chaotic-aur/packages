# Maintainer: aur.chaotic.cx

_pkgname="odamex"
pkgname="$_pkgname-git"
pkgver=11.0.0.r257.g90428368c
pkgrel=1
pkgdesc='A free client/server multiplayer engine for the classic FPS Doom'
url="https://github.com/odamex/odamex"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'curl'
  'libpng'
  'portmidi'
  'sdl2'
  'sdl2_mixer'
  'zlib'
)
makedepends=(
  'cmake'
  'deutex'
  'git'
  'ninja'
)
optdepends=(
  'timidity++: Required for the SDL2 MIDI music backend'
  'doomseeker: Online Doom multiplayer server launcher'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git#branch=stable")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  git submodule update --init --depth 1 --recursive
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  export CFLAGS CXXFLAGS
  CFLAGS+=" -DSDL20"
  CXXFLAGS+=" -DSDL20"

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DBUILD_LAUNCHER=True
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
