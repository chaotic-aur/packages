# Maintainer: aur.chaotic.cx

_pkgname="odamex"
pkgname="$_pkgname-git"
pkgver=11.2.0.r236.gde58a1d
pkgrel=2
pkgdesc='A free client/server multiplayer engine for the classic FPS Doom'
url="https://github.com/odamex/odamex"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'libcurl.so'        # curl
  'libfltk.so'        # fltk
  'libfltk_images.so' # fltk
  'libjsoncpp.so'     # jsoncpp
  'libminiupnpc.so'   # miniupnpc
  'libpng16.so'       # libpng
  'libportmidi.so'    # portmidi
  'libz.so'           # zlib
  'libzstd.so'        # zstd
  'sdl2'
  'sdl2_mixer'
  'wxwidgets-gtk3'
)
makedepends=(
  'cmake'
  'deutex' # AUR
  'git'
  'ninja'
)
optdepends=(
  'timidity++: Required for the SDL2 MIDI music backend'
  'doomseeker: Online Doom multiplayer server launcher'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git#branch=stable")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  git rm -r libraries/curl
  git rm -r libraries/fltk
  git rm -r libraries/jsoncpp
  git rm -r libraries/libpng
  git rm -r libraries/miniupnp
  git rm -r libraries/portmidi
  git rm -r libraries/zlib
  git submodule update --init --depth 1 --recursive
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  export CFLAGS CXXFLAGS
  CFLAGS+=" -DSDL20"
  CXXFLAGS+=" -DSDL20"

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DBUILD_TESTING=OFF
    -Wno-dev

    -DBUILD_OR_FAIL=ON
    -DUSE_LTO=OFF

    -DENABLE_PORTMIDI=ON

    -DUSE_INTERNAL_CPPTRACE=ON
    -DUSE_INTERNAL_CURL=OFF
    -DUSE_INTERNAL_FLTK=OFF
    -DUSE_INTERNAL_JSONCPP=OFF
    -DUSE_INTERNAL_LIBADLMIDI=ON
    -DUSE_INTERNAL_PNG=OFF
    -DUSE_INTERNAL_WXWIDGETS=OFF
    -DUSE_INTERNAL_ZLIB=OFF
    -DUSE_INTERNAL_ZSTD=OFF

    -DUSE_MINIUPNP=ON
    -DUSE_INTERNAL_MINIUPNP=OFF
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
