# Maintainer: Jan Cholasta <grubber at grubber cz>

pkgname=slade
pkgver=3.2.5
pkgrel=1
pkgdesc='SLADE3 Doom editor'
arch=('i686' 'x86_64')
url='http://slade.mancubus.net/'
license=('GPL')
depends=('bzip2'
  'fluidsynth>=2.2'
  'freeimage'
  'ftgl'
  'glu'
  'gtk3'
  'libgl'
  'lua'
  'mpg123'
  'sfml>=2.5'
  'webkit2gtk'
  'wxwidgets-gtk3>=3.2'
  'zlib')
makedepends=('cmake'
  'p7zip')
source=("https://github.com/sirjuddington/SLADE/archive/${pkgver}.tar.gz")
sha256sums=('7af0f63be12af561e3e5c2899b21c471415fce4f2a1f2c0c82b0ffeb20e692cf')

build() {
  cd SLADE-${pkgver}

  export CCACHE_SLOPPINESS=pch_defines,time_macros
  cmake -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D BUILD_PK3=ON \
    .
  make
}

package() {
  cd SLADE-${pkgver}

  make install DESTDIR="$pkgdir"
}
