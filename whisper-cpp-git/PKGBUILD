# Maintainer: aur.chaotic.cx

_pkgname="whisper-cpp"
pkgname="$_pkgname-git"
pkgver=1.7.5.r285.g1f5fdbe
pkgrel=2
pkgdesc="Port of OpenAI's Whisper model in C/C++"
url="https://github.com/ggml-org/whisper.cpp"
license=('MIT')
arch=('x86_64')

depends=(
  'libggml'
)
makedepends=(
  'cmake'
  'git'
  'ninja'
)

provides=(
  "$_pkgname"
  "${_pkgname//-/.}"
)
conflicts=(
  "$_pkgname"
  "${_pkgname//-/.}"
)

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() (
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DWHISPER_USE_SYSTEM_GGML=ON
    -DBUILD_SHARED_LIBS=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
)

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

  # unwanted files
  rm -rf "$pkgdir/usr"/{include,lib}
}
