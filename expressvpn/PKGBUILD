# Maintainer: Kyle Manna <kyle(at)kylemanna(dot)com>
# Co-Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=expressvpn
pkgver=3.76.0.4_1
pkgrel=1
pkgdesc="Proprietary VPN client for Linux"
arch=('x86_64' 'i686' 'armv7h')
depends=('glibc')
url="https://expressvpn.com"
license=('LicenseRef-custom')
options=(!strip)
install=expressvpn.install
_url="https://www.expressvpn.works/clients/linux"
source_x86_64=("${_url}/${pkgname}_${pkgver/_/-}_amd64.deb"{,.asc})
source_i686=("${_url}/${pkgname}_${pkgver/_/-}_i386.deb"{,.asc})
source_armv7h=("${_url}/${pkgname}_${pkgver/_/-}_armhf.deb"{,.asc})

sha512sums_x86_64=('eaa74288e31e833072a4b18593006448283ff1f6843c5a562af580f098c70db002b55e3146e1afca73f6eab6d0a42ad5ece3134e004b6163e73230686f7048e1'
  'SKIP')
sha512sums_i686=('d8bac9412fa96e655c21a994bde2b305612acabd0ba881634b575e9a2814619847fd272ea4da3397aa3cc430571ddb94e58a9f65f592b2405ce9485a6f05abb3'
  'SKIP')
sha512sums_armv7h=('a44c0c2f5c0f110f30f752f7b7799ab7fa962ddb8cf54ba114fe7f4d631853f29050169a8484032c0dee43d176f0065436ecb8348cb81818d08f535ca707f681'
  'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  # /usr/sbin is a symlink to /usr/bin, rewrite it. Upstream also install files to both /lib and /usr/lib
  # merge these and move to correct location
  bsdtar -C "${pkgdir}" -xf "${srcdir}/data.tar.gz" -s ":/usr/sbin:/usr/bin:" -s ":/usr/lib:/lib:"
  mv "${pkgdir}/lib" "${pkgdir}/usr/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/expressvpn/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  install -dm755 "${pkgdir}/var/lib/expressvpn/certs"

  # Remove unused /etc
  rm -r "$pkgdir/etc/"
}
