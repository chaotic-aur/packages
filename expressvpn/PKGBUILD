# Maintainer: Kyle Manna <kyle(at)kylemanna(dot)com>
# Co-Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=expressvpn
pkgver=3.79.0.0_1
pkgrel=1
pkgdesc="Proprietary VPN client for Linux"
arch=('x86_64' 'i686' 'armv7h')
depends=('glibc')
url="https://expressvpn.com"
license=('LicenseRef-custom')
options=(!strip)
install=expressvpn.install
_url="https://www.expressvpn.works/clients/linux"
source_x86_64=("${_url}/${pkgname}_${pkgver/_/-}_amd64.deb"{,.asc})
source_i686=("${_url}/${pkgname}_${pkgver/_/-}_i386.deb"{,.asc})
source_armv7h=("${_url}/${pkgname}_${pkgver/_/-}_armhf.deb"{,.asc})

sha512sums_x86_64=('a6ae15d7d4a409dfd84a4b3be01514495a435fefd315b400874f896d546db1cdf8111f22dc565285acfa82e199608e9f3570bc797740799b8ba6d581ce0285d7'
  'SKIP')
sha512sums_i686=('ac750ec33e8b464e7ecf83bb24471fcaf8f630350e4df2765287d0a19d6b3cd1a676bfc8cc956177170075140396eff236b04c58e4c4fb6fa1a3e4e3f2ba6ebe'
  'SKIP')
sha512sums_armv7h=('e4f8e888bdf414505543256862310d76c2d0f5f79ebf9dce97142557973d6301c1d9fc6f8e6eebc15e9563103d03a0c8d88d2c93f4b2b4c841e0113d287d9843'
  'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  # /usr/sbin is a symlink to /usr/bin, rewrite it. Upstream also install files to both /lib and /usr/lib
  # merge these and move to correct location
  bsdtar -C "${pkgdir}" -xf "${srcdir}/data.tar.gz" -s ":/usr/sbin:/usr/bin:" -s ":/usr/lib:/lib:"
  mv "${pkgdir}/lib" "${pkgdir}/usr/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/expressvpn/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  install -dm755 "${pkgdir}/var/lib/expressvpn/certs"

  # Remove unused /etc
  rm -r "$pkgdir/etc/"
}
