# Maintainer: Kyle Manna <kyle(at)kylemanna(dot)com>
# Co-Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=expressvpn
pkgver=3.81.0.2_1
pkgrel=1
pkgdesc="Proprietary VPN client for Linux"
arch=('x86_64' 'i686' 'armv7h')
depends=('glibc')
url="https://expressvpn.com"
license=('LicenseRef-custom')
options=(!strip)
install=expressvpn.install
_url="https://www.expressvpn.works/clients/linux"
source_x86_64=("${_url}/${pkgname}_${pkgver/_/-}_amd64.deb"{,.asc})
source_i686=("${_url}/${pkgname}_${pkgver/_/-}_i386.deb"{,.asc})
source_armv7h=("${_url}/${pkgname}_${pkgver/_/-}_armhf.deb"{,.asc})

sha512sums_x86_64=('5b94fe474d534c289d1794e4ee0449fc5e44833ea241640dff14a10f74e686ddbbdbc9ee83872ed2a122c82a7709eb591d5ecca57484a46c11b05b74e4283cb5'
  'SKIP')
sha512sums_i686=('7eafb3740efd23cd081f2af321435a7abbe16c334665520c46d4fb2c5804be7c81cc9f4dcd2ca75d9d8f501f0e89240f32017b143fb6d904d2f40007a6c1052c'
  'SKIP')
sha512sums_armv7h=('ab5f9d7e5f23077e3233842983df727b3a41e23ec43896d9e6f89187e3e9473b609ab5cd26d74569e06af2990d611b6c56e0f0239df8d1f43b5216724b46b0bd'
  'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  # /usr/sbin is a symlink to /usr/bin, rewrite it. Upstream also install files to both /lib and /usr/lib
  # merge these and move to correct location
  bsdtar -C "${pkgdir}" -xf "${srcdir}/data.tar.gz" -s ":/usr/sbin:/usr/bin:" -s ":/usr/lib:/lib:"
  mv "${pkgdir}/lib" "${pkgdir}/usr/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/expressvpn/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  install -dm755 "${pkgdir}/var/lib/expressvpn/certs"

  # Remove unused /etc
  rm -r "$pkgdir/etc/"
}
