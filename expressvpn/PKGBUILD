# Maintainer: Kyle Manna <kyle(at)kylemanna(dot)com>
# Co-Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=expressvpn
pkgver=3.75.0.0_1
pkgrel=1
pkgdesc="Proprietary VPN client for Linux"
arch=('x86_64' 'i686' 'armv7h')
depends=('glibc')
url="https://expressvpn.com"
license=('LicenseRef-custom')
options=(!strip)
install=expressvpn.install
_url="https://www.expressvpn.works/clients/linux"
source_x86_64=("${_url}/${pkgname}_${pkgver/_/-}_amd64.deb"{,.asc})
source_i686=("${_url}/${pkgname}_${pkgver/_/-}_i386.deb"{,.asc})
source_armv7h=("${_url}/${pkgname}_${pkgver/_/-}_armhf.deb"{,.asc})

sha512sums_x86_64=('1c0613c46fb51023bd68db4e6058b69976e3173fa3faa68df59794fb5d22dc399e7978d90606dc6f7d5225bbab9e62e85e9a86d48ad41bd3a490c65cb3689be5'
  'SKIP')
sha512sums_i686=('f9f01566006f5e0ed3b13dcc99e08a74f12a2ce7f035a894acf460284f902b0c4b6516836804c330db79d31910994b48a141096647e6af97b14f816615c9a86b'
  'SKIP')
sha512sums_armv7h=('8d10cd7cb2d5c46f436beafce95bfaa59d685c7f142cec7b57cb827fd100256223a525a416e96b40fda42eb587f1373ca2021a3a9f74a00b51b0c51d6ab0061b'
  'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  # /usr/sbin is a symlink to /usr/bin, rewrite it. Upstream also install files to both /lib and /usr/lib
  # merge these and move to correct location
  bsdtar -C "${pkgdir}" -xf "${srcdir}/data.tar.gz" -s ":/usr/sbin:/usr/bin:" -s ":/usr/lib:/lib:"
  mv "${pkgdir}/lib" "${pkgdir}/usr/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/expressvpn/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  install -dm755 "${pkgdir}/var/lib/expressvpn/certs"

  # Remove unused /etc
  rm -r "$pkgdir/etc/"
}
