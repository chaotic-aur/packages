# Maintainer: Kyle Manna <kyle(at)kylemanna(dot)com>
# Co-Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=expressvpn
pkgver=3.74.0.6_1
pkgrel=1
pkgdesc="Proprietary VPN client for Linux"
arch=('x86_64' 'i686' 'armv7h')
depends=('glibc')
url="https://expressvpn.com"
license=('LicenseRef-custom')
options=(!strip)
install=expressvpn.install
_url="https://www.expressvpn.works/clients/linux"
source_x86_64=("${_url}/${pkgname}_${pkgver/_/-}_amd64.deb"{,.asc})
source_i686=("${_url}/${pkgname}_${pkgver/_/-}_i386.deb"{,.asc})
source_armv7h=("${_url}/${pkgname}_${pkgver/_/-}_armhf.deb"{,.asc})

sha512sums_x86_64=('0c6a271fe1d8798d6ad82ca12a32f08da5b5da4d2b8e3b76b49a61379253f32e085a10fe0dfbcad0b1f9113315fef8441687e8344394b61c1b50dda0fcf97253'
  'SKIP')
sha512sums_i686=('99d49a4d0ea9656caef5a7a0afd074dc6ab2dbde53235a94f4389b06739ce7e1592fd08c9cac197a68df3c1c1b640df2185c2319add4a3688abda8f84f417d56'
  'SKIP')
sha512sums_armv7h=('4cf5c14b85e2dd41fd0648bbce3e28b28fbe59d8fe77a0533f9030d8c06bf66676a9cc4d26a063dc79b4776a389dcbeb06b498f528e76791fb0b3f6e7ed0ff48'
  'SKIP')
validpgpkeys=('1D0B09AD6C93FEE93FDDBD9DAFF2A1415F6A3A38')

package() {
  # /usr/sbin is a symlink to /usr/bin, rewrite it. Upstream also install files to both /lib and /usr/lib
  # merge these and move to correct location
  bsdtar -C "${pkgdir}" -xf "${srcdir}/data.tar.gz" -s ":/usr/sbin:/usr/bin:" -s ":/usr/lib:/lib:"
  mv "${pkgdir}/lib" "${pkgdir}/usr/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/expressvpn/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  install -dm755 "${pkgdir}/var/lib/expressvpn/certs"

  # Remove unused /etc
  rm -r "$pkgdir/etc/"
}
