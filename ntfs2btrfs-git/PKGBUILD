# Maintainer: Manuel HÃ¼sers <aur@huesers.de>
# Contributor: N Fytilis <n-fit AT live.com>

pkgname=ntfs2btrfs-git
pkgver=20240115.r0.g1d55788
pkgrel=1
pkgdesc="In-place conversion of Microsoft's NTFS filesystem to the open-source filesystem Btrfs"
arch=('x86_64')
url="https://github.com/maharmstone/${pkgname%-git}"
license=('GPL2')
depends=('fmt' 'zlib' 'lzo' 'zstd')
makedepends=('git' 'cmake' 'pkgconf')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/release-//g;s/-/./g'
}

build() {
  cmake -B build -S "${pkgname%-git}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SBINDIR=bin \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
