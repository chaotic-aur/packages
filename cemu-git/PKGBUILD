# Maintainer: aur.chaotic.cx

: ${_commit_fmt:=0c9fce2ffefecfdce794e1859584e25877b7b592}     # 11.0.2
: ${_commit_glslang:=fa9c3deb49e035a8abcabe366f26aac010f6cbfb} # 14.3.0

_pkgname="cemu"
pkgname="$_pkgname-git"
pkgver=2.5.r19.ge834515
pkgrel=2
pkgdesc="Nintendo Wii U emulator"
url="https://github.com/cemu-project/Cemu"
license=('MPL-2.0')
arch=('x86_64')

depends=(
  'libx11'
  'pugixml'
  'sdl2'
  'spirv-tools'
  'wxwidgets-gtk3'
)
makedepends=(
  'bluez-libs'
  'boost'
  'cmake'
  'curl'
  'git'
  'glib2'
  'glm'
  'glu'
  'gtk3'
  'hidapi'
  'libgl'
  'libpng'
  'libusb'
  'libzip'
  'nasm'
  'ninja'
  'openssl'
  'rapidjson'
  'spirv-headers'
  'vulkan-headers'
  'wayland'
  'wayland-protocols'
  'zarchive'
  'zlib'
  'zstd'
)

provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")

_source_main() {
  _pkgsrc="$_pkgname"
  source=("$_pkgsrc"::"git+$url.git")
  sha256sums=('SKIP')
}

_source_cemu() {
  local _sources_add=(
    #'exzap.zarchive'::'git+https://github.com/Exzap/ZArchive.git'
    #'khronosgroup.vulkan-headers'::'git+https://github.com/KhronosGroup/Vulkan-Headers.git'
    #'microsoft.vcpkg'::'git+https://github.com/microsoft/vcpkg.git'
    'mozilla.cubeb'::'git+https://github.com/mozilla/cubeb.git'
    'ocornut.imgui'::'git+https://github.com/ocornut/imgui.git'
  )

  local _p
  for _p in ${_sources_add[@]}; do
    source+=("$_p")
    sha256sums+=('SKIP')
  done

  _prepare_cemu() (
    cd "$srcdir/$_pkgsrc"
    local _submodules=(
      #'exzap.zarchive'::'dependencies/ZArchive'
      #'khronosgroup.vulkan-headers'::'dependencies/Vulkan-Headers'
      #'microsoft.vcpkg'::'dependencies/vcpkg'
      'mozilla.cubeb'::'dependencies/cubeb'
      'ocornut.imgui'::'dependencies/imgui'
    )
    _submodule_update
  )
}

_source_fmt() {
  source+=("fmtlib.fmt"::"git+https://github.com/fmtlib/fmt.git#commit=$_commit_fmt")
  sha256sums+=('SKIP')
}

_source_glslang() {
  source+=("khronosgroup.glslang"::"git+https://github.com/KhronosGroup/glslang.git#commit=$_commit_glslang")
  sha256sums+=('SKIP')
}

_source_main
_source_cemu
_source_fmt
_source_glslang

prepare() {
  _submodule_update() {
    local _module
    for _module in "${_submodules[@]}"; do
      git submodule init "${_module##*::}"
      git submodule set-url "${_module##*::}" "$srcdir/${_module%%::*}"
      git -c protocol.file.allow=always submodule update "${_module##*::}"
    done
  }

  _prepare_cemu

  sed -e '/CMAKE_INTERPROCEDURAL_OPTIMIZATION/d' -i "$_pkgsrc/CMakeLists.txt"
  sed -e '/FMT_HEADER_ONLY/d' -i "$_pkgsrc/src/Common/precompiled.h"
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

_build_fmt() (
  echo "Building fmt..."

  local _cmake_options=(
    -B build_fmt
    -S "fmtlib.fmt"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DFMT_CMAKE_DIR='lib/cmake/fmt'
    -DFMT_DOC=OFF
    -DFMT_INC_DIR='include/fmt'
    -DFMT_PKGCONFIG_DIR='lib/fmt/pkgconfig'
    -DFMT_TEST=OFF
    -DBUILD_SHARED_LIBS=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build_fmt
  DESTDIR="$srcdir/deps" cmake --install build_fmt
)

_build_glslang() (
  echo "Building glslang..."

  local _cmake_options=(
    -B build_glslang
    -S "khronosgroup.glslang"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DALLOW_EXTERNAL_SPIRV_TOOLS=ON
    -DBUILD_SHARED_LIBS=OFF
    -DGLSLANG_TESTS=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build_glslang
  DESTDIR="$srcdir/deps" cmake --install build_glslang
)

_build_main() (
  echo "Building cemu..."
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_DISCORD_RPC=OFF
    -DENABLE_VCPKG=OFF
    -DUSE_SANITIZERS=OFF # cubeb
    -Dfmt_DIR="$srcdir/deps/usr/lib/cmake/fmt"
    -Dglslang_DIR="$srcdir/deps/usr/lib/cmake/glslang"
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
)

build() (
  export CFLAGS CXXFLAGS LDFLAGS
  CFLAGS+=' -DNDEBUG'
  CXXFLAGS+=' -DNDEBUG'
  LDFLAGS+=" -ffat-lto-objects -Wl,--copy-dt-needed-entries"

  _build_fmt
  _build_glslang
  _build_main
)

package() {
  depends+=(
    'libbluetooth.so'
    'libboost_program_options.so'
    'libcrypto.so'
    'libcurl.so'
    'libgdk-3.so'
    'libgobject-2.0.so'
    'libgtk-3.so'
    'libhidapi-hidraw.so'
    'libssl.so'
    'libusb-1.0.so'
    'libwayland-client.so'
    'libz.so'
    'libzarchive.so'
    'libzip.so'
    'libzstd.so'
  )

  cd "$_pkgsrc"

  install -dm755 "$pkgdir"/usr/{bin,share/Cemu}
  mv bin/Cemu_none "$pkgdir"/usr/bin/Cemu
  cp --reflink=auto -a bin/* "$pkgdir"/usr/share/Cemu/

  install -Dm644 dist/linux/info.cemu.Cemu.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 dist/linux/info.cemu.Cemu.png -t "$pkgdir"/usr/share/pixmaps/

  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
