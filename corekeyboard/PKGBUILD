# Maintainer: Dan Johansen <strit@strits.dk>
# Contributor: Shaber

pkgname=corekeyboard
pkgver=4.5.0
pkgrel=2
pkgdesc="A virtual keyboard for X11 from the C Suite"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/cubocore/coreapps/$pkgname"
license=('GPL-3.0-or-later')
depends=(
  'qt5-base'
  'hicolor-icon-theme'
  'libcprime>=2.7.1'
  'qt5-x11extras'
  'libxtst'
)
makedepends=(
  'cmake'
  'ninja'
)
groups=('coreapps')
source=("https://gitlab.com/cubocore/coreapps/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('0b98cb0ba71334fb4dd8f753ea7086c02014df35d365511ab819d734f91ce286')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
