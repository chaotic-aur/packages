# Maintainer: aur.chaotic.cx

_pkgname="ocenaudio"
pkgname="$_pkgname-bin"
pkgver=3.16.4
pkgrel=1
pkgdesc="A fast, easy to use audio editor"
url="https://www.ocenaudio.com/"
license=('LicenseRef-Ocenaudio')
arch=('x86_64')

depends=(
  'hicolor-icon-theme'
  'jack'
  'libpulse'
  'qt6-base'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

options=('!debug' '!strip')

source=("${_pkgname}_${pkgver}_x86_64.tar.zst"::"https://www.ocenaudio.com/downloads/index.php/ocenaudio_archlinux.pkg.tar.zst?version=v${pkgver}")
sha256sums=('SKIP')

package() {
  # files
  mkdir -pm755 "$pkgdir"/{opt,usr/bin}
  cp -r opt/ocenaudio "$pkgdir/opt/"
  cp -r usr/share "$pkgdir/usr/"

  # fixes
  chmod ugo-x "$pkgdir"/opt/ocenaudio/lib/*
  find "$pkgdir"/opt/ocenaudio/lib -type f -exec patchelf --set-rpath '$ORIGIN' {} \;

  # symlink
  ln -sf "/opt/ocenaudio/bin/ocenaudio" "$pkgdir/usr/bin/$_pkgname"

  # license
  install -Dm644 opt/ocenaudio/bin/ocenaudio_license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/opt/ocenaudio/bin/ocenaudio_license.txt"
}
