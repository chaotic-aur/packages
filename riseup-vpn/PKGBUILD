# Maintainer: begin-theadventure <begin-thecontact.ncncb at dralias dot com>
# Contributor: peanut2 <riseup-vpn-git>
# Contributor: rany <rany at i2pmail dot org>
# Contributor: Akuma <https://0xacab.org/leap/bitmask-vpn/-/issues/94#note_173017>

pkgname=riseup-vpn
pkgver=0.24.8
_commit=8b3ac473f64b6de0262fbf945ff25af8029134f1
pkgrel=2
pkgdesc="Easy, fast, and secure VPN service from riseup.net"
url="https://0xacab.org/leap/bitmask-vpn"
license=('GPL-3.0-only')
arch=('x86_64')
depends=('gcc-libs' 'hicolor-icon-theme' 'libglvnd' 'lxsession' 'openvpn' 'python' 'qt6-base'
  'qt6-declarative' 'qt6-quickcontrols2' 'qt6-svg' 'qt6-tools')
makedepends=('cmake' 'go' 'git')
source=("git+$url.git#commit=$_commit"
  "$pkgname.png"
  "$pkgname.desktop")
sha256sums=('SKIP'
  '76955f7b4ab01aa9a6fa8213fc062765158dda8783075459b79c147febe45bb4'
  'e21a0d99dcea6b849f80960fccc488e6294e3e794b0033fdc163291ecc8595ff')

prepare() {
  cd bitmask-vpn
  export GOCACHE="$srcdir/GOCACHE"
  sed -i 's@/usr/sbin/bitmask-root@/usr/bin/bitmask-root@g' helpers/se.leap.bitmask.policy
  sed -i 's@/usr/sbin/bitmask-root@/usr/bin/bitmask-root@g' pkg/launcher/launcher_linux.go
  sed -i 's@/usr/sbin/bitmask-root@/usr/bin/bitmask-root@g' pkg/pickle/helpers.go
  sed -i 's@/usr/sbin/bitmask-root@/usr/bin/bitmask-root@g' pkg/pickle/helpers/bitmask-root
  PROVIDER=riseup make vendor
}

build() {
  cd bitmask-vpn
  export GOCACHE="$srcdir/GOCACHE"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  PROVIDER=riseup QMAKE=qmake6 LRELEASE=/usr/lib/qt6/bin/lrelease make build -j $(nproc)
}

check() {
  cd bitmask-vpn
  export GOCACHE="$srcdir/GOCACHE"
  CI="dont run CI tests as they are broken" make test
}

package() {
  install -Dm644 $pkgname.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 $pkgname.png -t "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  cd bitmask-vpn
  install -Dm644 gui/resources/riseup-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm644 helpers/se.leap.bitmask.policy -t "$pkgdir/usr/share/polkit-1/actions"
  install -Dm755 helpers/bitmask-root -t "$pkgdir/usr/bin"
  install -Dm755 build/qt/release/$pkgname -t "$pkgdir/usr/bin"
}
