# Maintainer: Luke Featherston <lukefeatherston1223 at gmail dot com>
pkgname=gearlever
pkgver=4.2.2
pkgrel=1
pkgdesc="Manage AppImages with ease"
arch=('x86_64' 'aarch64')
url="https://gearlever.mijorus.it/"
license=('GPL-3.0-or-later')
depends=('7zip' 'binutils' 'dconf' 'dwarfs' 'fuse2' 'gdk-pixbuf2' 'glibc' 'glib2' 'gtk4'
  'hicolor-icon-theme' 'libadwaita' 'squashfs-tools' 'pango' 'python' 'python-dbus'
  'python-desktop-entry-lib' 'python-ftputil' 'python-gobject' 'python-pyxdg'
  'python-requests' 'zlib')
makedepends=('gettext' 'meson')
checkdepends=('appstream' 'desktop-file-utils')
optdepends=('libxml2-legacy: required for LibreOffice appimage')
options=('!strip' '!debug')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/mijorus/gearlever/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('bb9e8f14c424b6abf3ac01815d0779f216b76776cb98f946df49de2612bd1076')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Arch linux uses the 7z command instead of the 7zz included in gearlever (both official 7zip)
  sed -i "s/7zz/7z/g" src/providers/AppImageProvider.py

  # Direct internally used script to its new location in lib
  sed -i "s/get_appimage_offset/\/usr\/lib\/gearlever\/get_appimage_offset/g" src/providers/AppImageProvider.py
}

build() {
  arch-meson "${srcdir}/${pkgname}-${pkgver}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"

  cd "${srcdir}/${pkgname}-${pkgver}/"
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/$pkgname/"
  mkdir -p "${pkgdir}/usr/lib/gearlever"
  install -Dm755 "build-aux/get_appimage_offset.sh" "${pkgdir}/usr/lib/gearlever/get_appimage_offset"

  cd "${pkgdir}"
  rm -v "usr/share/icons/hicolor/scalable/actions/meson.build"
  rm -v "usr/share/${pkgname}/${pkgname}/meson.build"
}
