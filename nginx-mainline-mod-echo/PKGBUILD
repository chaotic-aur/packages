# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mainline-mod-echo
pkgver=0.61
pkgrel=37

_modname="${pkgname#nginx-mainline-mod-}"

pkgdesc='Directives "echo", "sleep", "time" and more (module for mainline nginx)'
arch=('i686' 'x86_64')
depends=('nginx-mainline')
makedepends=('nginx-mainline-src')
url="https://github.com/openresty/echo-nginx-module"
license=('BSD')

source=(https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz)
sha256sums=('2e6a03032555f5da1bdff2ae96c96486f447da3da37c117e0f964ae0753d22aa')

prepare() {
  mkdir -p build
  cd build
  ln -sf /usr/src/nginx/auto
  ln -sf /usr/src/nginx/src
}

build() {
  cd build
  /usr/src/nginx/configure --with-compat --add-dynamic-module=../$_modname-nginx-module-$pkgver
  make modules
}

package() {
  install -Dm644 "$srcdir/"$_modname-nginx-module-$pkgver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd build/objs
  for mod in *.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
