# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: ValHue <vhuelamo at gmail dot com>
# Contributor: satanselbow <igdfpm at gmail dot com>
# Contributor: Artem Sereda <overmind88 at gmail dot com>

pkgname=flacon
pkgver=12.0.0
pkgrel=1
pkgdesc="An Audio File Encoder. Extracts audio tracks from an audio CD image to separate tracks."
arch=(x86_64 i686 aarch64)
url="https://flacon.github.io/"
license=(LGPL2.1)
depends=(hicolor-icon-theme qt6-base uchardet taglib glibc gcc-libs)
makedepends=(git cmake icu qt6-tools yaml-cpp)
optdepends=(
  'alacenc: For ALAC support'
  'faac: For AAC support'
  'flac: For FLAC support'
  'lame: For MP3 support'
  'mac: For APE support'
  'opus-tools: For OPUS support'
  'sox: For SoX support'
  'ttaenc: For TrueAudio support'
  'vorbis-tools: For OGG support'
  'wavpack: For WavPack support'
)
source=("git+https://github.com/flacon/flacon.git#tag=v${pkgver}")
sha256sums=('027158f731462f75fdd6af2a890d8f6cdeec55c9587df5df7ddb830e424e6339')

build() {
  cmake -B build -S "flacon" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT6=ON \
    -DUSE_QT5=OFF

  cmake --build build
}

#check() {
#  ctest --test-dir build --output-on-failure
#}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
