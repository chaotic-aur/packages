# options
: ${_build_git:=true}

unset _pkgtype
[[ "${_build_git::1}" == "t" ]] && _pkgtype+="-git"

# basic info
_pkgname=flameshot
pkgname="$_pkgname${_pkgtype:-}"
pkgver=12.1.0.r124.g3d21e49
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software"
url="https://github.com/flameshot-org/flameshot"
license=('GPL-3.0-or-later')
arch=('x86_64')

# main package
_main_package() {
  depends=(
    'qt5-svg'
    'hicolor-icon-theme'
    'kguiaddons5'
  )
  makedepends=(
    'make'
    'qt5-tools'
    'cmake'
  )
  optdepends=(
    'gnome-shell-extension-appindicator: system tray icon on GNOME'
    'grim: wlroots wayland support'
    'xdg-desktop-portal: wayland support'
  )

  if [ "${_build_git::1}" != "t" ]; then
    _main_stable
  else
    _main_git
  fi
}

# stable package
_main_stable() {
  _pkgsrc="$_pkgname-${pkgver%%.r*}"
  _pkgext="tar.gz"

  source+=("$_pkgsrc.$_pkgext"::"$url/archive/v${pkgver%%.r*}.$_pkgext")
  sha256sums+=(
    'c82c05d554e7a6d810aca8417ca12b21e4f74864455ab4ac94602668f85ac22a'
  )

  pkgver() {
    echo "${pkgver%%.r*}"
  }
}

# git package
_main_git() {
  makedepends+=('git')

  provides=("$_pkgname=${pkgver%%.r*}")
  conflicts=("$_pkgname")

  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git")
  sha256sums+=('SKIP')

  pkgver() {
    cd "$_pkgsrc"
    _tag=$(git tag | grep -Ev '[a-z]{2}' | sort -rV | head -1)
    _revision=$(git rev-list --count --cherry-pick "$_tag"...HEAD)
    _commit=$(git rev-parse --short=7 HEAD)

    printf '%s.r%s.g%s' "${_tag#v}" "${_revision:?}" "${_commit:?}"
  }
}

# common functions
build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DUSE_WAYLAND_CLIPBOARD=1
    -DUSE_WAYLAND_GRIM=true
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# execute
_main_package
