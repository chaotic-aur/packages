# Maintainer: aur.chaotic.cx

## links
# https://www.termius.com/

_pkgname="termius"
_snapname="$_pkgname-app"
pkgname="$_pkgname"
pkgver=9.13.1
pkgrel=1
pkgdesc="Desktop SSH Client"
url='https://snapcraft.io/termius-app'
arch=('x86_64')
license=('custom')

makedepends=(
  'html-xml-utils'
  'sha3sum'
  'squashfs-tools'
  'w3m'
)

_source_main() {
  source=("$_filename"::"$_dl_url")
  sha256sums=('SKIP')
}

_privacy_policy="privacy_policy"
_terms_of_use="terms_of_use"

prepare() {
  # check upstream sha3-384sum
  printf '%s  %s' "$_cksum" "$_filename" | sha3-384sum -c

  # privacy policy
  curl --no-progress-meter \
    -o "$_privacy_policy-1.html" \
    "https://termius.com/privacy-policy"

  hxextract .policy-section "$_privacy_policy-1.html" \
    1> "$_privacy_policy-2.html" \
    2> /dev/null

  w3m -O UTF-8 -cols 80 -dump "$_privacy_policy-2.html" > "$_privacy_policy.txt"

  # terms of use
  curl --no-progress-meter \
    -o "$_terms_of_use-1.html" \
    "https://termius.com/terms-of-use"

  hxextract .policy-section "$_terms_of_use-1.html" \
    1> "$_terms_of_use-2.html" \
    2> /dev/null

  w3m -O UTF-8 -cols 80 -dump "$_terms_of_use-2.html" > "$_terms_of_use.txt"

  # unpack
  mkdir -p "$_pkgname-$_pkgver"
  unsquashfs -q -n -f -d "$_pkgname-$_pkgver/" "$_filename"

  # .desktop file
  mv "$_pkgname-$_pkgver/meta/gui/$_snapname.desktop" "$srcdir/$_pkgname.desktop"
  sed \
    -e 's@'"$_snapname"'@'"${_pkgname}"'@g' \
    -e 's@${SNAP}/meta/gui/icon.png@'"${_pkgname}"'@g' \
    -i "$srcdir/$_pkgname.desktop"

  # icon
  mv "$_pkgname-$_pkgver/meta/gui/icon.png" "$srcdir"

  # unnecessary files
  rm -r "$_pkgname-$_pkgver/"{data-dir/,gnome-platform/,lib/,meta/,scripts/,usr/,*.sh}
}

package() {
  depends+=(
    'at-spi2-core'
    'dbus'
    'e2fsprogs'
    'expat'
    'gtk3'
    'keyutils'
    'libbsd'
    'libnotify'
    'libsecret'
    'libxss'
    'libxtst'
    'nss'
    'util-linux-libs'
    'xdg-utils'
  )
  optdepends+=(
    'libappindicator-gtk3'
  )

  # opt
  install -d "$pkgdir/opt/$_pkgname"
  cp -r "$srcdir/$_pkgname-$pkgver/"* "$pkgdir/opt/$_pkgname"

  # symlink binary
  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$_pkgname/$_snapname" "$pkgdir/usr/bin/$_pkgname"

  # icons
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/$_pkgname.png"
  install -Dm644 "$srcdir/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"

  # license/eula
  install -Dm644 "$_privacy_policy.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 "$_terms_of_use.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
}

_update_version() {
  : ${_pkgver:=${pkgver%%.r*}}

  local _response
  _response=$(
    curl -H 'Snap-Device-Series: 16' --no-progress-meter "http://api.snapcraft.io/v2/snaps/info/$_snapname" \
      | sed 's@,@,\n@g'
  )

  _dl_url=$(
    printf '%s' "$_response" \
      | grep '"url"' \
      | sed -E 's@^"url":"(https:.*\.snap)".*$@\1@' \
      | head -1
  )

  _pkgver_new=$(
    printf '%s' "$_response" \
      | grep '"version"' \
      | sed -E 's@^"version":"([0-9]+\.[0-9]+\.[0-9]+)".*$@\1@' \
      | head -1
  )

  # sha3-384 checksum
  _cksum=$(
    printf '%s' "$_response" \
      | grep '"sha3-384"' \
      | sed -E 's@^"sha3-384":"([a-f0-9]+)".*$@\1@' \
      | head -1
  )

  _filename="$_pkgname-$_pkgver_new.snap"

  # update _pkgver
  if [ $(vercmp "${_pkgver_new:?}" "$_pkgver") -gt 0 ]; then
    _pkgver="${_pkgver_new:?}"
  fi

  pkgver() {
    printf '%s' "${_pkgver:?}"
  }
}

_update_version
_source_main
