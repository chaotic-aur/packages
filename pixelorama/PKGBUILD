# Maintainer: aur.chaotic.cx

_pkgname="pixelorama"
pkgname="$_pkgname"
pkgver=1.1.6
pkgrel=1
pkgdesc="A 2D sprite editor made with the Godot Engine"
url="https://github.com/Orama-Interactive/Pixelorama"
license=('MIT')
arch=('any')

depends=(
  'bash'
  'godot-export-templates-linux' # use as runtime
)
makedepends=(
  'git'
  'godot'
)

_pkgsrc="${pkgname^}-$pkgver"
_pkgext="tar.gz"
source=("${_pkgsrc,,}.$_pkgext"::"$url/archive/v$pkgver.$_pkgext")
sha256sums=('SKIP')

build() {
  local _template=$(find /usr/share/godot/export_templates -type f -name "linux_release.x86_64" -print -quit)

  cd "$_pkgsrc"
  godot --export-pack "Linux 64-bit" "$_pkgname.pck" --headless
}

package() {
  local _template=$(find /usr/share/godot/export_templates -type f -name "linux_release.x86_64" -print -quit)

  cd "$_pkgsrc"

  # godot pack
  install -Dm644 "$_pkgname.pck" -t "$pkgdir/usr/share/$_pkgname/"

  # resources
  cp -a pixelorama_data/{Brushes,Palettes,Patterns} "$pkgdir/usr/share/$_pkgname/"

  # icon
  install -Dm644 "assets/graphics/icons/icon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  # launcher
  install -Dm644 "Misc/Linux/com.orama_interactive.Pixelorama.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"

  # script
  install -Dm755 /dev/stdin "${pkgdir}/usr/bin/$_pkgname" << END
#!/usr/bin/env sh
exec "$_template" --main-pack "/usr/share/$_pkgname/$_pkgname.pck" "\$@"
END

  # license
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

  # permissions
  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
