# Maintainer:  Alessandro Marchioro <marciozgaming@gmail.com>
# Co-Maintainer: Cp Dong <cp-dong at outlook dot com>
pkgname=apple-fonts
pkgver=6.0.1.1726709071
pkgrel=2
pkgdesc='Fonts for Apple platforms, including San Francisco and New York typefaces'
arch=(any)
url='https://developer.apple.com/fonts/'
license=('custom')
provides=('ttf-font')
depends=()
makedepends=(7zip)
_baseurl='https://devimages-cdn.apple.com/design/resources/download/'
source=("${_baseurl}SF-Pro.dmg"
  "${_baseurl}SF-Compact.dmg"
  "${_baseurl}SF-Mono.dmg"
  "${_baseurl}NY.dmg")
sha256sums=('21c701d2e5967cf0a2747617eac02eb2e1195fdd3a755628f086aa797eff3bcf'
  '3e5ada33a4b01fcb13c67541a3a2eab7d07ab406430bffd508fc2bfcf35c9e59'
  '6d4a0b78e3aacd06f913f642cead1c7db4af34ed48856d7171a2e0b55d9a7945'
  '1c2eedb4526cc0f326f8b7ea978f5a433756476a2c8c56072537608edb88f8f4')
noextract=("${source[@]##*/}")

prepare() {
  cd "$srcdir"
  mkdir -p fonts licenses tmp
  for archive in *.dmg; do
    7z e "$archive" -y -otmp/
    cd tmp/
    7z x -txar *.pkg -y
    font=$(grep -o -e "THE APPLE .* FONT" Resources/English.lproj/License.rtf | head -n 1)
    mv Resources/English.lproj/License.rtf "$srcdir/licenses/LICENSE.${font// /-}"
    cd *.pkg/
    7z x Payload -y
    7z x Payload\~ -y
    mv Library/Fonts/* "$srcdir/fonts/"
    cd "$srcdir"
    rm -r tmp/{*,.*}
  done
  rmdir tmp/
}

package() {
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/licenses"/*

  install -Dm644 -t "$pkgdir/usr/share/fonts/$pkgname" "$srcdir/fonts"/*
}
