# Maintainer: Atakku <atakkudev@gmail.com>
# Maintainer: txtsd <aur.archlinux@ihavea.quest>

pkgname=blockbench-git
_pkgname=${pkgname%%-git}
pkgver=4.11.2.r0.gcd266d76
pkgrel=1
pkgdesc="A low-poly 3D model editor (git version)"
arch=('x86_64' 'aarch64')
url=https://github.com/JannisX11/blockbench
license=('GPL-3.0-or-later')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_electron=electron
depends=(
  "${_electron}"
  'giblib'
  'libx11'
  'imlib2'
  'bash'
  'glibc'
)
makedepends=(git npm)
options=(!debug)
source=("${_pkgname}::git+https://github.com/JannisX11/blockbench.git"
  "${_pkgname}.desktop")
sha256sums=('SKIP'
  '2dd94f6c807fac6e83fc2176098390100f3c227285671804a855a092c0a2a2e7')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  npm install
}

build() {
  cd "${srcdir}/${_pkgname}"
  _electronDist=/usr/lib/${_electron}
  _electronVersion=$(cat ${_electronDist}/version)
  npm run dist -- --linux --x64 --dir -c.electronDist=${_electronDist} -c.electronVersion="${_electronVersion}"
}

package() {
  _electronDist=/usr/lib/${_electron}
  _electronVersion=$(cat ${_electronDist}/version)
  _electronVersionMajor=${_electronVersion%%.*}
  _electronDir=${_electronDist}${_electronVersionMajor}
  install -d "${pkgdir}/usr/"{bin,share/{pixmaps,applications}}
  install -d "${pkgdir}/${_electronDist}${_electronVersionMajor}/resources"
  echo -e "#!/bin/bash\nexec ${_electron} ${_electronDist}${_electronVersionMajor}/resources/${_pkgname}.asar \"\$@\"" > "${pkgdir}/usr/bin/${_pkgname}"
  chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
  install "${srcdir}/${_pkgname}/icon.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  install "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install "${srcdir}/${_pkgname}/dist/linux-unpacked/resources/app.asar" "${pkgdir}${_electronDist}${_electronVersionMajor}/resources/${_pkgname}.asar"
  cp -r "${srcdir}/${_pkgname}/dist/linux-unpacked/resources/app.asar.unpacked" "${pkgdir}${_electronDist}${_electronVersionMajor}/resources/${_pkgname}.asar.unpacked"
}
