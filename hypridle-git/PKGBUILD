# Maintainer: alba4k <blaskoazzolaaaron@gmail.com>

_pkgname="hypridle"
pkgname="${_pkgname}-git"
pkgver=0.1.5.r7.g93cb051c
pkgrel=1
pkgdesc="Hyprland's idle daemon"
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/hypridle"
license=('BSD-3-Clause')
depends=('wayland' 'hyprlang-git' 'sdbus-cpp>=2.0.0' 'systemd' 'hyprutils-git')
makedepends=('git' 'cmake' 'gcc' 'hyprland-protocols-git' 'hyprwayland-scanner-git' 'wayland-protocols')
source=("${_pkgname}::git+https://github.com/hyprwm/hypridle.git")
provides=("hypridle")
conflicts=("hypridle")
sha256sums=('SKIP')

pick_mr() {
  cd ${srcdir}/${_pkgname}

  git pull origin pull/$1/head --no-edit --rebase
}

prepare() {
  # Pick pull request from github using `pick_mr <pull request number>`
  # e.g. `pick_mr 100`
  echo -n
}

pkgver() {
  cd ${_pkgname}
  git describe --long --tags --abbrev=8 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake --no-warn-unused-cli -DCMAKE_INSTALL_PREFIX=/usr -S . -B ./build
  cmake --build ./build --config Release --target hypridle
}

package() {
  cd "${srcdir}/${_pkgname}"
  DESTDIR="${pkgdir}" cmake --install build

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
