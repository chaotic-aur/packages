# Maintainer: aur.chaotic.cx

_pkgname="eden"
pkgname="$_pkgname-git"
pkgver=0.0.3.r181.ge367bdf
pkgrel=2
pkgdesc="Yet another emulator fork"
url="https://git.eden-emu.dev/eden-emu/eden"
license=('GPL-3.0-or-later')
arch=('x86_64')

depends=(
  'enet'
  'libfmt.so' # fmt
  'mbedtls'
  'qt6-webengine'
  'libavcodec.so' # ffmpeg
  'libavutil.so'  # ffmpeg
  'libopus.so'    # opus
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'glslang'
  'nasm'
  'ninja'
  'nlohmann-json'
  'qt6-multimedia'
  'qt6-tools'
  'spirv-headers'
  'vulkan-headers'
  'vulkan-utility-libraries'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

options=('!emptydirs')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  local _tmp _tag _version _revision _hash
  _tmp=$(git tag | grep -Ev '[A-Za-z][A-Za-z]' | sed -E 's&([^0-9]*)(\S+)$&\2 \1\2&' | sort -rV | head -1)
  _version=$(cut -f1 -d' ' <<< ${_tmp:?})
  _tag=$(cut -f2 -d' ' <<< ${_tmp:?})
  _revision=$(git rev-list --count --cherry-pick "$_tag"...HEAD)
  _commit=$(git rev-parse --short=7 HEAD)
  printf '%s.r%s.g%s' "${_version:?}" "${_revision:?}" "${_commit:?}"
}

prepare() {
  cd "$_pkgsrc"

  # Fix for boost 1.89
  sed -E -e '/find_package\(Boost/s&\bsystem\b&&' -i CMakeLists.txt
  sed -e '1i #include <boost/system.hpp>' \
    -i src/core/debugger/debugger.cpp \
    src/input_common/drivers/udp_client.cpp \
    src/tests/input_common/calibration_configuration_job.cpp
}

build() (
  export CFLAGS+=' -DNDEBUG'
  export CXXFLAGS+=' -DNDEBUG'

  local _cmake_options=(
    -S "$_pkgsrc"
    -B build
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX="/usr"

    -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON
    -DENABLE_QT_TRANSLATION=OFF # spurious errors
    -DENABLE_WEB_SERVICE=ON
    -DTITLE_BAR_FORMAT_IDLE="$_pkgname | ${pkgver} {}"
    -DTITLE_BAR_FORMAT_RUNNING="$_pkgname | ${pkgver} {}"
    -DUSE_DISCORD_PRESENCE=OFF
    -DYUZU_CRASH_DUMPS=OFF
    -DYUZU_DOWNLOAD_TIME_ZONE_DATA=ON
    -DYUZU_ENABLE_COMPATIBILITY_REPORTING=OFF
    -DYUZU_ENABLE_LTO=OFF
    -DYUZU_ROOM=ON
    -DYUZU_USE_BUNDLED_FFMPEG=OFF
    -DYUZU_USE_BUNDLED_QT=OFF
    -DYUZU_USE_BUNDLED_SDL2=OFF
    -DYUZU_USE_EXTERNAL_FFMPEG=OFF
    -DYUZU_USE_EXTERNAL_SDL2=OFF
    -DYUZU_USE_FASTER_LD=OFF
    -DYUZU_USE_QT_MULTIMEDIA=ON
    -DYUZU_USE_QT_WEB_ENGINE=ON
    -Dhttplib_FORCE_BUNDLED=ON

    -DUSE_SANITIZERS=OFF # cubeb
    -DDYNARMIC_TESTS=OFF
    -DYUZU_TESTS=OFF
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
)

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$_pkgsrc/dist/72-yuzu-input.rules" "$pkgdir/usr/lib/udev/rules.d/72-eden-input.rules"
}
