# Maintainer: Alexandre Bouvier <contact@amb.tf>
# Contributor: rpkak <rpkak@users.noreply.github.com>
pkgname=vulkan-memory-allocator
pkgver=3.2.0
pkgrel=1
pkgdesc="Easy to integrate Vulkan memory allocation library"
arch=('any')
url="https://gpuopen.com/vulkan-memory-allocator/"
license=('MIT')
groups=('vulkan-devel')
depends=('vulkan-headers')
makedepends=('cmake' 'doxygen' 'git')
source=("$pkgname::git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git#tag=v$pkgver")
b2sums=('9f276869a8c6dcaef7674f841c15f409302d709a903fb294b6ea5674e0319d1c019960005399868b57a2002213e4cc85e7745e3ac1599efec7b91f994a3b08bc')

prepare() {
  cd $pkgname
  rm -r docs/html
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DVMA_BUILD_DOCUMENTATION=ON \
    -Wno-dev
  cmake --build build
}

package() {
  # shellcheck disable=SC2154
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname $pkgname/LICENSE.txt
}
