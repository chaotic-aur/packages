# Maintainer: Josesk Volpe <joseskvolpe at gmail dot com>
# Co-Maintainer: Melroy van den Berg <melroy at melroy dot org>
# Contributor: yochananmarqos <mark dot wagie at tutanota dot com>

pkgname=winegui
pkgver=3.0.0
pkgrel=1
pkgdesc="A user-friendly WINE graphical interface (build from source)"
arch=('x86_64')
url="https://gitlab.melroy.org/melroy/winegui"
license=('AGPL3')
depends=(
  'cabextract' # used by winetricks
  'gtkmm-4.0'
  'p7zip'
  'unzip'
  'wget' # to download winetricks latest release
  'wine'
  'zenity' # used by winetricks
)
optdepends=(
  'ccache: Speeds up rebuilds'
  'graphviz'
)
makedepends=(
  'cmake'
  'git'
  'json-glib'
  'ninja'
)
source=("https://winegui.melroy.org/downloads/WineGUI-Source-v$pkgver.tar.gz")
sha256sums=('2956c9248363711912346540dd432627b377cd62749679f1616a073c12bd7c8c')

build() {
  cmake -GNinja -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=Release -DCHECK_FOR_UPDATE=OFF -B build_prod
  cmake --build ./build_prod --config Release
}

package() {
  DESTDIR="$pkgdir" cmake --install build_prod
}
