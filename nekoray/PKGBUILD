# Maintainer: Chi_Tang <me@chitang.dev>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=nekoray
pkgver=4.0.beta1
_pkgver=${pkgver/.beta/-beta}
pkgrel=1
pkgdesc='Qt based cross-platform GUI proxy configuration manager (backend: v2ray / sing-box)'
arch=('x86_64' 'aarch64' 'riscv64')
url="https://matsuridayo.github.io"
license=('GPL-3.0-or-later')
makedepends=('cmake' 'git' 'ninja' 'go')
depends=(
  'qt6-base' 'qt6-svg' 'qt6-tools'
  'protobuf' 'yaml-cpp' 'zxing-cpp'
  'abseil-cpp'
)

optdepends=(
  'v2ray-domain-list-community: geosite data for NekoRay'
  'v2ray-geoip: geoip data for NekoRay'
  # AUR
  'sing-geoip: geoip data for NekoBox'
  'sing-geosite: geosite data for NekoBox'
)

source=(
  "git+https://github.com/MatsuriDayo/${pkgname}.git#tag=${_pkgver}"
  "${pkgname}.desktop"
)

sha512sums=('ef2005cbdd99be3a7c596cbfed0ddac098949952a6cfbffb363d4fac50bddb54976d38558b3f9b339e4810255c729dfd34377e8f6eccf0ff643abdd9f036f343'
  'c8ad5a0cf5d813e1e35b827f92d526c022d529d34dd4019066b550c8f0dbdb89b0fb62e6bfc650e8c18acac826591e858eff3b2e5e6c6f5014f0f4b50f1fa88b')

build() {
  cd "${pkgname}/"
  git submodule init
  git submodule update
  bash libs/get_source.sh

  mkdir build
  cd build
  cmake -GNinja -DQT_VERSION_MAJOR=6 -DNKR_PACKAGE=ON ..
  ninja

  cd ..

  export GOARCH

  case $CARCH in
    x86_64) GOARCH=amd64 ;;
    aarch64) GOARCH=arm64 ;;
    riscv64) GOARCH=riscv64 ;;
  esac

  GOOS=linux ./libs/build_go.sh
}

package() {
  install -dm755 "${pkgdir}/usr/bin/"

  # assets
  install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  install -Dm644 ${pkgname}/res/public/{nekoray,nekobox}.png -t "${pkgdir}/usr/share/pixmaps/"

  # core
  local core_srcdir

  case $CARCH in
    x86_64) core_srcdir=linux64 ;;
    aarch64) core_srcdir=linux-arm64 ;;
    riscv64) core_srcdir=linux-riscv64 ;;
  esac

  install -Dm755 ${pkgname}/deployment/${core_srcdir}/nekobox_core -t "${pkgdir}/usr/lib/${pkgname}/"

  # app
  install -Dm755 "${pkgname}/build/${pkgname}" -t "${pkgdir}/usr/lib/${pkgname}/"

  ln -s "/usr/lib/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
