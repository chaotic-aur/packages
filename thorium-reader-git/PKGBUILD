# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=thorium-reader-git
_pkgname="Thorium Reader"
_appname="EDRLab.${_pkgname// /}"
pkgver=2.4.1.r88.g0141f2fef
_electronversion=30
_nodeversion=20
pkgrel=1
pkgdesc="Cross-platform desktop reading app based on the Readium Desktop toolkit"
arch=('any')
url="https://github.com/edrlab/thorium-reader"
license=('BSD-3-Clause')
conflicts=("${pkgname%-git}")
provides=("${pkgname%-git}=${pkgver%.r*}")
depends=(
  "electron${_electronversion}"
)
makedepends=(
  'gendesk'
  'git'
  'nvm'
  'npm'
  'python>=3.8.10'
  'python-setuptools'
)
source=(
  "${pkgname%-git}.git::git+${url}.git"
  "${pkgname%-git}.sh"
)
sha256sums=('SKIP'
  '2b2e8aeed33fd71c521e49fd54fb2fa81218d16aef8bccb88d77909055ab8051')
pkgver() {
  cd "${pkgname%-git}.git"
  git describe --long --tags --exclude='*[a-z][a-z]*' | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}
_ensure_local_nvm() {
  export NVM_DIR="${srcdir}/.nvm"
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
  nvm install "${_nodeversion}"
  nvm use "${_nodeversion}"
}
build() {
  sed -e "s|@electronversion@|${_electronversion}|" \
    -e "s|@appname@|${pkgname%-git}|g" \
    -e "s|@runname@|app.asar|g" \
    -e "s|@cfgdirname@|${_appname}|g" \
    -e "s|@options@|env ELECTRON_OZONE_PLATFORM_HINT=auto|g" \
    -i "${srcdir}/${pkgname%-git}.sh"
  _ensure_local_nvm
  gendesk -q -f -n --pkgname="${pkgname%-git}" --categories="Office" --name="${_pkgname}" --exec="${pkgname%-git} %U"
  cd "${srcdir}/${pkgname%-git}.git"
  export npm_config_build_from_source=true
  export npm_config_cache="${srcdir}/.npm_cache"
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  #export SYSTEM_ELECTRON_VERSION="$(electron${_electronversion} -v | sed 's/v//g')"
  #export npm_config_target="${SYSTEM_ELECTRON_VERSION}"
  #export ELECTRONVERSION="${_electronversion}"
  HOME="${srcdir}/.electron-gyp"
  if [ $(curl -s ipinfo.io/country | grep CN | wc -l) -ge 1 ]; then
    export npm_config_registry=https://registry.npmmirror.com
    export npm_config_disturl=https://registry.npmmirror.com/-/binary/node/
    export npm_config_electron_mirror=https://registry.npmmirror.com/-/binary/electron/
    export npm_config_electron_builder_binaries_mirror=https://registry.npmmirror.com/-/binary/electron-builder-binaries/
  else
    echo "Your network is OK."
  fi
  sed "s|--linux --x64|-l --dir|g" -i package.json
  npm install --no-audit --no-fund --prefer-offline
  npm run clean
  npm run package:linux
}
package() {
  install -Dm755 "${srcdir}/${pkgname%-git}.sh" "${pkgdir}/usr/bin/${pkgname%-git}"
  install -Dm644 "${srcdir}/${pkgname%-git}.git/release/linux-"*/resources/app.asar -t "${pkgdir}/usr/lib/${pkgname%-git}/"
  for _icons in 256x256 512x512 1024x1024; do
    install -Dm644 "${srcdir}/${pkgname%-git}.git/resources/icons/${_icons}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_icons}/apps/${pkgname%-git}.png"
  done
  install -Dm644 "${srcdir}/${pkgname%-git}.desktop" -t "${pkgdir}/usr/share/applications/"
  install -Dm644 "${srcdir}/${pkgname%-git}.git/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
