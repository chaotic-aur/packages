# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=arch-update
pkgver=3.12.7
pkgrel=1
pkgdesc="An update notifier & applier that assists you with important pre / post update tasks"
url="https://github.com/Antiz96/arch-update"
arch=('any')
license=('GPL-3.0-or-later')
depends=('bash' 'systemd' 'pacman' 'pacman-contrib' 'archlinux-contrib' 'curl' 'fakeroot'
  'htmlq' 'diffutils' 'hicolor-icon-theme' 'python' 'python-pyqt6' 'qt6-svg' 'glib2')
makedepends=('scdoc')
checkdepends=('bats')
optdepends=('paru: AUR Packages support'
  'yay: AUR Packages support'
  'pikaur: AUR Packages support'
  'flatpak: Flatpak Packages support'
  'libnotify: Desktop notifications support on new available updates'
  'vim: Default diff program for pacdiff'
  'neovim: Default diff program for pacdiff if EDITOR=nvim'
  'qt6-wayland: Systray applet support on Wayland'
  'sudo: Privilege elevation'
  'opendoas: Privilege elavation')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ef111d929e718615493bff6e2588e4e8f4b55195c12775ebd80a5b150107d36a')

prepare() {
  cd "${pkgname}-${pkgver}"
  make clean
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
