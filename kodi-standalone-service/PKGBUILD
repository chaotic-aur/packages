# Maintainer: graysky <therealgraysky AT protonmail DOT com>

pkgname=kodi-standalone-service
pkgver=1.150
pkgrel=1
pkgdesc="Systemd services to run kodi in stand-alone mode without a DE"
# This is only for x86_64
# The kodi-rpi packages for ArchARM provide their own versions of this package
arch=(x86_64)
url="https://github.com/graysky2/kodi-standalone-service"
license=(MIT)
install=readme.install
depends=(polkit kodi)
replaces=(kodi-standalone-x11-service kodi-standalone-gbm-service kodi-standalone-wayland-service)
backup=(etc/conf.d/kodi-standalone)
optdepends=(
  'cage: for kodi-wayland.service'
  'wayland: for kodi-wayland.service'
  'xorg-server: for kodi-x11.service'
  'xorg-xinit: for kodi-x11.service'
)
source=("$pkgname-v$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz")
b2sums=('1ab3d5fc5e84cbe865acaf32e31b1b6979b9ca1f70a686162691bbde7872a253678017a2768383793fc6e15bfbe76dfac368a61e308d0cf3396ed5cc1835f3ee')

package() {
  cd "$pkgname-$pkgver"
  make
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
