# Maintainer: SpacingBat3 <aur@spacingbat3.anonaddy.com>

# shellcheck shell=bash disable=SC2164,SC2034,SC2154

_pname=OpenSCQ30
_powner=Oppzippy
_pkgid=com.oppzippy.$_pname
_deps_common=('libdbus-1.so' 'libsqlite3.so' 'libsystemd.so')
_deps_gui=('cosmic-icon-theme')

pkgbase=openscq30
pkgname=("$pkgbase"-{cli,gui})
pkgver=2.5.0
pkgrel=1
pkgdesc="Cross platform application for controlling settings of Soundcore headphones"
arch=(x86_64 aarch64 armv7l)
url="https://github.com/$_powner/$_pname"
license=('GPL-3.0-or-later')
groups=("$pkgbase")
makedepends=('cargo')
depends=("${_deps_common[@]}" "${_deps_gui[@]}")
source=("$_pname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
md5sums=('6cd3a8f2c077715023c5f79f099940e6')
sha512sums=('1ba96377118d2c869d12fbc51707dc2670bec332de94c46d64c4c21a66a9f8df838d843f439456c859da55ca545d2b1d7f1b2d304e3e91a559d92f414961c414')
b2sums=('f4f3b7e3734aa386c1317cf8cda0fa821ae51f6222a42cd153d8d06cde81b43514e816bc234103300d08cd4991990dce2a35b4bec7c631d7f31762fe13da7250')

prepare() {
  cd "$srcdir/$_pname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$srcdir/$_pname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  # shellcheck disable=SC2046
  cargo build --release --frozen $(echo "-p openscq30-"{cli,gui})
}

check() {
  cd "$srcdir/$_pname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  # shellcheck disable=SC2046
  env LC_ALL=C \
    cargo test --frozen $(echo "-p openscq30-"{lib,cli,gui})
}

package_openscq30-cli() {
  # Metadata
  pkgdesc="$pkgdesc - CLI application"
  depends=("${_deps_common[@]}")
  # Packaging
  cd "$srcdir/$_pname-$pkgver"
  ## Install binary
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/${pkgbase}"
  # shellcheck disable=SC2128
  ln -s "${pkgbase}" "$pkgdir/usr/bin/$pkgname"
}

package_openscq30-gui() {
  # Metadata
  pkgdesc="$pkgdesc - Cosmic GUI application"
  # Packaging
  cd "$srcdir/$_pname-$pkgver"
  ## Install binary
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/${pkgbase}-gui"
  ## Install resources
  install -Dm0644 -t "$pkgdir/usr/share/metainfo/" "gui/resources/$_pkgid.metainfo.xml"
  install -Dm0644 -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/" "gui/resources/$_pkgid.svg"
  install -Dm0644 -t "$pkgdir/usr/share/applications/" "gui/resources/$_pkgid.desktop"
}
