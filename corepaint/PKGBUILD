# Maintainer: <strit@strits.dk>
# Contributor: Shaber

pkgname=corepaint
pkgver=4.5.0
pkgrel=2
pkgdesc="A paint app from the C Suite"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/cubocore/coreapps/$pkgname"
license=('GPL-3.0-or-later')
depends=(
  'qt5-base'
  'hicolor-icon-theme'
  'libcprime>=3.0.1'
)
makedepends=(
  'cmake'
  'ninja'
)
groups=('coreapps')
source=("https://gitlab.com/cubocore/coreapps/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('7476667be787cc648c32ed5473663eae36101d7b3d1885788de087b234adfe80')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
