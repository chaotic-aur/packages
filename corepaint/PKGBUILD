# Maintainer: <strit@strits.dk>
# Contributor: Shaber

pkgname=corepaint
pkgver=5.0.0
pkgrel=1
pkgdesc="A paint app from the C Suite"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/cubocore/coreapps/$pkgname"
license=('GPL-3.0-or-later')
depends=(
  'qt6-base'
  'hicolor-icon-theme'
  'libcprime'
)
makedepends=(
  'cmake'
  'ninja'
)
groups=('coreapps')
source=("https://gitlab.com/cubocore/coreapps/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('d813f423788b5370853050b29e6731631b6179457e5643f8c1ff4c72d0eb00e5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
