# Maintainer: Rafael Dominiquini <rafaeldominiquini at gmail dor com>
# Contributor: Xyne <xyne at archlinux dot org>

pkgname=bindfs
pkgver=1.18.1
pkgrel=1
pkgdesc="A FUSE filesystem for mirroring a directory to another directory, similar to 'mount --bind', with permission settings."
arch=('i686' 'x86_64' 'armv6h' 'armv6l' 'armv7h')
url="http://bindfs.org/"
license=('GPL-2.0')
depends=('fuse3')
source=("http://bindfs.org/downloads/${pkgname}-${pkgver}.tar.gz")
sha512sums=('002c9fac3819a766689e2b323b6c1d351b33947ff5ff15f9634263f0ce1a257efc3833d330d26793f0291cf2750fbfdaca51aac297912d348c16f889eef27dfb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr

  make CPUOPTIMIZATIONS="${CFLAGS}"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  ln -s bindfs "$pkgdir/usr/bin/mount.bindfs"
  ln -s bindfs "$pkgdir/usr/bin/mount.fuse.bindfs"

  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
