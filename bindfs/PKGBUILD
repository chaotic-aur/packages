# Maintainer: Rafael Dominiquini <rafaeldominiquini at gmail dor com>
# Contributor: Xyne <xyne at archlinux dot org>

pkgname=bindfs
pkgver=1.17.7
pkgrel=3
pkgdesc="A FUSE filesystem for mirroring a directory to another directory, similar to 'mount --bind', with permission settings."
arch=('i686' 'x86_64' 'armv6h' 'armv6l' 'armv7h')
url="http://bindfs.org/"
license=('GPL')
depends=('fuse3')
source=("http://bindfs.org/downloads/${pkgname}-${pkgver}.tar.gz")
sha512sums=('035593e65b5ffaff77395a6c8c79a6ea01e7702e8f138dfee25342c7e6cd280673cb98c3364f50c036038935a3ace2fa8ebe0713f3bc6b7226fafaf0c9107738')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr

  make CPUOPTIMIZATIONS="${CFLAGS}"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  ln -s bindfs "$pkgdir/usr/bin/mount.bindfs"
  ln -s bindfs "$pkgdir/usr/bin/mount.fuse.bindfs"

  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
