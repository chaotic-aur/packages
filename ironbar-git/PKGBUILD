# Maintainer: aur.chaotic.cx

: ${CARGO_HOME:=$SRCDEST/cargo-home}
: ${CARGO_TARGET_DIR:=target}
: ${RUSTUP_TOOLCHAIN:=stable}
export CARGO_HOME CARGO_TARGET_DIR RUSTUP_TOOLCHAIN

_pkgname="ironbar"
pkgname="$_pkgname-git"
pkgver=0.17.1.r11.gbd6b5a1
pkgrel=1
pkgdesc="Customisable wlroots/sway bar written in rust"
url="https://github.com/JakeStanger/ironbar"
license=('MIT')
arch=('x86_64')

depends=(
  'gtk4-layer-shell'
  'libinput'
  'libpulse'
  'lua51-lgi'
  'luajit'
)
makedepends=(
  'cargo'
  'git'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

options=('!lto')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgsrc"
  cargo build --release --locked
}

package() {
  cd "$_pkgsrc"
  install -Dm755 "$CARGO_TARGET_DIR/release/ironbar" -t "$pkgdir/usr/bin/"

  install -Dm644 "$CARGO_TARGET_DIR/completions/ironbar.bash" "$pkgdir/usr/share/bash-completion/completions/ironbar"

  install -Dm644 "$CARGO_TARGET_DIR/completions/_ironbar" -t "$pkgdir/usr/share/zsh/site-functions/"

  install -Dm644 "$CARGO_TARGET_DIR/completions/ironbar.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"

  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
