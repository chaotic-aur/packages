# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=lcevcdec
pkgver=4.0.5
pkgrel=1
pkgdesc='Low Complexity Enhancement Video Codec Decoder (LCEVC_DEC)'
arch=('x86_64')
url='https://github.com/v-novaltd/LCEVCdec/'
license=('BSD-3-Clause-Clear')
depends=(
  'gcc-libs'
  'glibc')
makedepends=(
  'cmake'
  'git'
  'python'
  'range-v3'
  'rapidjson')
options=('!emptydirs')
source=("git+https://github.com/v-novaltd/LCEVCdec.git#tag=${pkgver}")
sha256sums=('0d3f43dfdf3c46da6b54e10af36dc9225968c64baa68b44461218ada77d4b1dc')

export GIT_LFS_SKIP_SMUDGE='1'

build() {
  export CFLAGS+=' -DNDEBUG -ffat-lto-objects'
  export CXXFLAGS+=' -DNDEBUG -ffat-lto-objects'
  cmake -B build -S LCEVCdec \
    -DCMAKE_BUILD_TYPE:STRING='None' \
    -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
    -DVN_SDK_EXECUTABLES:BOOL='OFF' \
    -DVN_SDK_SIMD:BOOL='OFF' \
    -DVN_SDK_UNIT_TESTS:BOOL='OFF' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  mv "${pkgdir}/usr/share/doc/LCEVCdec_SDK/licenses"/{COPYING,LICENSE.md} "${pkgdir}/usr/share/licenses/${pkgname}"
}
