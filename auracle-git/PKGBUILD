# Maintainer: Leonidas Spyropoulos <artafinde at gmail dot com>

pkgname=auracle-git
_pkgname="${pkgname%-git}"
pkgver=r424.231c554
pkgrel=1
pkgdesc='A flexible client for the AUR'
arch=('x86_64' 'i686')
url="https://github.com/falconindy/auracle"
license=('MIT')
depends=('glaze' 'pacman' 'libcurl.so' 'libsystemd')
makedepends=('meson' 'git' 'perl' 'systemd')
checkdepends=('python' 'fakechroot' 'gtest')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url")
b2sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      echo "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done

  local meson_args=(
    --prefix=/usr
    --libexecdir=lib
    --sbindir=bin
    --buildtype=plain
    --default-library=static
    -Db_pie=true
  )

  [[ -d build ]] && meson_args+=(--wipe)

  meson setup build "${meson_args[@]}"
}

build() {
  cd "$_pkgname"

  meson compile -C build
}

check() {
  cd "$_pkgname"

  meson test -C build
}

package() {
  cd "$_pkgname"

  DESTDIR="$pkgdir" meson install -C build --skip-subprojects
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: et ts=2 sw=2
