# Maintainer: aur.chaotic.cx

_pkgname="shadps4"
pkgname="$_pkgname-git"
pkgver=0.6.0.r219.ga711f4d
pkgrel=1
pkgdesc="PlayStation 4 emulator"
url="https://github.com/shadps4-emu/shadPS4"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'crypto++'
  'glslang'
  'ffmpeg'
  'libfmt.so'
  'xxhash'
  'pugixml'
  'qt6-multimedia'
  'sdl3'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'half'
  'ninja'
  'qt6-tools'
  'rapidjson'
  'robin-map'
  'spirv-headers'
  'stb'
  'toml11'
)

provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")

_source_main() {
  _pkgsrc="$_pkgname"
  source=("$_pkgsrc"::"git+https://github.com/shadps4-emu/shadPS4.git")
  sha256sums=('SKIP')
}

_source_shadps4() {
  local _sources_add=(
    #'cyan4973.xxhash'::'git+https://github.com/Cyan4973/xxHash.git'
    'gpuopen-librariesandsdks.vulkanmemoryallocator'::'git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git'
    'herumi.xbyak'::'git+https://github.com/herumi/xbyak.git'
    #'howardhinnant.date'::'git+https://github.com/HowardHinnant/date.git'
    #'khronosgroup.glslang'::'git+https://github.com/KhronosGroup/glslang.git'
    #'khronosgroup.moltenvk'::'git+https://github.com/KhronosGroup/MoltenVK.git'
    #'khronosgroup.spirv-cross'::'git+https://github.com/KhronosGroup/SPIRV-Cross.git'
    'khronosgroup.vulkan-headers'::'git+https://github.com/KhronosGroup/Vulkan-Headers.git'
    'neargye.magic_enum'::'git+https://github.com/Neargye/magic_enum.git'
    #'pnggroup.libpng'::'git+https://github.com/pnggroup/libpng.git'
    #'rocm.half'::'git+https://github.com/ROCm/half.git'
    #'shadps4-emu.ext-boost'::'git+https://github.com/shadps4-emu/ext-boost.git'
    #'shadps4-emu.ext-cryptopp'::'git+https://github.com/shadps4-emu/ext-cryptopp.git'
    #'shadps4-emu.ext-cryptopp-cmake'::'git+https://github.com/shadps4-emu/ext-cryptopp-cmake.git'
    #'shadps4-emu.ext-cryptoppwin'::'git+https://github.com/shadps4-emu/ext-cryptoppwin.git'
    'shadps4-emu.ext-discord-rpc'::'git+https://github.com/shadps4-emu/ext-discord-rpc.git'
    #'shadps4-emu.ext-ffmpeg-core'::'git+https://github.com/shadps4-emu/ext-ffmpeg-core.git'
    #'shadps4-emu.ext-fmt'::'git+https://github.com/shadps4-emu/ext-fmt.git'
    'shadps4-emu.ext-imgui'::'git+https://github.com/shadps4-emu/ext-imgui.git'
    'shadps4-emu.ext-libatrac9'::'git+https://github.com/shadps4-emu/ext-LibAtrac9.git'
    #'shadps4-emu.ext-sdl'::'git+https://github.com/shadps4-emu/ext-SDL.git'
    #'shadps4-emu.ext-zlib-ng'::'git+https://github.com/shadps4-emu/ext-zlib-ng.git'
    'shadps4-emu.sirit'::'git+https://github.com/shadps4-emu/sirit.git'
    'shadps4-emu.tracy'::'git+https://github.com/shadps4-emu/tracy.git'
    #'shadps4-emu.winpthreads'::'git+https://github.com/shadps4-emu/winpthreads.git'
    #'tessil.robin-map'::'git+https://github.com/Tessil/robin-map.git'
    #'toruniina.toml11'::'git+https://github.com/ToruNiina/toml11.git'
    #'uscilab.cereal'::'git+https://github.com/USCiLab/cereal.git'
    #'zeux.pugixml'::'git+https://github.com/zeux/pugixml.git'
    'zyantific.zydis'::'git+https://github.com/zyantific/zydis.git'
  )

  local _p
  for _p in ${_sources_add[@]}; do
    source+=("$_p")
    sha256sums+=('SKIP')
  done

  _prepare_shadps4() (
    cd "$srcdir/$_pkgsrc"
    local _submodules=(
      #'cyan4973.xxhash'::'externals/xxhash'
      'gpuopen-librariesandsdks.vulkanmemoryallocator'::'externals/vma'
      'herumi.xbyak'::'externals/xbyak'
      #'howardhinnant.date'::'externals/date'
      #'khronosgroup.glslang'::'externals/glslang'
      #'khronosgroup.moltenvk'::'externals/MoltenVK/MoltenVK'
      #'khronosgroup.spirv-cross'::'externals/MoltenVK/SPIRV-Cross'
      'khronosgroup.vulkan-headers'::'externals/vulkan-headers'
      'neargye.magic_enum'::'externals/magic_enum'
      #'pnggroup.libpng'::'externals/libpng'
      #'rocm.half'::'externals/half'
      #'shadps4-emu.ext-boost'::'externals/ext-boost'
      #'shadps4-emu.ext-cryptopp'::'externals/cryptopp'
      #'shadps4-emu.ext-cryptopp-cmake'::'externals/cryptopp-cmake'
      #'shadps4-emu.ext-cryptoppwin'::'externals/cryptoppwin'
      'shadps4-emu.ext-discord-rpc'::'externals/discord-rpc'
      #'shadps4-emu.ext-ffmpeg-core'::'externals/ffmpeg-core'
      #'shadps4-emu.ext-fmt'::'externals/fmt'
      'shadps4-emu.ext-imgui'::'externals/dear_imgui'
      'shadps4-emu.ext-libatrac9'::'externals/LibAtrac9'
      #'shadps4-emu.ext-sdl'::'externals/sdl3'
      #'shadps4-emu.ext-zlib-ng'::'externals/zlib-ng'
      'shadps4-emu.sirit'::'externals/sirit'
      'shadps4-emu.tracy'::'externals/tracy'
      #'shadps4-emu.winpthreads'::'externals/winpthreads'
      #'tessil.robin-map'::'externals/robin-map'
      #'toruniina.toml11'::'externals/toml11'
      #'uscilab.cereal'::'externals/MoltenVK/cereal'
      #'zeux.pugixml'::'externals/pugixml'
      'zyantific.zydis'::'externals/zydis'
    )
    _submodule_update
  )
}

_source_main
_source_shadps4

prepare() {
  _submodule_update() {
    local _module
    for _module in "${_submodules[@]}"; do
      git submodule init "${_module##*::}"
      git submodule set-url "${_module##*::}" "$srcdir/${_module%%::*}"
      git -c protocol.file.allow=always submodule update "${_module##*::}"
    done
  }

  _run_if_exists _prepare_shadps4
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DCMAKE_SKIP_RPATH=ON
    -DENABLE_QT_GUI=ON
    -DENABLE_UPDATER=OFF
    -DSIRIT_USE_SYSTEM_SPIRV_HEADERS=ON
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

_run_if_exists() {
  if declare -F "$1" > /dev/null; then
    eval "$1"
  fi
}
