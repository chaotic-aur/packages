# Maintainer: Stephan Springer <buzo+arch@Lini.de>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>
# Contributor: Davi da Silva BÃ¶ger <dsboger@gmail.com>

pkgname=gcdemu
pkgver=3.3.0
pkgrel=1
pkgdesc="GNOME panel applet controlling cdemu-daemon"
arch=('any')
url="http://cdemu.sourceforge.net/"
license=(GPL-2.0-only)
depends=(cdemu-daemon python python-gobject gdk-pixbuf2 glib2 gtk3 libappindicator libnotify)
makedepends=(cmake intltool)
source=("https://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.xz")
sha256sums=('0bd77846fe3b91086cda46d30b001471d9be75c96303c21593084b247269512d')

build() {
  mkdir "$srcdir/build"
  cd "$srcdir/build"
  cmake -DPOST_INSTALL_HOOKS=off -DCMAKE_INSTALL_PREFIX=/usr ../"$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}
