# Maintainer: Cabel <mxzcabel at tuta dot com>
# Contributor: metiis <aur at metiis dot com>
# Contributor: Julio Gutierrez <bubuntux at gmail dot com>
# Contributor: Martoko <mbastholm at gmail dot com>

pkgname=nordvpn-bin
pkgver=4.1.1
pkgrel=1
pkgdesc="NordVPN CLI tool for Linux"
arch=('x86_64' 'i686' 'armv7h' 'aarch64' 'armeabi')
url="https://nordvpn.com/download/linux/"
license=('GPL3')
depends=('libxml2-legacy')
provides=('nordvpn')
conflicts=('openvpn-nordvpn')
install=nordvpn-bin.install
source_x86_64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_amd64.deb")
source_i686=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_i386.deb")
source_armv7h=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_armhf.deb")
source_aarch64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_arm64.deb")
source_armeabi=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_armel.deb")
sha256sums_x86_64=('256818d15d866c0f5f274d5586219f52f5736aefc52ce77f903bb69d0deb32e6')
sha256sums_i686=('fa9162eaade6a8be8bebc662364a65302fb0a0e5a30c638976857e9cb96b6ad0')
sha256sums_armv7h=('44fc88c0b773e5a49f56ed65274443d0120776f9c0842e0340de2d319b14486a')
sha256sums_aarch64=('c139d2facf214298ed0823124c794226df994739affb736ca1f20cf23b12e4ef')
sha256sums_armeabi=('eefd98dc162de321189e67a2415a4805315e225622fbabd4dc4f28778a56a4e6')

package() {
  bsdtar -O -xf *.deb data.tar.gz | bsdtar -C "${pkgdir}" -xJf -

  mv "${pkgdir}/usr/sbin/nordvpnd" "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/etc/init.d"
  rm -r "${pkgdir}/usr/sbin"
  echo "g nordvpn - -" | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
