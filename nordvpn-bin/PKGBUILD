# Maintainer: Cabel <mxzcabel at proton dot me>
# Contributor: metiis <aur at metiis dot com>
# Contributor: Julio Gutierrez <bubuntux at gmail dot com>
# Contributor: Martoko <mbastholm at gmail dot com>

pkgname=nordvpn-bin
pkgver=3.18.3
pkgrel=1
pkgdesc="NordVPN CLI tool for Linux"
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url="https://nordvpn.com/download/linux/"
license=('GPL3')
depends=('libxml2')
provides=('nordvpn')
conflicts=('openvpn-nordvpn')
install=nordvpn-bin.install
source_x86_64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/nordvpn_${pkgver//_/-}_amd64.deb")
source_i686=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/nordvpn_${pkgver//_/-}_i386.deb")
source_armv7h=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/nordvpn_${pkgver//_/-}_armhf.deb")
source_aarch64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/nordvpn_${pkgver//_/-}_arm64.deb")
sha256sums_x86_64=('a42bde37c704c045ddbd68f61406adce0f3d7d32d5f5e61e84465f2aae4975a6')
sha256sums_i686=('35c6f6973c5ee69fdac88ed658f6d38ac6bfc7457c01158005860fd2c5d2892d')
sha256sums_armv7h=('5d4e9147564cc38fdaf02891bb55b0e13c3f3a1ce2971cb9142a260a1ae32cda')
sha256sums_aarch64=('14a649001aa9d1ce3004514f1536f26a84ad20ec17d2b87eece480f7e8309210')

# There is also ArmEABI architecture for android devices available. Download with:
# https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/nordvpn_3.18.3_armel.deb

package() {
  bsdtar -O -xf *.deb data.tar.gz | bsdtar -C "${pkgdir}" -xJf -

  mv "${pkgdir}/usr/sbin/nordvpnd" "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/etc/init.d"
  rm -r "${pkgdir}/usr/sbin"
  echo "g nordvpn - -" | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
