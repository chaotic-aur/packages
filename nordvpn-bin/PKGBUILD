# Maintainer: Cabel <mxzcabel at proton dot me>
# Contributor: metiis <aur at metiis dot com>
# Contributor: Julio Gutierrez <bubuntux at gmail dot com>
# Contributor: Martoko <mbastholm at gmail dot com>

pkgname=nordvpn-bin
pkgver=3.19.1
pkgrel=1
pkgdesc="NordVPN CLI tool for Linux"
arch=('x86_64' 'i686' 'armv7h' 'aarch64' 'armeabi')
url="https://nordvpn.com/download/linux/"
license=('GPL3')
depends=('libxml2')
provides=('nordvpn')
conflicts=('openvpn-nordvpn')
install=nordvpn-bin.install
source_x86_64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_amd64.deb")
source_i686=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_i386.deb")
source_armv7h=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_armhf.deb")
source_aarch64=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_arm64.deb")
source_armeabi=("https://repo.nordvpn.com/deb/nordvpn/debian/pool/main/n/nordvpn/nordvpn_${pkgver//_/-}_armel.deb")
sha256sums_x86_64=('49911163674b3782a1c1ab59b4801ceb7423fe2c85dd4f9855fb440543584569')
sha256sums_i686=('e59651e968ae686f34010e33ca5cc1dbe03f3cf2008d04d45aa1e1809b06744e')
sha256sums_armv7h=('49de0de152182e5784b01e71986eb118205043a6f625fa159d161887ed05801d')
sha256sums_aarch64=('36a302dacd147b17c70d49e5a649c5a4597333941f348a563c2073b9e4f42557')
sha256sums_armeabi=('c9fbbf5e763e2beaa1411d6d8cd5bfc5c3cf0e1925cc2a9036d62f1ee700152f')

package() {
  bsdtar -O -xf *.deb data.tar.gz | bsdtar -C "${pkgdir}" -xJf -

  mv "${pkgdir}/usr/sbin/nordvpnd" "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/etc/init.d"
  rm -r "${pkgdir}/usr/sbin"
  echo "g nordvpn - -" | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
