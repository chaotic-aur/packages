# Maintainer:

_pkgname="image-roll"
pkgname="$_pkgname"
pkgver=2.1.0
pkgrel=1
pkgdesc="GTK image viewer with basic image manipulation tools"
url="https://github.com/weclaw1/image-roll"
license=('MIT')
arch=('x86_64')

  depends=(
    gtk4
  )
  makedepends=(
    cargo
    git
  )

_source_main() {
  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git#tag=${_pkgver:?}")
  sha256sums+=('SKIP')
}

_rust_env() {
  : ${CARGO_HOME:=$SRCDEST/cargo-home}
  export CARGO_HOME
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
}

prepare() {
  _rust_env

  cd "$_pkgsrc"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
  echo "${_pkgver:?}"
}

build() {
  _rust_env

  cd "$_pkgsrc"
  cargo build --release --frozen --all-features
}

check() {
  _rust_env

  cd "$_pkgsrc"
  cargo test --tests -- --skip current_file || true
}

package() {
  _rust_env

  cd "$_pkgsrc"
  install -Dm755 "$CARGO_TARGET_DIR/release/image-roll" -t "$pkgdir/usr/bin/"

  install -Dm644 "src/resources/com.github.weclaw1.ImageRoll.desktop" \
    -t "$pkgdir/usr/share/applications/"

  install -Dm644 "src/resources/com.github.weclaw1.ImageRoll.svg" -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/"

  install -Dm644 "src/resources/com.github.weclaw1.ImageRoll.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

_update_version() {
  : ${_pkgver:=${pkgver%%.r*}}

  if [[ x"${_autoupdate::1}" != "xt" ]]; then
    return
  fi

  _repo="${url#*//*/}"
  _response=$(curl "https://api.github.com/repos/${_repo:?}/tags" -s)

  _get() {
    printf '%s' "$_response" \
      | awk -F '"' '/"'"$1"'":/{print $4}' \
      | sed 's@^.*[a-z-].*$@@' | sort -rV | head -1
  }
  _pkgver_new=$(_get name)

  # update pkgver
  if [ x"$_pkgver" != x"${_pkgver_new:?}" ]; then
    _pkgver="${_pkgver_new:?}"
  fi
}

_update_version
_source_main
