# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: LLL2yu <lll2yu@protonmail.com>

pkgname=gallery-dl
pkgver=1.27.0
pkgrel=1
pkgdesc='Command-line program to download image-galleries and collections from several image hosting sites'
arch=(any)
url=https://github.com/mikf/gallery-dl
license=(GPL-2.0-or-later)
depends=(python python-requests)
makedepends=(python-build python-installer python-setuptools python-wheel git)
checkdepends=(yt-dlp)
optdepends=('ffmpeg: Pixiv Ugoira conversion'
  'yt-dlp: Video downloads'
  'youtube-dl: Video downloads'
  'python-pysocks: SOCKS proxy support'
  'python-brotli: Brotli compression support'
  'python-brotlicffi: Brotli compression support'
  'python-yaml: YAML configuration file support'
  'python-toml: TOML configuration file support for Python<3.11'
  'python-secretstorage: GNOME keyring passwords for --cookies-from-browser')
source=(git+"${url}".git#tag=v"${pkgver}"?signed)
validpgpkeys=(3E09F5908333DD83DBDCE7375680CA389D365A88) #Mike FÃ¤hrmann
sha512sums=('da790185e8bf22aed4256cfff4f30ff9bc514442598b927d6f4bdd8a93ffbc6ec39677b51b2805340aef05533f3362de6568c45ecc51995f3488c1819f1589d9')

prepare() {
  # Clean out old wheels etc.
  git -C "${pkgname}" clean -dfx

  git -C "${pkgname%-git}" cherry-pick -n 5227bb6b1d62ecef5b281592b0d001e7f9c101e3 # [text] catch general Exceptions

}

build() {
  cd ${pkgname}
  make
  python -m build --wheel --no-isolation
}

check() {
  make -C ${pkgname} test
}

package() {
  cd ${pkgname}
  python -m installer --destdir="$pkgdir" dist/*.whl
}
