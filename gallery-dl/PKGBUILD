# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: LLL2yu <lll2yu@protonmail.com>

pkgname=gallery-dl
pkgver=1.31.2
pkgrel=1
pkgdesc='Command-line program to download image-galleries and collections from several image hosting sites'
arch=(any)
url=https://github.com/mikf/gallery-dl
license=(GPL-2.0-or-later)
depends=(python python-requests)
makedepends=(python-build python-installer python-setuptools python-wheel git)
checkdepends=(yt-dlp)
optdepends=(
  'yt-dlp: HLS/DASH video downloads, ytdl integration'
  'youtube-dl: HLS/DASH video downloads, ytdl integration'
  'ffmpeg: Pixiv Ugoira conversion'
  'mkvtoolnix-cli: Accurate Ugoira frame timecodes'
  'python-pysocks: SOCKS proxy support'
  'python-brotli: Brotli compression support'
  'python-brotlicffi: Brotli compression support'
  'python-zstandard: Zstandard compression support'
  'python-yaml: YAML configuration file support'
  'python-toml: TOML configuration file support for Python<3.11'
  'python-secretstorage: GNOME keyring passwords for --cookies-from-browser'
  'python-psycopg: PostgreSQL support'
  'python-truststore: Support using system certificates'
  'python-jinja: Jinja template support'
)
source=(git+"${url}".git#tag=v"${pkgver}"?signed)
validpgpkeys=(3E09F5908333DD83DBDCE7375680CA389D365A88) #Mike FÃ¤hrmann
sha512sums=('7890d633ab5a2dd63bfcab45c88561bb62b002f56e9959f199eb5e73742b5fabbe0ce9ee6e9835a418a7f464e3952238756219bc20f094a1fe805f92a9fe71c5')

prepare() {
  # Clean out old wheels etc.
  git -C "${pkgname}" clean -dfx
}

build() {
  cd ${pkgname}
  make
  python -m build --wheel --no-isolation
}

check() {
  make -C ${pkgname} test
}

package() {
  cd ${pkgname}
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" docs/{configuration.rst,formatting.md,gallery-dl-example.conf} CHANGELOG.md
}
