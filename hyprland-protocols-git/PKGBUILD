# Maintainer: TheK0tYaRa
# Inspired by: FabioLolix, Ã©clairevoyant, ThatOneCalculator <kainoa at t1c dot dev>

pkgname=hyprland-protocols-git
pkgver=0.4.0.r1.a7c1838
pkgrel=1
pkgdesc="Wayland protocol extensions for Hyprland"
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/hyprland-protocols"
license=(BSD-3-Clause)
makedepends=(
  git
  meson
  ninja
)
provides=("${pkgname%%-git}=${pkgver%%.r*}")
conflicts=(${pkgname%%-git})
source=(
  "git+${url}.git"
)
b2sums=(
  'SKIP'
)

pkgver() {
  git -C ${pkgname%%-git} describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd ${pkgname%%-git}

  meson setup build \
    --wipe \
    --prefix /usr \
    --libexecdir lib \
    --buildtype release \
    --wrap-mode nodownload

  meson compile -C build
}

package() {
  cd ${pkgname%%-git}

  meson install -C build --destdir "$pkgdir"

  # license
  install -Dm0644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
# vi: et ts=2 sw=2
