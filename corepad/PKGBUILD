# Maintainer: Dan Johansen <strit@strits.dk>
# Contributor: Shaber

pkgname=corepad
pkgver=5.0.0
pkgrel=1
pkgdesc="A document editor from the C Suite"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/cubocore/coreapps/$pkgname"
license=('GPL-3.0-or-later')
depends=(
  'qt6-base'
  'hicolor-icon-theme'
  'libcprime'
)
makedepends=(
  'cmake'
  'ninja'
)
groups=('coreapps')
source=("https://gitlab.com/cubocore/coreapps/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('47ffc6e165ed6bc8617cd85d43b44d9e0d90d8b181bb2f8ccb06166508b33791')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
