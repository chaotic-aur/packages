# Maintainer: Sainnhe Park <sainnhe@gmail.com>
_variant='vim'
_extname=coc-vimlsp
_packdir="usr/share/vim/vimfiles/pack/coc/start/${_extname}"
url='https://github.com/iamcco/coc-vimlsp'
pkgdesc='VimL support for coc.nvim'

pkgname="${_variant}-${_extname}-git"
provides=("${_variant}-${_extname}")
conflicts=("${_variant}-${_extname}")
arch=('any')
depends=("${_variant}-coc")
makedepends=('yarn' 'npm' 'git')
license=('unknown')
source=("${_extname}::git+${url}.git")
pkgver=0.8.0.r50.g9e88f05
pkgrel=1
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_extname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_extname}"
  # openssl legacy provider is required for the moment (2023.09.19)
  NODE_OPTIONS=--openssl-legacy-provider yarn install --frozen-lockfile --preferred-cache-folder "${srcdir}/.cache"
}

package() {
  cd "${srcdir}/${_extname}"
  yarn pack
  tar xvf *.tgz
  rm *.tgz
  cd package
  _dependencies=$(grep -Po '"dependencies":' package.json) || _dependencies=""
  if [ -n "${_dependencies}" ]; then
    yarn install --production --no-lockfile --ignore-scripts --prefer-offline --preferred-cache-folder "${srcdir}/.cache"
  fi
  find . -type f -exec \
    install -Dm 644 '{}' "${pkgdir}/${_packdir}/{}" \;
  rm -rf "${srcdir}/${_extname}/package"
  find "$pkgdir" -name package.json -print0 | xargs -r -0 sed -i '/_where/d'
  chown -R root:root "${pkgdir}"
}
