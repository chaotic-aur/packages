# Maintainer:

_pkgname="gimp"
pkgname="$_pkgname-git"
pkgver=2.99.18.r996.gff9f71e
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
url="https://gitlab.gnome.org/GNOME/gimp"
license=('GPL-3.0-or-later')
arch=('i686' 'x86_64')

depends=(
  'appstream-glib'
  'babl'
  'cairo'
  'dbus-glib'
  'desktop-file-utils'
  'enchant'
  'gegl'
  'gobject-introspection'
  'gtk-doc'
  'icu'
  'lcms2'
  'libart-lgpl'
  'libexif'
  'libgexiv2'
  'librsvg'
  'libwmf'
  'mypaint-brushes1'
  'openexr'
  'poppler-data'
  'poppler-glib'
)
makedepends=(
  'aalib'
  'alsa-lib'
  'curl'
  'ghostscript'
  'git'
  'gjs'
  'glib-networking'
  'glib2-devel'
  'intltool'
  'iso-codes'
  'libheif'
  'libmng'
  'libwebp'
  'libxpm'
  'libxslt'
  'luajit'
  'meson'
  'python-gobject'
  'python-packaging'
  'webkit2gtk'
  'zlib'

  'cfitsio'
  'vala'
)
checkdepends=(
  'xorg-server-xvfb'
)
optdepends=(
  'aalib: ASCII art support'
  'alsa-lib: for MIDI event controller module'
  'curl: for URI support'
  'ghostscript: for postscript support'
  'gjs: JavaScript scripting support'
  'gutenprint: for sophisticated printing only as gimp has built-in cups print support'
  'iso-codes: Language support'
  'libheif: HEIF support'
  'libjxl: JPEG XL support'
  'libmng: MNG support'
  'libwebp: WebP support'
  'libxpm: XPM support'
  'luajit: LUA scripting support'
)

_pkgsrc="gimp"
source=(
  "$_pkgsrc"::"git+https://gitlab.gnome.org/GNOME/gimp.git"
  'gimp-data'::'git+https://gitlab.gnome.org/GNOME/gimp-data.git'
)
sha256sums=(
  'SKIP'
  'SKIP'
)

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/[-_]/./g'
}

prepare() {
  local _submodules _module

  cd "$_pkgsrc"
  _submodules=(
    'gimp-data'::'gimp-data'
  )
  for _module in "${_submodules[@]}"; do
    git submodule init "${_module##*::}"
    git submodule set-url "${_module##*::}" "$srcdir/${_module%::*}"
    git -c protocol.file.allow=always submodule update "${_module##*::}"
  done

  # fix batch-interpreter name
  sed -E -e 's&"python-fu-eval"&"plug-in-script-fu-eval"&' -i "gimp-data/images/meson.build"
}

build() {
  local meson_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --libexecdir=/usr/bin
    -Dappdata-test=disabled
    -Dcheck-update=no
    -Dg-ir-doc=false
    -Dgi-docgen=disabled
    -Dheadless-tests=enabled
    -Dilbm=disabled
  )

  arch-meson "$_pkgsrc" build "${meson_options[@]}"
  meson compile -C build
}

check() {
  ninja -C "build" test || :
}

package() {
  local _pkgver=$(sed -E 's@^([0-9]+\.[0-9]+).*$@\1@' <<< ${pkgver})

  meson install -C build --destdir "$pkgdir"

  # .desktop
  sed \
    -e 's/^\(Name.*\)/\1 (devel)/g' \
    -e 's/Icon=gimp/Icon=gimp-'"${_pkgver:?}"'/g' \
    -i "$pkgdir/usr/share/applications/gimp.desktop"
  mv "$pkgdir/usr/share/applications"/gimp{,-${_pkgver:?}}.desktop

  # appdata.xml
  sed \
    -e 's/org.gimp.GIMP/org.gimp.GIMP-'"${_pkgver:?}"'/g' \
    -e 's/gimp.desktop/gimp-'"${_pkgver:?}"'.desktop/g' \
    -i "$pkgdir/usr/share/metainfo/org.gimp.GIMP.appdata.xml"
  mv "$pkgdir/usr/share/metainfo"/org.gimp.GIMP{,-${_pkgver:?}}.appdata.xml

  # icons
  for i in 16 22 24 32 48 256; do
    mv "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps"/gimp{,-${_pkgver:?}}.png
  done

  # conflicts
  local _conflicts=(
    usr/bin/gimp
    usr/bin/gimp-console
    usr/bin/gimptool
    usr/share/man/man1/gimp-console.1
    usr/share/man/man1/gimp.1
    usr/share/man/man1/gimptool.1
    usr/share/man/man5/gimprc.5
  )

  for i in "${_conflicts[@]}"; do
    rm -rf "$pkgdir/$i"
  done

  # palette
  install -Dm644 /dev/stdin -t "$pkgdir/usr/share/gimp/${_pkgver:?}/palettes/" << 'END'
GIMP Palette
Name: linux
#
  0   0   0
  0   0 170
  0 170   0
  0 170 170
170   0   0
170   0 170
170  85   0
170 170 170
 85  85  85
 85  85 255
 85 255  85
 85 255 255
255  85  85
255  85 255
255 255  85
255 255 255
END
}
