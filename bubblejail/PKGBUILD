# Maintainer: aur.chaotic.cx

_pkgname="bubblejail"
pkgname="$_pkgname"
pkgver=0.10.1
pkgrel=2
pkgdesc="Bubblewrap based sandboxing utility"
url="https://github.com/igo95862/bubblejail"
license=('GPL-3.0-or-later')
arch=('x86_64')

depends=(
  'bubblewrap'
  'desktop-file-utils'
  'hicolor-icon-theme'
  'libnotify'
  'python'
  'python-cattrs'
  'python-pyqt6'
  'python-pyxdg'
  'python-tomli-w'
  'xdg-dbus-proxy'
)
makedepends=(
  'git'
  'meson'
  'python-jinja'
  'scdoc'

  # For python-lxns
  'linux-headers'
)
optdepends=(
  'bash-completion: completions for bash shell'
  'fish: completions for fish shell'
  'slirp4netns: Namespaced networking stack'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=(
  "$_pkgsrc"::"git+$url.git#tag=$pkgver"
  "0096-expandvars-0.patch"::"https://github.com/igo95862/bubblejail/pull/96.diff"
)
sha256sums=(
  'SKIP'
  'SKIP'
)

prepare() {
  cd "$_pkgsrc"
  patch -Np1 -F100 -i "$srcdir/0096-expandvars-0.patch"

  # change notification priority
  sed -E \
    -e '/--urgency/d' \
    -e '/critical/d' \
    -i "src/bubblejail/bubblejail_cli.py"

  # enable extra features
  sed -E -e '/flags\s?=\s?ServiceFlags.(EXPERIMENTAL|NO_GUI)/d' \
    -i "src/bubblejail/services.py"
}

build() {
  local opts=(
    -Dversion_display="'AUR $pkgver'"
    -Duse-vendored-python-lxns=enabled
  )

  arch-meson "${opts[@]}" "$_pkgsrc" build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
