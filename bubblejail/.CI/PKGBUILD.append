makedepends+=('git')

license=('GPL-3.0-or-later')

source=(
  "$pkgname"::"git+https://github.com/igo95862/bubblejail#tag=$pkgver"
  "0096-expandvars-0.patch"::"https://github.com/igo95862/bubblejail/pull/96.diff"
)
sha512sums=(
  'SKIP'
  'SKIP'
)

prepare() {
  cd "$pkgname"
  patch -Np1 -F100 -i "$srcdir/0096-expandvars-0.patch"

  # change notification priority
  sed -E \
    -e '/--urgency/d' \
    -e '/critical/d' \
    -i "src/bubblejail/bubblejail_cli.py"

  # enable advanced features
  sed -E -e 's@^(\s+display_in_gui) = False$@\1 = True@' \
    -i "src/bubblejail/services.py"
}

build() {
  local opts=(
    -Dversion_display="'AUR $pkgver'"
    -Duse-vendored-python-lxns=enabled
  )

  arch-meson "${opts[@]}" "$pkgname" build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
