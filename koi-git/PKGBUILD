# Maintainer: aur.chaotic.cx

_pkgname="koi"
pkgname="$_pkgname-git"
pkgver=0.6.r0.gbe28f61
pkgrel=1
pkgdesc="Scheduled light/dark theme switcher for KDE Plasma"
url="https://github.com/baduhai/Koi"
license=('LGPL-3.0-only')
arch=('x86_64')

depends=(
  'hicolor-icon-theme'
  'kcoreaddons'
  'kwidgetsaddons'
  'qt6-base'
  'qt6-svg'
)
makedepends=(
  'cmake'
  'git'
  'kconfig'
  'ninja'
)

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
