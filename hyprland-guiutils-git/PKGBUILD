# Maintainer: alba4k <blaskoazzolaaaron[at]gmail.com>

_pkgname="hyprland-guiutils"
pkgname="$_pkgname-git"
pkgver=0.1.0.r0.ga415eba
pkgrel=1
pkgdesc="Hyprland GUI utilities (successor to hyprland-qtutils)"
arch=('x86_64' 'aarch64')
url="https://github.com/hyprwm/hyprland-guiutils"
license=('BSD-3-Clause')

depends=(
  hyprlang-git
  hyprtoolkit-git
  hyprutils-git
  libdrm
  pixman
)
makedepends=(
  cmake
  git
  ninja
)

provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")
replaces=('hyprland-qtutils' 'hyprland-qtutils-git')

_pkgsrc=$_pkgname
source=("$_pkgsrc::git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  (
    set -o pipefail # CHANGE THIS ONCE A TAGGED RELEASE COMES OUT
    git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' \
      || printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  local cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
