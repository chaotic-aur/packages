# Maintainer: aur.chaotic.cx

_pkgname="smenu"
pkgname="$_pkgname"
pkgver=1.5.0
pkgrel=1
pkgdesc="A powerful and versatile selection tool for interactive or scripting use"
arch=('x86_64')
url="https://github.com/p-gen/smenu"
license=('MPL-2.0')
depends=('ncurses')

_pkgsrc="$pkgname-$pkgver"
_pkgext="tar.gz"
source=("$_pkgsrc.$_pkgext"::"$url/archive/v$pkgver.$_pkgext")
sha256sums=('SKIP')

build() {
  cd "$_pkgsrc"
  ./configure --prefix="/usr"
  make
}

package() {
  cd "$_pkgsrc"
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  find examples/ -type f -exec install -Dm644 "{}" "$pkgdir/usr/share/doc/$pkgname/{}" \;
}
