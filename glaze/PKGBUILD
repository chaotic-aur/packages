#!/hint/bash
# shellcheck disable=SC2034 disable=SC2154

# Maintainer: Michael Bolden Jnr / SM9(); <me@sm9.dev>

pkgname="glaze"
pkgver=4.2.2
pkgrel=1
pkgdesc="An Extremely fast, In-Memory JSON and Interface Library for Modern C++"
arch=('any')
url="https://github.com/stephenberry/glaze"
license=('MIT')
makedepends=('cmake' 'ninja')
optdepends=('clang: for building with clang' 'gcc: for building with gcc')
provides=("${pkgname}")
conflicts=("${pkgname}")

source=(
  "${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
)

sha512sums=('e1e1f0aff6b352ba22a8cd6a5871f8f0b5269930cd7cad77f4df199bb3e7e27600125512b804bd5d1c15d4f36852e59fa2a02845f7eb2bd976360d3383e9309f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}" || return 1
  rm -rf build && mkdir -p build || return 1
}

build() {
  cd "${pkgname}-${pkgver}/build" || return 1

  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -G "Ninja" .. || return 1

  ninja || return 1
}

package() {
  cd "${pkgname}-${pkgver}/build" || return 1

  DESTDIR="${pkgdir}" ninja install || return 1
}
