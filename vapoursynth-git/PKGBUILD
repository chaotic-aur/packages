# Maintainer:

_pkgname="vapoursynth"
pkgname="$_pkgname-git"
pkgver=R68.r10.g269ac37
pkgrel=1
pkgdesc="A video processing framework with simplicity in mind"
url="https://github.com/vapoursynth/vapoursynth"
arch=('x86_64')
license=(
  'LGPL-2.1-or-later'
  #'OFL-1.1-RFN' # Terminus
  #'WTFPL' # VSHelper headers
)

depends=(
  libzimg.so
  python
)
makedepends=(
  cython
  git
  python-sphinx
)

provides=(
  "vapoursynth=${pkgver%%.r*}"
  'libvapoursynth.so'
)
conflicts=('vapoursynth')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+https://github.com/vapoursynth/vapoursynth.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgsrc"
  ./autogen.sh
}

build() {
  cd "$_pkgsrc"
  ./configure \
    --prefix=/usr \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$_pkgsrc"

  make DESTDIR="${pkgdir}" install

  install -Dm644 /dev/stdin "${pkgdir}/usr/share/mime/packages/vapoursynth.xml" << END
<?xml version="1.0"?>
<mime-info xmlns='http://www.freedesktop.org/standards/shared-mime-info'>
  <mime-type type="text/x-vpy">
    <sub-class-of type="text/x-python"/>
    <comment>VapourSynth script</comment>
    <glob pattern="*.vpy"/>
  </mime-type>
</mime-info>
END
}
