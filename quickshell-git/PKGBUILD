# Maintainer: aur.chaotic.cx

: ${_distributor=chaotic-aur/quickshell-git}

_pkgname="quickshell"
pkgname="$_pkgname-git"
pkgver=0.2.1.r13.gc9d3ffb
pkgrel=1
pkgdesc="Simple and flexbile QtQuick based desktop shell toolkit"
url='https://git.outfoxxed.me/quickshell/quickshell'
license=('LGPL-3.0-only')
arch=('x86_64')

depends=(
  'jemalloc'
  'libdrm'
  'libglvnd'
  'libpipewire'
  'libxcb'
  'mesa'
  'pam'
  'qt6-base'
  'qt6-declarative'
  'qt6-svg'
  'wayland'
)
makedepends=(
  'cli11'
  'cmake'
  'git'
  'ninja'
  'qt6-shadertools'
  'wayland-protocols'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

install="$_pkgname.install"

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() (
  cd "$_pkgsrc"
  local _tmp _tag _version _revision _hash
  _tmp=$(git tag | grep -Ev '[A-Za-z][A-Za-z]' | sed -E 's&([^0-9]*)(\S+)$&\2 \1\2&' | sort -rV | head -1)
  _version=$(cut -f1 -d' ' <<< ${_tmp:?})
  _tag=$(cut -f2 -d' ' <<< ${_tmp:?})
  _revision=$(git rev-list --count --cherry-pick "$_tag"...HEAD)
  _commit=$(git rev-parse --short=7 HEAD)
  printf '%s.r%s.g%s' "${_version:?}" "${_revision:?}" "${_commit:?}"
)

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DCRASH_REPORTER=OFF
    -DDISTRIBUTOR="${_distributor:-aur/$pkgname}"
    -DDISTRIBUTOR_DEBUGINFO_AVAILABLE=NO
    -DINSTALL_QML_PREFIX=lib/qt6/qml
    -DBUILD_TESTING=$CHECKFUNC
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  local i _ver
  for i in qt6-base; do
    local _ver=$(LC_ALL=C pacman -Si extra/$i | grep -Pom1 '^Version\s+:\s+\K\S+(?=-[0-9])')
    [ -n "$_ver" ] && depends=(${depends[@]//$i*/$i=$_ver})
  done

  DESTDIR="$pkgdir" cmake --install build
}
