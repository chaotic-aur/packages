# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=insomnium
pkgver=0.2.3_a
_electronversion=25
_nodeversion=18
pkgrel=8
pkgdesc="A fast local API testing tool that is privacy-focused and 100% local. For testing GraphQL, REST, WebSockets and gRPC.This is a fork of Kong/insomnia"
arch=('any')
url="https://archgpt.dev/insomnium"
_ghurl="https://github.com/ArchGPT/insomnium"
license=('MIT')
conflicts=("${pkgname}")
depends=(
  "electron${_electronversion}-bin"
  'nodejs'
)
makedepends=(
  'gendesk'
  'npm'
  'nvm'
  'git'
)
source=(
  "${pkgname}.git::git+${_ghurl}.git#tag=core@${pkgver//_/-}"
  "${pkgname}.sh"
)
sha256sums=('e2cd1141ddb8b65ccf9ec886993e6d0440f2d02d64f2ae64236028188e323597'
  'dc0c5ca385ad81a08315a91655c7c064b5bf110eada55e61265633ae198b39f8')
_ensure_local_nvm() {
  export NVM_DIR="${srcdir}/.nvm"
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
  nvm install "${_nodeversion}"
  nvm use "${_nodeversion}"
}
build() {
  sed -e "s|@electronversion@|${_electronversion}|" \
    -e "s|@appname@|${pkgname%-git}|g" \
    -e "s|@runname@|app.asar|g" \
    -e "s|@options@||g" \
    -i "${srcdir}/${pkgname%-git}.sh"
  _ensure_local_nvm
  gendesk -q -f -n --categories="Development" --name="${pkgname%-git}" --exec="${pkgname%-git} %U"
  cd "${srcdir}/${pkgname}.git"
  export npm_config_build_from_source=true
  export npm_config_cache="${srcdir}/.npm_cache"
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  export SYSTEM_ELECTRON_VERSION="$(electron${_electronversion} -v | sed 's/v//g')"
  export npm_config_target="${SYSTEM_ELECTRON_VERSION}"
  export ELECTRONVERSION="${_electronversion}"
  export npm_config_disturl=https://electronjs.org/headers
  HOME="${srcdir}/.electron-gyp"
  if [ $(curl -s ipinfo.io/country | grep CN | wc -l) -ge 1 ]; then
    echo 'registry="https://registry.npmmirror.com/"' >> .npmrc
    echo 'electron_mirror="https://registry.npmmirror.com/-/binary/electron/"' >> .npmrc
    echo 'electron_builder_binaries_mirror="https://registry.npmmirror.com/-/binary/electron-builder-binaries/"' >> .npmrc
  else
    echo "Your network is OK."
  fi
  sed "s|electron-builder build|electron-builder build --dir|g" -i packages/insomnia/package.json
  npm ci
  npm run app-package
}
package() {
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.git/packages/insomnia/dist/linux-"*/resources/app.asar -t "${pkgdir}/usr/lib/${pkgname}"
  install -Dm755 "${srcdir}/${pkgname}.git/packages/insomnia/dist/linux-"*/resources/bin/yarn-standalone.js -t "${pkgdir}/usr/lib/${pkgname}/bin"
  install -Dm644 "${srcdir}/${pkgname}.git/packages/insomnia/dist/linux-"*/resources/opensource-licenses.txt -t "${pkgdir}/usr/lib/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/${pkgname}.git/packages/insomnia/build/static/insomnia-core-logo_16x.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.git/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
