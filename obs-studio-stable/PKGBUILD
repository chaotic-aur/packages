# Maintainer: aur.chaotic.cx

## links
# https://obsproject.com
# https://github.com/obsproject/obs-studio
# https://github.com/obsproject/cef

## options
: ${_autoupdate:=true}

: ${_plugin_aja:=false}
: ${_plugin_webrtc:=false}

# build-aux/modules/99-cef.json
: ${_cef_branch:=5060}
: ${_cef_ver=_v3}

## basic info
_pkgname="obs-studio"
pkgname="$_pkgname-stable"
pkgver=30.2.3
pkgrel=1
pkgdesc="Free, open source software for live streaming and recording - Browser enabled"
url="https://github.com/obsproject/obs-studio"
license=("GPL-2.0-or-later")
arch=("x86_64")

depends=(
  curl
  ffmpeg
  jack
  jansson
  libxcomposite
  mbedtls
  pciutils
  qt6-svg
  rnnoise

  librist
  libvpl
  speexdsp

  qrcodegencpp-cmake # AUR
)
makedepends=(
  asio
  cmake
  #ffnvcodec-headers
  git
  libfdk-aac
  luajit
  nlohmann-json
  python
  qt6-wayland
  sndio
  swig
  uthash
  vlc
  wayland
  websocketpp
  x264
  xdg-desktop-portal
)
optdepends=(
  'libfdk-aac: FDK AAC codec support'
  'libva-intel-driver: hardware encoding'
  'libva-mesa-driver: hardware encoding'
  'luajit: scripting support'
  'python: scripting support'
  'sndio: sndio input client'
  'v4l2loopback-dkms: virtual camera support'

  'vlc: VLC Media Source'
)

provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")

options=(!debug)

_source_main() {
  _pkgsrc="$_pkgname"
  source=("$_pkgsrc"::"git+$url.git#tag=$_pkgver")
  sha256sums=('SKIP')

  if [ "${_plugin_aja::1}" == "t" ]; then
    makedepends+=('libajantv2') # AUR
    _plugin_aja='ON'
  else
    _plugin_aja='OFF'
  fi

  if [ "${_plugin_webrtc::1}" == "t" ]; then
    makedepends+=('libdatachannel') # AUR
    _plugin_webrtc='ON'
  else
    _plugin_webrtc='OFF'
  fi
}

_source_fixes() {
  local _patch_commit="19f9097dd3803abf8296acac36c7ab4a6b684a3f"
  source+=(
    "nv-codec-headers"::"git+https://github.com/FFmpeg/nv-codec-headers.git#tag=n12.1.14.0"
    "0004-mbedtls-${_patch_commit::7}.patch"::"https://aur.archlinux.org/cgit/aur.git/plain/0004-Max_tls_v1_2_mbedtls_3_6_0_workaround.patch?h=obs-studio-browser&id=$_patch_commit"
  )
  sha256sums+=(
    'SKIP'
    'c397a8da291547c757a42f7727a5e6650aa70e6e531f2ef150356eb9eb1fb49c'
  )

  _prepare_fixes() (
    cd "$_pkgsrc"
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      src="${src%.zst}"
      if [[ $src == *.patch ]]; then
        printf '\nApplying patch: %s\n' "$src"
        patch -Np1 -F100 -i "${srcdir:?}/$src"
        echo
      fi
    done

    make PREFIX="$srcdir/build_nvcodec_headers" -C "$srcdir/nv-codec-headers" install
  )
}

_source_obs_studio() {
  source+=(
    'mixer.ftl-sdk'::'git+https://github.com/Mixer/ftl-sdk.git'
    #'obsproject.libdshowcapture'::'git+https://github.com/obsproject/libdshowcapture.git'
    'obsproject.obs-browser'::'git+https://github.com/obsproject/obs-browser.git'
    'obsproject.obs-websocket'::'git+https://github.com/obsproject/obs-websocket.git'
  )

  sha256sums+=(
    'SKIP'
    'SKIP'
    'SKIP'
  )

  _prepare_obs_studio() (
    cd "$srcdir/$_pkgsrc"
    local _submodules=(
      'mixer.ftl-sdk'::'plugins/obs-outputs/ftl-sdk'
      #'obsproject.libdshowcapture'::'plugins/win-dshow/libdshowcapture'
      'obsproject.obs-browser'::'plugins/obs-browser'
      'obsproject.obs-websocket'::'plugins/obs-websocket'
    )
    _submodule_update
  )
}

_source_mixer_ftl_sdk() {
  source+=(
    'akheron.jansson'::'git+https://github.com/akheron/jansson.git'
    'curl'::'git+https://github.com/curl/curl.git'
  )
  sha256sums+=(
    'SKIP'
    'SKIP'
  )

  _prepare_mixer_ftl_sdk() (
    cd "${srcdir:?}/$_pkgsrc"
    cd 'plugins/obs-outputs/ftl-sdk'
    local _submodules=(
      'akheron.jansson'::'libjansson'
      'curl'::'libcurl'
    )
    _submodule_update
  )
}

_source_cef() {
  depends+=(
    at-spi2-core
    libxrandr
    nspr
    nss
  )

  makedepends+=(
    alsa-lib
    dbus
    expat
    gcc-libs
    glib2
    glibc
    libcups
    libdrm
    libx11
    libxcb
    libxcomposite
    libxdamage
    libxext
    libxfixes
    libxkbcommon
    mesa
    wayland
  )

  _cef_src="cef_binary_${_cef_branch}_linux_${CARCH}"
  _cef_ext="tar.xz"
  _cef_filename="$_cef_src$_cef_ver.$_cef_ext"
  _cef_dl_url="https://cdn-fastly.obsproject.com/downloads/"

  source+=("$_cef_filename"::"$_cef_dl_url/$_cef_filename")
  sha256sums+=('SKIP')
}

prepare() {
  _submodule_update() {
    local _module
    for _module in "${_submodules[@]}"; do
      git submodule init "${_module##*::}"
      git submodule set-url "${_module##*::}" "$srcdir/${_module%::*}"
      git -c protocol.file.allow=always submodule update "${_module##*::}"
    done
  }

  _prepare_obs_studio
  _prepare_mixer_ftl_sdk
  _prepare_fixes

  sed -E \
    -e 's&(\s*find_package\(FFnvcodec)\b.*\b(REQUIRED\))&\1 12 \2&' \
    -i "$_pkgsrc/plugins/obs-ffmpeg/cmake/dependencies.cmake" \
    -i "$_pkgsrc/plugins/obs-ffmpeg/cmake/legacy.cmake"
}

_build_cef() {
  local _cmake_options=(
    -S "$_cef_src"
    -B "$_cef_src"
    -DCMAKE_BUILD_TYPE=Release
    -DPROJECT_ARCH=$CARCH
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build "$_cef_src"

  # need absolute path later
  _cef_src="$srcdir/$_cef_src"
}

_build_obs_studio() {
  export CFLAGS+=" -fno-strict-aliasing -Wno-error=deprecated-declarations"
  export CXXFLAGS+=" -fno-strict-aliasing -Wno-error=deprecated-declarations"

  #export PKG_CONFIG_PATH="$srcdir/build_nvcodec_headers/lib/pkgconfig"

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DCMAKE_INSTALL_LIBDIR='lib'
    -DENABLE_AJA="${_plugin_aja:?}"
    -DENABLE_JACK=ON
    -DENABLE_LIBFDK=ON
    -DENABLE_NEW_MPEGTS_OUTPUT=ON
    -DENABLE_VLC=OFF
    -DENABLE_VST=ON
    -DENABLE_WEBRTC="${_plugin_webrtc:?}"

    -DENABLE_BROWSER=ON # qrcodegencpp-cmake
    -DCEF_ROOT_DIR="$_cef_src"

    -DFFnvcodec_INCLUDE_DIR="$srcdir/nv-codec-headers/include"

    -DOBS_VERSION_OVERRIDE="${pkgver%%.r*}"
    -DBUILD_FOR_DISTRIBUTION=ON
    -DBUILD_TESTS=OFF
    -DENABLE_UNIT_TESTS=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

build() {
  export CFLAGS CXXFLAGS
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=*/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=*/_FORTIFY_SOURCE=2}"

  _build_cef
  _build_obs_studio
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # fix permissions
  chmod -R u=rwX,go=rX "$pkgdir"
}

_update_version() {
  : ${_pkgver:=$pkgver}

  if [[ "${_autoupdate::1}" != "t" ]]; then
    return
  fi

  local _response=$(curl -Ssf "$url/releases.atom")
  local _tag=$(
    printf '%s' "$_response" \
      | grep '/releases/tag/' \
      | sed -E 's@^.*/releases/tag/(.*)".*$@\1@' \
      | grep -Ev '[a-z]{2}' | sort -V | tail -1
  )
  local _pkgver_new="${_tag#v}"

  # update _pkgver
  if [ "$_pkgver" != "${_pkgver_new:?}" ]; then
    _pkgver="${_pkgver_new:?}"
  fi

  pkgver() {
    echo "${_pkgver:?}"
  }
}

_update_version
_source_main
_source_fixes
_source_cef
_source_obs_studio
_source_mixer_ftl_sdk
