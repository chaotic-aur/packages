# Maintainer: aur.chaotic.cx

## options
: ${_electron_version=}
: ${_nodeversion=}
: ${_install_path:=usr/lib}

_pkgname="ipfs-desktop"
pkgname="$_pkgname"
pkgver=0.46.1
pkgrel=1
pkgdesc="Desktop client for the InterPlanetary File System"
url="https://github.com/ipfs/ipfs-desktop"
license=('MIT')
arch=('x86_64')

depends=(
  "electron${_electron_version:-}"
)
makedepends=(
  'libicns'
  'nvm'
)

_pkgsrc="$pkgname-$pkgver"
_pkgext="tar.gz"
source=("$_pkgsrc.$_pkgext"::"$url/archive/refs/tags/v$pkgver.$_pkgext")
sha256sums=('SKIP')

_nvm_env() {
  export HOME="$SRCDEST/node-home"
  export NVM_DIR="$SRCDEST/node-nvm"

  # set up nvm
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
  nvm install ${_nodeversion:-node}
  nvm use ${_nodeversion:-node}
}

_electron_env() {
  export SYSTEM_ELECTRON_VERSION=$(< "/usr/lib/electron${_electron_version:-}/version")
  export ELECTRON_VERSION=${SYSTEM_ELECTRON_VERSION%%.*}
}

prepare() {
  _electron_env

  # set electron version
  sed -E \
    -e 's&^(\s*)("electron"): "(.*)"(,?)$&\1\2: "'"$SYSTEM_ELECTRON_VERSION"'"\4&' \
    -i "$_pkgsrc/package.json"

  # extract icons
  icns2png -x "$_pkgsrc"/assets/build/icon.icns
}

build() (
  _nvm_env
  _electron_env

  local _builder_options=(
    -c.electronDist="'/usr/lib/electron${ELECTRON_VERSION:-}'"
    -c.electronVersion=${SYSTEM_ELECTRON_VERSION}
  )

  cd "$_pkgsrc"
  npm install --no-audit --no-fund
  npm run build
  npm exec -c "electron-builder --linux --dir --publish never ${_builder_options[@]}"
)

package() {
  _electron_env

  depends=("electron${ELECTRON_VERSION:-}")

  mkdir -pm755 "$pkgdir/$_install_path/$_pkgname"
  cp -a "$_pkgsrc/dist/linux-unpacked/resources"/* -t "$pkgdir/$_install_path/$_pkgname/"

  install -Dm644 "icon_256x256x32.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  install -Dm644 "$_pkgsrc/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"

  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$_pkgname" << END
#!/usr/bin/env bash

name=$_pkgname
flags_file="\${XDG_CONFIG_HOME:-\$HOME/.config}/\${name}-flags.conf"

lines=()
if [[ -f "\${flags_file}" ]]; then
  mapfile -t lines < "\${flags_file}"
fi

flags=()
for line in "\${lines[@]}"; do
  if [[ ! "\${line}" =~ ^[[:space:]]*#.* ]] && [[ -n "\${line}" ]]; then
    flags+=("\${line}")
  fi
done

: \${ELECTRON_IS_DEV:=0}
export ELECTRON_IS_DEV
: \${ELECTRON_FORCE_IS_PACKAGED:=true}
export ELECTRON_FORCE_IS_PACKAGED

exec electron${ELECTRON_VERSION:-} "\${flags[@]}" "/$_install_path/$_pkgname/app.asar" "\$@"
END

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" << END
[Desktop Entry]
Type=Application
Name=IPFS Desktop
Comment=$pkgdesc
Exec=$_pkgname %u
Icon=$_pkgname
Terminal=false
StartupNotify=true
Categories=Network;FileTransfer;P2P;
MimeType=x-scheme-handler/ipfs;x-scheme-handler/ipns;
StartupWMClass=IPFS Desktop
END

  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
