# Maintainer: aur.chaotic.cx

: ${_install_path:=opt}

_pkgname="vita3k"
pkgname="$_pkgname-git"
pkgver=r3751.7c2110290
pkgrel=1
pkgdesc="Experimental PlayStation Vita emulator"
url="https://github.com/Vita3K/Vita3K"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'dbus'
  'sdl2'
)
makedepends=(
  'clang'
  'cmake'
  'git'
  'lld'
  'ninja'
)
optdepends=(
  'vulkan-validation-layers: Descriptive vulkan errors'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=9 HEAD)"
}

prepare() {
  cd "$_pkgsrc"
  git submodule update --init --depth 1 --recursive

  # Needed to have correct title version
  git remote set-url origin "$url.git"
}

build() {
  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  export CC CXX
  CC=clang
  CXX=clang++

  local _ldflags=(${LDFLAGS})
  export LDFLAGS="${_ldflags[@]//*fuse-ld*/} -fuse-ld=lld"

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DUSE_DISCORD_RICH_PRESENCE=OFF
    -DUSE_VITA3K_UPDATE=OFF
    -DUSE_SANITIZERS=OFF # cubeb
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  install -dm755 -p "$pkgdir/usr/bin" "$pkgdir/$_install_path/$_pkgname"
  cp -r "build/bin"/* "$pkgdir/$_install_path/$_pkgname/"
  ln -sf "/$_install_path/$_pkgname/Vita3K" "$pkgdir/usr/bin/$_pkgname"

  install -Dm644 "$_pkgsrc/data/image/icon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" << END
[Desktop Entry]
Type=Application
Name=Vita3K
GenericName=PlayStation Vita Emulator
Icon=$_pkgname
Exec=$_pkgname
Comment=$pkgdesc
Categories=Game;Emulator;
Terminal=false
END
}
