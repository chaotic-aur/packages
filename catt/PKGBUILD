# Maintainer: Dashon Wells <me@me.me>
# Contributor Michael Riegert <michael at eowyn net>
# Contributor: Evan McCarthy <evan@mccarthy.mn>
# Contributor: Sibren Vasse <arch@sibrenvasse.nl>
# Contributor: Clint Valentine <valentine.clint@gmail.com>
# Contributor: xiota <aur commenter>
# Contributor: yochananmarqos <aur commenter>

pkgname="catt"
pkgver=0.13.0
pkgrel=4
pkgdesc='Cast All The Things - Send videos from many, many online sources to your Chromecast.'
arch=('any')
url="https://github.com/skorokithakis/catt"
license=('BSD-2-Clause')
depends=(
  'python-click'
  'python-ifaddr'
  'python-requests'
  'python-pychromecast'
  'yt-dlp')
makedepends=(
  'python-installer'
  'python-poetry-core'
  'python-wheel'
  'python-build'
)
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('30e5b024f92321a9deaa3e10285f3721fdb2931629cc8e399db238b17b6a0b35')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  python -m installer --destdir="$pkgdir" dist/*.whl
}
