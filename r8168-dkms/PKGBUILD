# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>
# Contributor: René Wagner <rwa at clttr dot info>
# Contributor: Juan Simón <play4pro at protonmail dot com>
# Contributor: alium
# Contributor: angelsl
# Contributor: hayao <hayao@fascode.net>

_pkgbase=r8168
pkgname=${_pkgbase}-dkms
pkgver=8.053.00
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards (DKMS version)"
url="https://github.com/mtorromeo/$_pkgbase"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'dkms')
makedepends=('git')
conflicts=("${_pkgbase}")
provides=("${_pkgbase}")
source=("r8168-dkms::git+${url}.git#tag=${pkgver}"
  'dkms.conf'
  "$_pkgbase-60.patch::https://github.com/mtorromeo/r8168/pull/60.patch")
sha256sums=('8e9d49c9e1e0e6f5a7f7336e5f7d55fb711e243827027ea5430f03332492ed36'
  'd37b8acbd4fe06f81538581712a04751a96fc37bad3a4bd3ae8329f8744c49b3'
  '583274ab545c9fe453a434a5f2b243575a0aeb3bf9c982f524c8efabb2745f84')

prepare() {
  cd "${pkgname}"
  sed -i 's/strlcpy/strscpy/' 'src/r8168_n.c'
  patch -p1 -i "${srcdir}/$_pkgbase-60.patch"
}

package() {
  install -Dm644 'dkms.conf' "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

  sed -e "s/@PKGNAME@/${_pkgbase}/g" \
    -e "s/@PKGVER@/${pkgver}/g" \
    -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

  cd "${pkgname}"
  cp -dr --no-preserve='ownership' src "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/src"

  echo "blacklist r8169" \
    | install -Dm644 '/dev/stdin' "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
}
