# Maintainer: Brenek Harrison <brenekharrison @ gmail d0t com>
# Contributor: Daurnimator <daurnimator@archlinux.org>
# Contributor: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: Peter Sutton (foxxy) <foxxy@foxdogstudios.com>

pkgname=ngrok
pkgver=3.16.1
pkgrel=1
pkgdesc='A tunneling, reverse proxy for developing and understanding networked, HTTP services'
url='https://ngrok.com'
license=('LicenseRef-custom')
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
optdepends=('bash-completion: Bash completion support')
source=("TERMS-OF-SERVICE.txt")
source_x86_64=("https://bin.equinox.io/a/kbbWdXr6mbu/ngrok-v3-3.16.1-linux-amd64.tar.gz")
source_i686=("https://bin.equinox.io/a/XaHTfxJFRB/ngrok-v3-3.16.1-linux-386.tar.gz")
source_armv7h=("https://bin.equinox.io/a/7szerc6A43f/ngrok-v3-3.16.1-linux-arm.tar.gz")
source_aarch64=("https://bin.equinox.io/a/ksEdnR9tz2z/ngrok-v3-3.16.1-linux-arm64.tar.gz")
sha256sums=('d1fce3c7e78d5f7e5307238ee5f371b7a10911aeb249e3358e945eaa8cf2f1b2')
sha256sums_i686=('25ddbb65019844e0dfb0795da1d690f0ea5418dc4dffa3d6e24b2dc246ad4deb')
sha256sums_x86_64=('390cdb149dee2efe48974270e8d7a1b71aac4a2e0a2402d70bb8aa84d2dc5914')
sha256sums_armv7h=('751a59e7e42e8f14095a291742ba4589906288206f01a49f12af991e21eb0d7a')
sha256sums_aarch64=('3c8d7ca9f3726d63ed0ad34cf1816c6d59fa1997112e540b7fee7eae98cfdd36')

# In order to make Ngrok's built-in updater happy, we need to avoid modifying the binary
options=(!strip)

package() {
  BASH_COMPLETION_DIR="$pkgdir/usr/share/bash-completion/completions"
  ZSH_COMPLETION_DIR="$pkgdir/usr/share/zsh/site-functions"

  # Copy terms of service to license directory
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" TERMS-OF-SERVICE.txt

  cd "${srcdir}"

  # Install the program.
  install -Dm755 "ngrok" "${pkgdir}/usr/bin/ngrok"

  # Generate completion for bash and zsh
  mkdir -p "$BASH_COMPLETION_DIR" "$ZSH_COMPLETION_DIR"
  SHELL=/usr/bin/bash ${pkgdir}/usr/bin/ngrok completion > "$BASH_COMPLETION_DIR/ngrok"
  SHELL=/usr/bin/zsh ${pkgdir}/usr/bin/ngrok completion > "$ZSH_COMPLETION_DIR/_ngrok"
}

# vim: ft=sh ts=2 sw=2 et
