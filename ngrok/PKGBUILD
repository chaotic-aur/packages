# Maintainer: Brenek Harrison <brenekharrison @ gmail d0t com>
# Contributor: Daurnimator <daurnimator@archlinux.org>
# Contributor: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: Peter Sutton (foxxy) <foxxy@foxdogstudios.com>

pkgname=ngrok
pkgver=3.18.1
pkgrel=1
pkgdesc='A tunneling, reverse proxy for developing and understanding networked, HTTP services'
url='https://ngrok.com'
license=('LicenseRef-custom')
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
optdepends=('bash-completion: Bash completion support')
source=("TERMS-OF-SERVICE.txt")
source_x86_64=("https://bin.equinox.io/a/a6eCucKKdUR/ngrok-v3-3.18.1-linux-amd64.tar.gz")
source_i686=("https://bin.equinox.io/a/3qJKm41MAaJ/ngrok-v3-3.18.1-linux-386.tar.gz")
source_armv7h=("https://bin.equinox.io/a/dsGf5Rrd6bk/ngrok-v3-3.18.1-linux-arm.tar.gz")
source_aarch64=("https://bin.equinox.io/a/3mFRnBc6cGp/ngrok-v3-3.18.1-linux-arm64.tar.gz")
sha256sums=('d1fce3c7e78d5f7e5307238ee5f371b7a10911aeb249e3358e945eaa8cf2f1b2')
sha256sums_i686=('c3e0f0a4acfa8e63107bd4929e3e76b7667689d4a35d12c260c1c0ac2f86540c')
sha256sums_x86_64=('4fcbf973fa10c9d318b9131c2402a53c14d3b919d202234dc93a364d1cd3b18d')
sha256sums_armv7h=('bd27d2568115c3fc69045ee6e168e65bbe02137807c0b1bdc7b3c7c7b73f2805')
sha256sums_aarch64=('8803b7c5d44fe724c6d457706cfd644ae3d7930fe8b5820063c0b76aa4dc2153')

# In order to make Ngrok's built-in updater happy, we need to avoid modifying the binary
options=(!strip)

package() {
  BASH_COMPLETION_DIR="$pkgdir/usr/share/bash-completion/completions"
  ZSH_COMPLETION_DIR="$pkgdir/usr/share/zsh/site-functions"

  # Copy terms of service to license directory
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" TERMS-OF-SERVICE.txt

  cd "${srcdir}"

  # Install the program.
  install -Dm755 "ngrok" "${pkgdir}/usr/bin/ngrok"

  # Generate completion for bash and zsh
  mkdir -p "$BASH_COMPLETION_DIR" "$ZSH_COMPLETION_DIR"
  SHELL=/usr/bin/bash ${pkgdir}/usr/bin/ngrok completion > "$BASH_COMPLETION_DIR/ngrok"
  SHELL=/usr/bin/zsh ${pkgdir}/usr/bin/ngrok completion > "$ZSH_COMPLETION_DIR/_ngrok"
}

# vim: ft=sh ts=2 sw=2 et
