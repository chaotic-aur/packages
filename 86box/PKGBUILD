# Maintainer: Lili1228 <aur at lili dot lgbt>
pkgname=86box
_pkgname=86Box
pkgver=5.1
pkgrel=2
pkgdesc='An emulator for classic IBM PC clones'
arch=('pentium4' 'x86_64' 'armv7h' 'aarch64')
url='https://86box.net/'
license=('GPL-2.0-or-later')
depends=('fluidsynth' 'hicolor-icon-theme' 'libserialport' 'libslirp' 'openal' 'qt6-base' 'rtmidi' 'sdl2'                                                                    # explicit
  'freetype2' 'gcc-libs' 'glib2' 'glibc' 'libevdev' 'libglvnd' 'libpng' 'libsndfile' 'libx11' 'libxcb' 'libxext' 'libxi' 'libxkbcommon-x11' 'libxkbcommon' 'wayland' 'zlib') # implicit
makedepends=('cmake>=3.21' 'extra-cmake-modules' 'ninja' 'qt6-tools' 'vde2' 'vulkan-headers')
optdepends=(
  '86box-roms: ROM files'
  'discord-game-sdk: Discord Rich Presence'
  'ghostscript: Printing with Generic PostScript Printer'
  'libpcap: Networking not limited to TCP/IP'
)
options=('!buildflags')
source=(
  "${pkgname}_$pkgver.tgz::https://github.com/${_pkgname}/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
  https://github.com/86Box/86Box/commit/3f61475d9ae0574efd81ccb924e970f7d7476374.patch # fix building with Qt 6.10
)
sha512sums=(
  5c68a21621da58a37a5e9f8199b2cabf0b3483303a44a53221c746f76a80d248d26b56499b771bd18670a16c3942ed6f3f92db95211a37677bda19becb75f70b
  c8a818eebfe2875a452f71a5d9224d2a11a40b1285d0ae1e1ace3387f1539be502d143cb02c30f0dd38bca3684e3c2158dae1c6865c7fc24df52a07929a75cc0
)

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -p1 -i ../3f61475d9ae0574efd81ccb924e970f7d7476374.patch
}

build() {
  case "$CARCH" in
    pentium4) _TOOLCHAIN=cmake/flags-gcc-i686.cmake ;;
    x86_64) _TOOLCHAIN=cmake/flags-gcc-x86_64.cmake ;;
    armv7h) _TOOLCHAIN=cmake/flags-gcc-armv7.cmake ;;
    aarch64) _TOOLCHAIN=cmake/flags-gcc-aarch64.cmake ;;
  esac
  LDFLAGS='-z now -z shstk' cmake -S"$_pkgname-$pkgver" -Bbuild --preset regular --toolchain "$_TOOLCHAIN" -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT6=on
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --build "${srcdir}/build" --target install
  for i in 48x48 64x64 72x72 96x96 128x128 192x192 256x256 512x512; do
    install -Dm644 "$srcdir/$_pkgname-$pkgver/src/unix/assets/$i/net.86box.86Box.png" -t "$pkgdir/usr/share/icons/hicolor/$i/apps"
  done
  install -Dm644 "$srcdir/$_pkgname-$pkgver/src/unix/assets/net.86box.86Box.desktop" "$pkgdir/usr/share/applications/net.86box.86Box.desktop"
}
