# Maintainer: aur.chaotic.cx

_pkgname="skypeforlinux"
_snapname="skype"
pkgname="$_pkgname-bin"
pkgver=8.131.0.202
pkgrel=1
pkgdesc='Skype for Linux'
url="https://snapcraft.io/$_snapname"
license=('LicenseRef-Skype')
arch=('x86_64')

makedepends=(
  'html-xml-utils'
  'pandoc'
  'sha3sum'
  'squashfs-tools'
)

_source_main() {
  source=("$_filename"::"$_dl_url")
  sha256sums=('SKIP')
}

_terms_of_use="terms_of_use"
_terms_of_use_url="https://go.skype.com/tou"
_privacy_policy="privacy_policy"
_privacy_policy_url="https://go.skype.com/privacy"

prepare() {
  # check upstream sha3-384sum
  printf '%s  %s' "$_cksum" "$_filename" | sha3-384sum -c

  # terms of use
  curl -L --no-progress-meter \
    -o "$_terms_of_use-1.html" \
    "$_terms_of_use_url"

  hxnormalize -x "$_terms_of_use-1.html" \
    | hxselect body \
    | hxremove script,.justify-content-end \
    | hxremove "body:nth-of-type(1),body:nth-of-type(2)" \
    | hxremove "body:nth-of-type(3),body:nth-of-type(5)" \
      1> "$_terms_of_use-2.html" \
      2> /dev/null

  pandoc --quiet -t plain -o "$_terms_of_use.txt" "$_terms_of_use-2.html"

  # privacy policy
  curl -L --no-progress-meter \
    -o "$_privacy_policy-1.html" \
    "$_privacy_policy_url"

  hxnormalize -x "$_privacy_policy-1.html" \
    | hxselect body \
    | hxremove script,.uhf,.m-l-md,.expand_collapse,.div_print \
    | hxremove .xl-col-18-24,.displayNone \
    | hxremove "[id^=navigationHeader]" \
    | hxremove "[id^=moduleName]" \
    | hxremove "[class^=gcc_cookies]" \
      1> "$_privacy_policy-2.html" \
      2> /dev/null

  pandoc --quiet -t plain -o "$_privacy_policy.txt" "$_privacy_policy-2.html"

  # unpack
  mkdir -p "$_pkgname-$_pkgver"
  unsquashfs -q -n -f -d "$_pkgname-$_pkgver/" "$_filename"
}

package() {
  depends+=(
    'alsa-lib'
    'gtk3'
    'libsecret'
    'nss'
  )

  # opt
  install -dm755 "$pkgdir/opt/$_pkgname"
  mv "$_pkgname-$pkgver/usr/share/skypeforlinux"/* "$pkgdir/opt/$_pkgname/"

  # symlink
  install -dm755 "$pkgdir/usr/bin"
  ln -s /opt/skypeforlinux/skypeforlinux "$pkgdir/usr/bin/skypeforlinux"

  # desktop file
  install -Dm644 "$_pkgname-$_pkgver/meta/gui"/*.desktop -t "$pkgdir/usr/share/applications/"
  sed -E \
    -e 's&^Icon=.*$&Icon=skypeforlinux&' \
    -e 's&Exec=skype\b&Exec=skypeforlinux&' \
    -i "$pkgdir/usr/share/applications"/*.desktop

  # icon
  install -Dm644 "$_pkgname-$_pkgver/meta/gui/skypeforlinux.png" -t "$pkgdir/usr/share/pixmaps/"

  # license/eula
  install -Dm644 "$_terms_of_use.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 "$_privacy_policy.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
}

_update_version() {
  : ${_pkgver:=${pkgver%%.r*}}

  _response=$(
    curl -H 'Snap-Device-Series: 16' --no-progress-meter "http://api.snapcraft.io/v2/snaps/info/$_snapname" \
      | sed 's@,@,\n@g'
  )

  _dl_url=$(
    printf '%s' "$_response" \
      | grep '"url"' \
      | sed -E 's@^"url":"(https:.*\.snap)".*$@\1@' \
      | head -1
  )

  _pkgver_new=$(
    printf '%s' "$_response" \
      | grep '"version"' \
      | sed -E 's@^"version":"([0-9]+(\.[0-9]+)*)".*$@\1@' \
      | head -1
  )

  # sha3-384 checksum
  _cksum=$(
    printf '%s' "$_response" \
      | grep '"sha3-384"' \
      | sed -E 's@^"sha3-384":"([a-f0-9]+)".*$@\1@' \
      | head -1
  )

  _filename="$_pkgname-${_pkgver_new:?}.snap"

  # update _pkgver
  if [ "$_pkgver" != "${_pkgver_new:?}" ]; then
    _pkgver="${_pkgver_new:?}"
  fi

  pkgver() {
    printf '%s' "${_pkgver:?}"
  }
}

_update_version
_source_main
