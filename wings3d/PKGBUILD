# Maintainer:
# Contributor: slomomojo <slomomojo@gmail.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: kappa <kappacurve@gmail.com>

## links
# https://www.wings3d.com
# https://github.com/dgud/wings

_pkgname="wings3d"
pkgname="$_pkgname"
pkgver=2.3
pkgrel=3
pkgdesc='3D modeler using the winged edge data structure'
url="https://www.wings3d.com/"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'erlang'
  'erlang-cl'
)
makedepends=(
  'git'
)
optdepends=(
  'povray: render scenes with POV-Ray'
)

_pkgsrc="${_pkgname%3d}-$pkgver"
_pkgext="tar.gz"
source=(
  "$_pkgname-$pkgver.$_pkgext"::"https://github.com/dgud/wings/archive/refs/tags/v$pkgver.$_pkgext"
)
sha256sums=(
  'd8d7751a898afa983b017fe9cd6bdc47dc430e0d86125da782344d4cf1be0f1d'
)

prepare() {
  sed -e "/desktop-id/ s/com.wings3d.WINGS.desktop/$pkgname.desktop/" -i "$_pkgsrc/unix/wings.appdata.xml"

  # fix excess quotes
  sed -e '/material[0-9]/ s/""//g' -i "$_pkgsrc/plugins_src/import_export/wpc_yafaray.erl"

  # quote reserved word: maybe
  sed -Ee 's&\bmaybe\b&'\''maybe'\''&' -i "$_pkgsrc/src/wings_draw.erl"
  sed -Ee '/invalidate/s&\bmaybe\b&'\''maybe'\''&;s&'\'\''&'\''&g' -i "$_pkgsrc/src/wings_proxy.erl"
}

build() {
  export ERL_LIBS="$srcdir"
  make -C "$_pkgsrc" unix
}

package() {
  cd "$_pkgsrc"
  install -Dm644 "icons/wings_icon_379x379.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  install -Dm644 unix/wings.appdata.xml "$pkgdir/usr/share/metainfo/$_pkgname.appdata.xml"

  cd build
  install -dm755 "$pkgdir/usr/lib/$_pkgname"
  cp --reflink=auto -r wings-*-linux/lib/"$_pkgsrc"/* "$pkgdir/usr/lib/$_pkgname/"

  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$_pkgname" << 'END'
#!/usr/bin/env bash
GDK_BACKEND=x11 exec /usr/bin/erl -noinput -smp \
  -pa /usr/lib/wings3d/ebin \
  -run wings_start start_halt "$@"
END

  install -Dm644 /dev/stdin -t "$pkgdir/usr/share/applications/$_pkgname.desktop" << END
[Desktop Entry]
Type=Application
Name=Wings 3D
GenericName=3D Modeler
Comment=$pkgdesc
Exec=$_pkgname
Icon=$_pkgname
Terminal=false
StartupNotify=false
Categories=Graphics;3DGraphics;
END

  # fix permissions
  chmod -R u=rwX,go=rX,go-w "$pkgdir"
}
