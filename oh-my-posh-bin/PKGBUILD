# Maintainer: Kamack38 <kamack38.biznes@gmail.com>
_pkgname='oh-my-posh'
pkgname="${_pkgname}-bin"
pkgver=29.4.0
pkgrel=1
pkgdesc="A prompt theme engine for any shell."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=('curl')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('d85de04313723078574a83bd5e8d181d27156684929bedfb8ea9ab4cab050b77'
  'a5308c4e51268229a039ec4ec9a251a4cdb89d9380383e6e13aeba64a74f19ad')
sha256sums_x86_64=('4c55f188f1815dbff0e34a0512b0cc53418b3d9ad1d28a0fcf588e0944768f35')
sha256sums_armv7h=('fc726935ef1ca21ac02683c7d4da0121df73da8dd2b4a10f2491e0e4fb6f0eba')
sha256sums_aarch64=('93c4dc231b4959913e16cad8a9058692391e518f4732ccfb3a3585ea5259c2b9')
source=(
  "themes-${sha256sums[0]}.zip::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/themes.zip"
  "LICENSE-${sha256sums[1]}::https://raw.githubusercontent.com/JanDeDobbeleer/oh-my-posh/v${pkgver}/COPYING"
)
source_x86_64=("posh-linux-amd64-${sha256sums_x86_64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-amd64")
source_armv7h=("posh-linux-arm-${sha256sums_armv7h}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm")
source_aarch64=("posh-linux-arm64-${sha256sums_aarch64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm64")
noextract=("themes-${sha256sums[0]}.zip")

pkgver() {
  curl --silent -L "https://api.github.com/repos/JanDeDobbeleer/oh-my-posh/releases/latest" \
    |
    # Get latest release from GitHub api
    grep '"tag_name":' \
    |
    # Get tag line
    sed -E 's/.*"([^"]+)".*/\1/' \
    | sed 's/v//'
}

package() {
  install -Dm 644 "${srcdir}/LICENSE-${sha256sums[1]}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  if [[ "$CARCH" = 'x86_64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-amd64-${sha256sums_x86_64}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'armv7h' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm-${sha256sums_armv7h}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'aarch64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm64-${sha256sums_aarch64}" "${pkgdir}/usr/bin/oh-my-posh"
  fi

  mkdir -p "${pkgdir}/usr/share/oh-my-posh/themes"
  bsdtar -xf "${srcdir}/themes-${sha256sums[0]}.zip" -C "${pkgdir}/usr/share/oh-my-posh/themes"
  find "${pkgdir}/usr/share/oh-my-posh/themes/" -type f -exec chmod 644 {} +
}
