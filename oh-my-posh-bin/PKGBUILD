# Maintainer: Kamack38 <kamack38.biznes@gmail.com>
_pkgname='oh-my-posh'
pkgname="${_pkgname}-bin"
pkgver=26.2.2
pkgrel=1
pkgdesc="A prompt theme engine for any shell."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=('curl' 'grep' 'sed' 'unzip')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('5b37e904bfca6ecadff212ade55b6833a9e705927f9921c10f1fc0d4a4770dcb'
  'a5308c4e51268229a039ec4ec9a251a4cdb89d9380383e6e13aeba64a74f19ad')
sha256sums_x86_64=('39c2fa1193d132233dcc4b416bca564b96fcb691c69a49b6020584a44a7a1f47')
sha256sums_armv7h=('e184358f10b78c90b15ed2138924e607200a361391877b4e646135b8c70b5467')
sha256sums_aarch64=('542d648a6d1c777705c87e0ed8688e5dab770140d6a34c8e3bdbf45ea8fc7307')
source=(
  "themes-${sha256sums[0]}.zip::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/themes.zip"
  "LICENSE-${sha256sums[1]}::https://raw.githubusercontent.com/JanDeDobbeleer/oh-my-posh/v${pkgver}/COPYING"
)
source_x86_64=("posh-linux-amd64-${sha256sums_x86_64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-amd64")
source_armv7h=("posh-linux-arm-${sha256sums_armv7h}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm")
source_aarch64=("posh-linux-arm64-${sha256sums_aarch64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm64")
noextract=('themes.zip')

pkgver() {
  curl --silent -L "https://api.github.com/repos/JanDeDobbeleer/oh-my-posh/releases/latest" \
    |
    # Get latest release from GitHub api
    grep '"tag_name":' \
    |
    # Get tag line
    sed -E 's/.*"([^"]+)".*/\1/' \
    | sed 's/v//'
}

package() {
  install -Dm 644 "${srcdir}/LICENSE-${sha256sums[1]}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  if [[ "$CARCH" = 'x86_64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-amd64-${sha256sums_x86_64}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'armv7h' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm-${sha256sums_armv7h}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'aarch64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm64-${sha256sums_aarch64}" "${pkgdir}/usr/bin/oh-my-posh"
  fi

  mkdir -p "${pkgdir}/usr/share/oh-my-posh/themes"
  unzip "${srcdir}/themes-${sha256sums}.zip" -d "${pkgdir}/usr/share/oh-my-posh/themes"
  find "${pkgdir}/usr/share/oh-my-posh/themes/" -type f -exec chmod 644 {} +
}
