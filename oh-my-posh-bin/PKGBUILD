# Maintainer: Kamack38 <kamack38.biznes@gmail.com>
_pkgname='oh-my-posh'
pkgname="${_pkgname}-bin"
pkgver=23.14.1
pkgrel=1
pkgdesc="A prompt theme engine for any shell."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=('curl' 'grep' 'sed' 'unzip')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('f5e464b535d9cc409b0a39db07ab746e1c08683f27442b522508c8addb394760'
  'a5308c4e51268229a039ec4ec9a251a4cdb89d9380383e6e13aeba64a74f19ad')
sha256sums_x86_64=('5c142fdd263cf9fb046dca703a45eec97f92d59e854f06702b75a6416795313d')
sha256sums_armv7h=('abeb2131983f9c9b8b0dc3050d294ef09051a434a743ab56f80804442197fdc9')
sha256sums_aarch64=('6351b468c95369bad380f35f2e12df4823d7729b7ea5c0b1ee42b12923170a39')
source=(
  "themes-${sha256sums[0]}.zip::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/themes.zip"
  "LICENSE-${sha256sums[1]}::https://raw.githubusercontent.com/JanDeDobbeleer/oh-my-posh/v${pkgver}/COPYING"
)
source_x86_64=("posh-linux-amd64-${sha256sums_x86_64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-amd64")
source_armv7h=("posh-linux-arm-${sha256sums_armv7h}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm")
source_aarch64=("posh-linux-arm64-${sha256sums_aarch64}::https://github.com/JanDeDobbeleer/oh-my-posh/releases/download/v$pkgver/posh-linux-arm64")
noextract=('themes.zip')

pkgver() {
  curl --silent -L "https://api.github.com/repos/JanDeDobbeleer/oh-my-posh/releases/latest" \
    |
    # Get latest release from GitHub api
    grep '"tag_name":' \
    |
    # Get tag line
    sed -E 's/.*"([^"]+)".*/\1/' \
    | sed 's/v//'
}

package() {
  install -Dm 644 "${srcdir}/LICENSE-${sha256sums[1]}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  if [[ "$CARCH" = 'x86_64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-amd64-${sha256sums_x86_64}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'armv7h' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm-${sha256sums_armv7h}" "${pkgdir}/usr/bin/oh-my-posh"
  elif [[ "$CARCH" = 'aarch64' ]]; then
    install -Dm 755 "${srcdir}/posh-linux-arm64-${sha256sums_aarch64}" "${pkgdir}/usr/bin/oh-my-posh"
  fi

  mkdir -p "${pkgdir}/usr/share/oh-my-posh/themes"
  unzip "${srcdir}/themes-${sha256sums}.zip" -d "${pkgdir}/usr/share/oh-my-posh/themes"
  find "${pkgdir}/usr/share/oh-my-posh/themes/" -type f -exec chmod 644 {} +
}
