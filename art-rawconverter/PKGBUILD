# Maintainer: aur.chaotic.cx

_pkgname="art-rawconverter"
pkgname="$_pkgname"
pkgver=1.24.1
pkgrel=1
pkgdesc="Raw image converter forked from RawTherapee with ease of use in mind"
url="https://github.com/artpixls/ART"
license=('GPL-3.0-or-later')
arch=('x86_64')

depends=(
  'exiv2'
  'fftw'
  'glibmm'
  'gtk3'
  'gtkmm3'
  'lcms2'
  'lensfun'
  'libcanberra'
  'libiptcdata'
  'libraw'
  'mimalloc'
  'opencolorio'
  'openmp'
)
makedepends=(
  'cmake'
  'fakeroot'
  'git'
  'ninja'
  'pkgconf'
)
optdepends=(
  'art-rawconverter-imageio: add support for additional image formats'
  'lcms2-ff: lcms2 with fast-float plugin for improved export speed'
  'perl-image-exiftool: metadata support for CR3 images'
)

_source_main() {
  _update_version

  options=('!lto')

  _pkgsrc="${_pkgname}.github"
  source=("$_pkgsrc"::"git+$url.git#tag=$_pkgver")
  sha256sums=('SKIP')

  pkgver() {
    echo "${_pkgver:?}"
  }
}

prepare() {
  _pkgver=$(pkgver)

  # fix version in about dialog
  sed -E \
    -e 's@^(\s*)execute_process.*GIT_CMD.*describe.*GIT_DESCRIBE.*$@set(GIT_DESCRIBE "'"${_pkgver:?}"'")@' \
    -i "$srcdir/$_pkgsrc/UpdateInfo.cmake"
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_LIBRAW=ON
    -DENABLE_OCIO=ON
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # symlinks
  ln -s "ART" "$pkgdir/usr/bin/art"
  ln -s "ART-cli" "$pkgdir/usr/bin/art-cli"
}

_update_version() {
  : ${_pkgver:=${pkgver%%.r*}}

  if [ "${_autoupdate::1}" != "t" ]; then
    return
  fi

  local _response _tag _pkgver_new
  _response=$(curl -Ssf "$url/releases.atom")
  _tag=$(
    printf '%s' "$_response" \
      | grep -Po '(?<=/tag/)[0-9.]+(?=")' \
      | sort -rV | head -1
  )
  _pkgver_new="${_tag:?}"

  if [ $(vercmp "${_pkgver_new:?}" "$_pkgver") -gt 0 ]; then
    _pkgver="${_pkgver_new:?}"
  fi
}

_source_main
