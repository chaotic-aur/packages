# Maintainer: Marco Rubin <marco.rubin@protonmail.com>
# Contributor: Noel Kuntze <noel.kuntze at thermi dot consulting>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de

pkgname=pdfcpu
pkgver=0.9.1
pkgrel=1
pkgdesc='A PDF processor written in Go.'
arch=(x86_64)
url='https://github.com/hhrutter/pdfcpu'
license=('Apache-2.0')
makedepends=(go)
source=($url/archive/v$pkgver.tar.gz)
b2sums=('31ca9525ea1bc8853e1a7e7404cffcbf0cba6344cd5898be577d64d33a2882db6decef66ef104c07841f6000467412d21deda8e83f08fd914a211507459a575b')

build() {
  cd $pkgname-$pkgver/cmd/pdfcpu
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -buildvcs=false
}

check() {
  cd $pkgname-$pkgver
  ./coverage.sh
}

package() {
  cd $pkgname-$pkgver/cmd/pdfcpu
  install -Dm755 pdfcpu "$pkgdir/usr/bin/$pkgname"
}
