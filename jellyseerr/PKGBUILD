# Maintainer: jab416171 <jab416171@gmail.com>
# Maintainer: txtsd <aur.archlinux@ihavea.quest>
# Contributor: Donald Webster <fryfrog@gmail.com>
# Contributor: Martins Mozeiko <martins.mozeiko@gmail.com>

pkgname=jellyseerr
pkgver=2.3.0
pkgrel=1
pkgdesc='Request management and media discovery tool for the Plex ecosystem'
arch=(x86_64 aarch64)
url='https://github.com/Fallenbagel/jellyseerr'
license=('MIT')
depends=(
  bash
  gcc-libs
  glibc
)
optdepends=(
  'jellyfin-server: The Free Software Media System'
  'plex-media-server: Plex Media Server'
  'emby-server: The open media solution'
  'sonarr: Smart PVR for newsgroup and torrent users'
  'radarr: Movie organizer/manager for usenet and torrent users'
)
makedepends=(pnpm nodejs-lts-jod)
backup=(
  etc/conf.d/jellyseerr
  usr/lib/jellyseerr/config/settings.json
)
options=(!strip !debug)
install=jellyseerr.install
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/Fallenbagel/jellyseerr/archive/v${pkgver}.tar.gz"
  jellyseerr.sysusers
  jellyseerr.tmpfiles
  jellyseerr.service
  jellyseerr.conf.d
)
sha256sums=('05ed38173f9835ccedd2c24fe490637c9fa64ca3f213747b6776bde634b66827'
  '092221b83ded237f57003587533f623d9bc70455493dae9508bb0edac226f9cd'
  '202d6e4c1d4ee555c33fe7e8473c74aef795e145f3bbc885333c003a75e17f14'
  'ee4a4ed8b2ff41fe00582e11283526a08a2ed1154e8e278d898d98c8184c65eb'
  '5a446cc8fa0a47a49dbbd6920d49eb4569f988e808cbb0bdbb609ab179a94426')

prepare() {
  cd "${pkgname}-${pkgver}"

  sed -i 's/husky install//' package.json
  sed -i 's/"pnpm": "^9.0.0"/"pnpm": "^10.0.0"/' package.json

  echo "{\"commitTag\": \"${pkgver}\"}" > committag.json

  export NEXT_TELEMETRY_DISABLED=1
  pnpm install --frozen-lockfile
}

build() {
  cd "${pkgname}-${pkgver}"

  export NEXT_TELEMETRY_DISABLED=1
  export CYPRESS_INSTALL_BINARY=0
  # See: https://aur.archlinux.org/packages/jellyseerr#comment-998270
  export SHARP_IGNORE_GLOBAL_LIBVIPS=1
  pnpm build
  pnpm prune --prod --ignore-scripts
}

package() {
  depends+=(nodejs)

  cd "${pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/jellyseerr"

  # Copy jellyseerr
  cp -dr --no-preserve='ownership' ./{.next,dist,public,node_modules} "${pkgdir}/usr/lib/jellyseerr"
  cp -d --no-preserve='ownership' ./{package.json,overseerr-api.yml,next.config.js} "${pkgdir}/usr/lib/jellyseerr"

  # Remove cache
  rm -rf "${pkgdir}/usr/lib/jellyseerr/.next/cache"

  # Fix paths
  find "${pkgdir}/usr/lib/jellyseerr/.next" -type f -print0 | xargs -0 sed -i "s^${srcdir}/${pkgname}-${pkgver}^/usr/lib/jellyseerr^g"

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Systemd
  cd "${srcdir}"
  install -Dm644 jellyseerr.conf.d "${pkgdir}/etc/conf.d/jellyseerr"
  install -Dm644 jellyseerr.sysusers "${pkgdir}/usr/lib/sysusers.d/jellyseerr.conf"
  install -Dm644 jellyseerr.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/jellyseerr.conf"
  install -Dm644 jellyseerr.service "${pkgdir}/usr/lib/systemd/system/jellyseerr.service"
}
