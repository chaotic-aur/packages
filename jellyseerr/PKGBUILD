# Maintainer: jab416171 <jab416171@gmail.com>
# Maintainer: txtsd <aur.archlinux@ihavea.quest>
# Contributor: Donald Webster <fryfrog@gmail.com>
# Contributor: Martins Mozeiko <martins.mozeiko@gmail.com>

pkgname=jellyseerr
pkgver=2.0.1
pkgrel=6
pkgdesc='Request management and media discovery tool for the Plex ecosystem'
arch=(x86_64 aarch64)
url='https://github.com/Fallenbagel/jellyseerr'
license=('MIT')
depends=(
  bash
  gcc-libs
  glibc
)
optdepends=(
  'jellyfin-server: The Free Software Media System'
  'plex-media-server: Plex Media Server'
  'emby-server: The open media solution'
)
makedepends=(pnpm nodejs-lts-iron)
backup=(
  etc/conf.d/jellyseerr
  etc/jellyseerr/settings.json
)
options=(!debug)
install=jellyseerr.install
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/Fallenbagel/jellyseerr/archive/v${pkgver}.tar.gz"
  jellyseerr.sysusers
  jellyseerr.tmpfiles
  jellyseerr.service
  jellyseerr.conf.d
  jellyseerr.install
  0001-Don-t-use-husky.patch
  0002-Remove-need-for-npm-npx.patch
)
sha256sums=('3ea5ae6ffd68eac767acb9a36adb41f1dc4018232cd6dca0cc31668c1f5c005b'
  '2bac9f51b528ac40b7e0f952a032dde323533c0a986ac8be4c762fbe8dbad21f'
  'eb38f0bcb71a3553df8f8c9a6f536a1daf93508d244e7cd7ae491abb2b7ce578'
  '7cba7aadecc255a69e5d94c2ecc788cb925293aee230e9a1ead8aebd58fbe012'
  '5a446cc8fa0a47a49dbbd6920d49eb4569f988e808cbb0bdbb609ab179a94426'
  '8d7bf6eb0a71905d764ffa8a43934b645c9e8945c3fecec995674acf053dd7bd'
  '1d2ee741644c3dc4520c2e4f0213343429eadfda7231950e03170dddae73d9bc'
  'e2777886a0e7000bd680fbdc17547d42faeb4cebd7b0ddc6493630d70b3f9f42')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-Don-t-use-husky.patch"
  patch -Np1 -i "${srcdir}/0002-Remove-need-for-npm-npx.patch"

  echo "{\"commitTag\": \"${pkgver}\"}" > committag.json

  export NEXT_TELEMETRY_DISABLED=1
  pnpm install --frozen-lockfile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rm -rf .next/cache

  export NEXT_TELEMETRY_DISABLED=1
  export CYPRESS_INSTALL_BINARY=0
  pnpm build
  pnpm prune --prod --ignore-scripts
}

package() {
  depends+=(nodejs)
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/lib/jellyseerr"
  install -dm755 "${pkgdir}/usr/lib/jellyseerr/.next/standalone/.next"
  cp -dr --no-preserve='ownership' "${srcdir}/${pkgname}-${pkgver}/"{.next,dist,public,node_modules} "${pkgdir}/usr/lib/jellyseerr"
  cp -d --no-preserve='ownership' "${srcdir}/${pkgname}-${pkgver}/"{package.json,overseerr-api.yml,next.config.js} "${pkgdir}/usr/lib/jellyseerr"

  # Handle .next cache
  install -dm755 "${pkgdir}/usr/lib/jellyseerr/.next/cache"

  find "${pkgdir}/usr/lib/jellyseerr/.next" -type f -print0 | xargs -0 sed -i "s^${srcdir}/${pkgname}-${pkgver}^/usr/lib/jellyseerr^g"

  ln -s "/var/lib/jellyseerr" "${pkgdir}/usr/lib/jellyseerr/config"

  install -Dm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/jellyseerr.conf.d" "${pkgdir}/etc/conf.d/jellyseerr"
  install -Dm644 "${srcdir}/jellyseerr.sysusers" "${pkgdir}/usr/lib/sysusers.d/jellyseerr.conf"
  install -Dm644 "${srcdir}/jellyseerr.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/jellyseerr.conf"
  install -Dm644 "${srcdir}/jellyseerr.service" "${pkgdir}/usr/lib/systemd/system/jellyseerr.service"
}
