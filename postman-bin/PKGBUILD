# Maintainer: Jay Ta'ala <jay@jaytaala.com>
# Contributor: Claudio d'Angelis <claudiodangelis at gmail dot com>
pkgname=postman-bin
pkgver=11.45.0.r250512.1424
pkgrel=1
pkgdesc="Build, test, and document your APIs faster"
provides=('postman')
conflicts=('postman')
arch=('x86_64' 'aarch64')
url="https://www.getpostman.com"
options=(!strip)
license=('custom')
source_x86_64=(
  "postman-11.45.0-r250512-1424-linux-x64.tar.gz::https://dl.pstmn.io/download/version/11.45.0-r250512-1424/linux64"
  "postman.desktop"
)
source_aarch64=(
  "postman-11.45.0-r250512-1424-linux-arm64.tar.gz::https://dl.pstmn.io/download/version/11.45.0-r250512-1424/linux_arm64"
  "postman.desktop"
)
depends=(libxss nss gtk3)
sha256sums_x86_64=('ad0e0f5ec59807720ca01f9825db94b1519793abbd2e78aeabaf5e78ee6bac71'
  'd87542ac18455ff341da7c5efd01db96a01f659b1bf546840aa4ac8bd085802d')
sha256sums_aarch64=('c468932af5aeafe2c845ca0b92b7e7a774c72bea43f6f0d45ec76a3c672d513d'
  'd87542ac18455ff341da7c5efd01db96a01f659b1bf546840aa4ac8bd085802d')

package() {
  install -dm755 "${pkgdir}/opt/"
  cp -r "Postman" "${pkgdir}/opt/postman"
  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/postman/Postman" "${pkgdir}/usr/bin/postman"
  # Desktop file
  install -D -m644 "postman.desktop" \
    "${pkgdir}/usr/share/applications/postman.desktop"
  # Icon
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/128x128/apps"
  ln -s "/opt/postman/app/resources/app/assets/icon.png" \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/postman.png"
}
