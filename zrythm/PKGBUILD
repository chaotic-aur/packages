# Maintainer: aur.chaotic.cx

## links
# https://www.zrythm.org
# https://gitlab.zrythm.org/zrythm/zrythm

: ${_pkgver:=1.0.0}

_pkgname="zrythm"
pkgname="$_pkgname"
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='Highly automated and intuitive digital audio workstation'
url="https://gitlab.zrythm.org/zrythm/zrythm"
license=('AGPL-3.0-or-later')
arch=('x86_64')

depends=(
  'appstream'
  'boost'
  'breeze-icons'
  'chromaprint'
  'dconf'
  'fftw'
  'fluidsynth'
  'graphene'
  'graphviz'
  'gtk4'
  'gtksourceview5'
  'guile'
  'json-glib'
  'libadwaita'
  'libbacktrace'
  'libepoxy'
  'libpanel'
  'libxinerama'
  'libxrandr'
  'lilv'
  'pcre'
  'portaudio'
  'qt5-base'
  'rtaudio'
  'rtmidi'
  'rubberband'
  'sdl2'
  'serd'
  'sratom'
  'vamp-plugin-sdk'
  'xxhash'
  'yyjson'
  'zix'

  # AUR
  'carla>=2.5.9.r303'
  'libaudec'
  'libcyaml'
  'lsp-dsp-lib'
  'reproc'
)
makedepends=(
  'cmake'
  'git'
  'glib2-devel'
  'help2man'
  'meson'
  'ruby-sass'
  'sassc'
)
optdepends=(
  'realtime-privileges: allow memory locking'
)

_pkgsrc="$_pkgname-$_pkgver"
_pkgext="tar.xz"
source=(
  "https://www.zrythm.org/releases/$_pkgsrc.$_pkgext"{,.asc}
  "git+https://github.com/drobilla/zix.git"
)
sha256sums=(
  'd143a0a17066b50db8a6db875d2699c9e5b70f1bbe18db00fee034840144fede'
  'SKIP'
  'SKIP'
)
validpgpkeys=(
  '48132384AD3DF7D86E254B83022EAE42313D70F3' # Alexandros Theodotou <alex@zrythm.org>
)

prepare() {
  cd "$_pkgsrc"

  # use our local clones
  sed -i "s|https://github.com/drobilla/zix|$srcdir/zix|" "subprojects/zix.wrap"
  meson subprojects download zix
}

build() {
  local _meson_args=(
    --wrap-mode=nofallback
    --force-fallback-for=zix-0
    -Ddebug=false
    -Dmanpage=true
    -Dcheck_updates=false
    -Dcarla_binaries_dir=/usr/lib/carla
    -Dportaudio=enabled
    -Drtmidi=enabled
    -Drtaudio=enabled
    -Dsdl=enabled
  )

  arch-meson "${_meson_args[@]}" "$_pkgsrc" build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
  rm -rf "$pkgdir"{/usr/include/zix-0/,/usr/lib/pkgconfig/zix-0.pc}
}
