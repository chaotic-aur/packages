# Maintainer: Patrizio Bekerle <patrizio at bekerle dot com>
# https://wiki.archlinux.org/title/PKGBUILD

pkgname=qownnotes
pkgver=26.2.3
tag="6e6b0fdd3efed3336de7ec9092e20476d7109115"
pkgrel=1
pkgdesc="Plain-text file markdown note taking with Nextcloud/ownCloud integration"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://www.qownnotes.org/'
license=('GPL2')
groups=('qownnotes')
depends=('qt6-base' 'qt6-svg' 'qt6-declarative' 'qt6-websockets' 'aspell')
makedepends=('qt6-tools')
source=("https://github.com/pbek/QOwnNotes/releases/download/v${pkgver}/qownnotes-${pkgver}.tar.xz")
sha256sums=('21e820b33b87035021b2db16e732d535d07ad7918f8a3fae2b7a5329de2e56a9')

prepare() {
  cd "${pkgname}-${pkgver}"
  echo "#define RELEASE \"AUR\"" > release.h
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake6 QMAKE_CFLAGS_RELEASE="${CFLAGS}" QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" QMAKE_LFLAGS_RELEASE="${LDFLAGS}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  # install application
  install -D -m755 QOwnNotes "${pkgdir}/usr/bin/QOwnNotes"

  # install visuals
  install -D -m644 PBE.QOwnNotes.desktop "${pkgdir}/usr/share/applications/PBE.QOwnNotes.desktop"
  for format in {16x16,24x24,32x32,48x48,64x64,96x96,128x128,256x256,512x512}; do
    install -D -m644 "images/icons/${format}/apps/QOwnNotes.png" "${pkgdir}/usr/share/icons/hicolor/$format/apps/QOwnNotes.png"
  done
  install -D -m644 "images/icons/scalable/apps/QOwnNotes.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/QOwnNotes.svg"

  # install languages
  install -d "${pkgdir}/usr/share/qt6/translations/"
  install -D -m644 languages/*.qm "${pkgdir}/usr/share/qt6/translations/"
}
