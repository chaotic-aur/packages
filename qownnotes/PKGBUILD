# Maintainer: Patrizio Bekerle <patrizio at bekerle dot com>
# https://wiki.archlinux.org/title/PKGBUILD

pkgname=qownnotes
pkgver=26.2.13
tag="3cd3874fd0aa2f7dba54a8e9c75bd457de5ce856"
pkgrel=1
pkgdesc="Plain-text file markdown note taking with Nextcloud/ownCloud integration"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://www.qownnotes.org/'
license=('GPL2')
groups=('qownnotes')
depends=('qt6-base' 'qt6-svg' 'qt6-declarative' 'qt6-websockets' 'aspell' 'botan' 'libgit2')
makedepends=('qt6-tools' 'cmake')
source=("https://github.com/pbek/QOwnNotes/releases/download/v${pkgver}/qownnotes-${pkgver}.tar.xz")
sha256sums=('e4c8f0d6ae73175e9c770ac6288f50c93e1c3dc6b62b49742228f66ddef49d19')

prepare() {
  cd "${pkgname}-${pkgver}"
  echo "#define RELEASE \"AUR\"" > release.h
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQON_QT6_BUILD=ON \
    -DBUILD_WITH_SYSTEM_BOTAN=ON \
    -DBUILD_WITH_LIBGIT2=ON \
    -DBUILD_WITH_ASPELL=ON \
    -DCMAKE_C_FLAGS_RELEASE="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS_RELEASE="${CXXFLAGS}" \
    -DCMAKE_EXE_LINKER_FLAGS_RELEASE="${LDFLAGS}"
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"

  # Use cmake install target
  DESTDIR="${pkgdir}" cmake --install build
}
