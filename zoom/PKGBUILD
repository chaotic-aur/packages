# Maintainer: Gordian Edenhofer <gordian.edenhofer@gmail.com>
# Maintainer: Christian Heusel <christian@heusel.eu>
pkgbase=zoom
pkgname=(${pkgbase}{,-native})
#pkgname=(zoom-native)
#pkgname=(zoom) uncomment to skip building another one
pkgver=6.5.11.4015
pkgrel=1
arch=('x86_64')
license=('LicenseRef-zoom')
url="https://zoom.us/"
makedepends=(patchelf binutils)
options=(!strip emptydirs)
source=("${pkgbase}-${pkgver}_orig_x86_64.pkg.tar.xz"::"https://zoom.us/client/${pkgver}/zoom_x86_64.pkg.tar.xz")
sha512sums=('c150fa1469b9f1bec922a2b47a89a9ebab322427303d37936b364c8b21f2f281debbaa265f52458ea8642df16f49790f8d82fbcbb7c7e947a331f8f91a85e302')
noextract=(${source[0]%::*}) # Do not fail makepkg on small BUILDDIR

package_zoom() {
  pkgdesc="Video Conferencing and Web Conferencing Service"
  replaces=(zoom-libs-bin) # Drop this at next release
  depends=('fontconfig' 'glib2' 'libpulse' 'libsm' 'ttf-font' 'libx11' 'libxtst' 'libxcb'
    'libxcomposite' 'libxfixes' 'libxi' 'libxcursor' 'libxkbcommon-x11' 'libxrandr'
    'libxrender' 'libxshmfence' 'libxslt' 'mesa' 'nss' 'xcb-util-image'
    'xcb-util-keysyms' 'xcb-util-cursor' 'dbus' 'libdrm' 'gtk3'
    # libcef.so dependencies is missing according to namcap.
  )
  optdepends=(
    'picom: needed by some window managers for screen sharing'
    'ibus: remote control'
  )
  # source is already pacman -U able. Remove pacman info
  tar -C "$pkgdir" -xf "${noextract[0]}" --exclude .*
}

# --- about native libs ---
# Maintainer oech3
# Contributor: tiziodcaio
# Contributor: netcrusher
# Contributor: Caleb Maclennan
# Contributor: mnabid

package_zoom-native() {
  pkgdesc="Zoom Workspace with native runtimes"
  replaces=(zoom-libs) # Drop this at next release
  depends=(ocl-icd mpg123 libxtst sqlite
    quazip-qt5 qt5-{base,x11extras,graphicaleffects,quickcontrols,quickcontrols2,svg,declarative})
  optdepends=(
    'qt5-webengine: SSO login'
    'qt5-wayland: Wayland support'
    qt5-{3d,multimedia,imageformats,remoteobjects} ffmpeg
    vulkan-icd-loader chromium
  )

  tar -C "$pkgdir" -xf "${noextract[0]}" --exclude .* \
    --exclude opt/zoom/Qt --exclude opt/zoom/qt.conf --exclude opt/zoom/translations \
    --exclude opt/zoom/libOpenCL.so* --exclude opt/zoom/libmpg123.so* \
    --exclude 'opt/zoom/libav*.so*' --exclude opt/zoom/libswresample.so* \
    --exclude opt/zoom/cef/libsqlite3.so* --exclude opt/zoom/cef/libvulkan.so* #--exclude opt/zoom/cef
  cd "$pkgdir"/opt/zoom
  install -d "$pkgdir"/opt/zoom/Qt # needed for CEF
  # Let native libs usable
  for b in zoom zopen Zoom{Launcher,WebviewHost} aomhost libaomagent.so; do
    patchelf --remove-rpath $b $(nm -D "$b" | grep -E '(_Z|__cx).*@Qt_5$' | sed 's/@Qt_5.*//;s/^\s*U/--clear-symbol-version/' | tr '\n' ' ')
  done
  ln -sf /usr/lib/libquazip1-qt5.so libquazip.so*
  ln -sf /usr/lib/chromium/chrome-sandbox cef/chrome_sandbox
}
