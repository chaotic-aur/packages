# Maintainer: alba4k <blaskoazzolaaaron@gmail.com>

_pkgname="hyprland-qtutils"
pkgname="${_pkgname}-git"
pkgver=0.1.3.r5.g96bf0677
pkgrel=1
pkgdesc="Hyprland QT/qml utility apps"
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/hyprland-qtutils"
license=('BSD-3-Clause')
depends=('hyprutils-git' 'hyprland-qt-support-git' 'qt6-wayland')
makedepends=('git' 'cmake' 'gcc')
source=("${_pkgname}::git+https://github.com/hyprwm/hyprland-qtutils.git")
provides=("hyprland-qtutils")
conflicts=("hyprland-qtutils")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  git describe --long --tags --abbrev=8 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake --no-warn-unused-cli -DCMAKE_INSTALL_PREFIX=/usr -S . -B ./build
  cmake --build ./build --config Release --target all
}

package() {
  cd "${srcdir}/${_pkgname}"
  DESTDIR="${pkgdir}" cmake --install build

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
