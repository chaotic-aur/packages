# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=openfx-gmic-git
pkgver=2.4.4.r9.g292ac55
pkgrel=1
arch=('x86_64')
pkgdesc="OpenFX wrapper for the G'MIC framework"
url="https://github.com/NatronGitHub/openfx-gmic"
license=('CECILL-C OR CECILL-2.0')
depends=('fftw' 'libgl' 'libpng')
makedepends=('git' 'openmp')

_pkgname=${pkgname%-git}
_url=${url%/${_pkgname}}

conflicts=("${_pkgname}")

source=("${_pkgname}::git+${url}"
  "openfx::git+${_url}/openfx")
sha512sums=('SKIP'
  'SKIP')

pkgver() {
  cd ${_pkgname}
  git describe --long --tags | sed 's/^Natron.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgname}
  git submodule init
  git config submodule.openfx.url ${srcdir}/openfx
  git -c protocol.file.allow=always submodule update
}

build() {
  cd ${_pkgname}
  make CONFIG=release \
    OPENMP=1
}

package() {
  cd ${_pkgname}
  install -d "${pkgdir}/usr/OFX/Plugins"
  make install PLUGINPATH="${pkgdir}/usr/OFX/Plugins" \
    CONFIG=release

  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"

  for license_file in COPYING Licence_CeCILL-C_V1-en.txt Licence_CeCILL_V2-en.txt; do
    install -Dm644 $license_file \
      "${pkgdir}/usr/share/licenses/${pkgname}/"
  done
}
