# Maintainer: aur.chaotic.cx

_pkgname="citron"
pkgname="$_pkgname-git"
pkgver=0.8.0.r8.gc15b651
pkgrel=1
pkgdesc="Yet another emulator fork"
url="https://git.citron-emu.org/citron/emulator"
license=('GPL-3.0-or-later')
arch=('x86_64')

depends=(
  'libavcodec.so'       # ffmpeg
  'libavfilter.so'      # ffmpeg
  'libavutil.so'        # ffmpeg
  'libboost_context.so' # boost-libs
  'libfmt.so'
  'libswscale.so' # ffmpeg
  'libusb'
  'openal'
  'qt6-multimedia'
  'qt6-webengine'
  'sdl2'
)
makedepends=(
  'boost'
  'cmake'
  'git'
  'glslang'
  'ninja'
  'nlohmann-json'
  'qt6-tools'
  'rapidjson'
  'robin-map'
  'stb'
  'vulkan-headers'
  'vulkan-utility-libraries'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  git rm -r externals/ffmpeg/ffmpeg
  git rm -r externals/Vulkan-Headers
  git rm -r externals/Vulkan-Utility-Libraries
  git rm -r externals/SDL
  git rm -r externals/libusb/libusb
  git rm -r externals/vcpkg
  git submodule update --init --recursive --depth=1

  # prevent unnecessary errors
  find src -name "CMakeLists.txt" -exec sed -E '/Werror=/d' -i {} \;

  # fix for Qt 6.10
  sed -e '$a find_package(Qt6 REQUIRED COMPONENTS GuiPrivate)\ntarget_link_libraries(citron PRIVATE Qt6::GuiPrivate)' \
    -i src/citron/CMakeLists.txt
}

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 \
    | sed -E 's/^[^0-9]*//;s/-[^g][a-z\-]+-/-/;s/([^-]*-g)/r\1/;s/-/./g'
}

build() (
  local _cmake_options=(
    -S "$_pkgsrc"
    -B build
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX="/usr"
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 # xbyak
    -DUSE_SANITIZERS=OFF               # cubeb
    -Wno-dev

    ## citron
    -DBUILD_REPOSITORY=citron/citron
    -DBUNDLE_SPEEX=ON
    -DCITRON_CHECK_SUBMODULES=OFF
    -DCITRON_CRASH_DUMPS=OFF
    -DCITRON_DOWNLOAD_TIME_ZONE_DATA=ON
    -DCITRON_ENABLE_COMPATIBILITY_REPORTING=OFF
    -DCITRON_ENABLE_LTO=ON
    -DCITRON_ROOM=ON
    -DCITRON_TESTS=OFF
    -DCITRON_USE_BUNDLED_FFMPEG=OFF
    -DCITRON_USE_BUNDLED_QT=OFF
    -DCITRON_USE_BUNDLED_SDL2=OFF
    -DCITRON_USE_BUNDLED_VCPKG=OFF
    -DCITRON_USE_EXTERNAL_SDL2=OFF
    -DCITRON_USE_EXTERNAL_VULKAN_HEADERS=OFF
    -DCITRON_USE_EXTERNAL_VULKAN_UTILITY_LIBRARIES=OFF
    -DCITRON_USE_FASTER_LD=OFF
    -DCITRON_USE_QT_MULTIMEDIA=ON
    -DCITRON_USE_QT_WEB_ENGINE=ON
    -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON
    -DENABLE_QT_TRANSLATION=OFF
    -DENABLE_WEB_SERVICE=ON
    -DTITLE_BAR_FORMAT_IDLE="citron | ${pkgver} {}"
    -DTITLE_BAR_FORMAT_RUNNING="citron | ${pkgver} {}"
    -DUSE_DISCORD_PRESENCE=OFF
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
)

package() {
  DESTDIR="$pkgdir" cmake --install build
}
