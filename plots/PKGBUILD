# Maintainer: Mubashshir <ahmubashshir@gmail.com>
# from: github
# what: alexhuntley/Plots
# deps: setuptools

pkgname=plots
_name=Plots

pkgver=0.8.5
pkgrel=1
pkgdesc="A graph plotting app for GNOME"
arch=('any')
url="https://github.com/alexhuntley/Plots/"
license=('GPL')
depends=(
  'gtk3'
  'libhandy'
  'python-gobject'
  'python-opengl'
  'python-jinja'
  'python-numpy'
  'python-lark-parser'
  'otf-latinmodern-math'
  'python-freetype-py'
  'python-pyglm'
)

makedepends=('python-setuptools')
checkdepends=('python-pytest')

source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/alexhuntley/$_name/archive/v$pkgver.tar.gz"
)
sha256sums=('e37295676143a0712827e8b79f2789f628b8d26161f18503737752dcb0c430d6')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

package() {
  cd "$_name-$pkgver"
  rm -rf build/lib/tests
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -t "$pkgdir/usr/share/help/C/plots/" help/C/*
  install -Dm644 res/com.github.alexhuntley.Plots.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/plots.svg"
  install -Dm644 res/com.github.alexhuntley.Plots-symbolic.svg "$pkgdir/usr/share/icons/hicolor/symbolic/apps/plots-symbolic.svg"
  install -Dm644 res/com.github.alexhuntley.Plots.desktop "$pkgdir/usr/share/applications/plots.desktop"
  sed -Ei 's/^(Icon=).*$/\1plots/' "$pkgdir/usr/share/applications/plots.desktop"
}
