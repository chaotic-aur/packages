# Maintainer: Martin Rys <https://rys.rs/contact>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

_pkgname=Flatseal
pkgname=flatseal
pkgver=2.4.0
pkgrel=1
pkgdesc='A permissions manager for Flatpak'
url="https://github.com/tchx84/Flatseal/"
arch=('any')
license=('GPL-3.0-or-later')
depends=('flatpak' 'gjs' 'webkitgtk-6.0' 'appstream' 'libadwaita')
makedepends=('meson' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tchx84/Flatseal/archive/v${pkgver}.tar.gz")
sha256sums=('8f00ab3a3ea8e5ce3c5c7c1f345518c18da72fa9fad3a3f27b86ffc93e339178')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  meson setup --prefix /usr --buildtype=plain . build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build
}
