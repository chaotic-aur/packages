# Contributor: Greg White <gwhite@kupulau.com>
# Maintainer: Greg White <gwhite@kupulau.com>

pkgname=brave-beta-bin
pkgver=1.68.115
pkgrel=1
pkgdesc='Web browser that blocks ads and trackers by default (beta binary release).'
arch=(x86_64 aarch64)
url='https://brave.com/download-beta'
license=('MPL2')
depends=('gtk3' 'nss' 'alsa-lib' 'libxss' 'ttf-font')
optdepends=('cups: Printer support'
  'mesa: Hardware accelerated rendering'
  'libglvnd: Support multiple different OpenGL drivers at any given time'
  'libgnome-keyring: gnome keyriung support')
provides=("${pkgname}" 'brave-beta-browser')
conflicts=()
source=("brave-beta-bin.sh")
source_x86_64=("https://github.com/brave/brave-browser/releases/download/v${pkgver}/brave-browser-beta_${pkgver}_amd64.deb")
source_aarch64=("https://github.com/brave/brave-browser/releases/download/v${pkgver}/brave-browser-beta_${pkgver}_arm64.deb")
sha512sums=('56be4daa8294c7c3d4b3f1ce7169ac575be0faa623d7cd977eb135e971914b90e3f596ecad72d89178de04e65cd3d49c138e8518d1dccf9fc814d0858b38767e')
sha512sums_x86_64=('f732d9cde1ee7ab8c6cb175dc235284877044075ed25aa3db19a637b593749b26c78f33e3ff27548d62e1be4a18140e13191d8d299f5bd0b6a3d51800e9dbe17')
sha512sums_aarch64=('0ccfda9e4a45a25824130ad128db2e5854e9670dc8ce0c4e9ca51529b15057ed1519fde70465dfe3f8b38f1895aa967c5cda138352a2d1263cd4ebcadcd97919')

options=(!strip)

prepare() {
  mkdir -p brave
  tar xf data.tar.xz -C brave
  # Delete unneeded cron job
  rm -rf brave/opt/brave.com/brave-beta/cron
  # Use our script to launch (allows overriding flags, sets up data dir)
  sed -i "s/\/usr\/bin\/brave-browser-beta/\/usr\/bin\/brave-beta/g" brave/usr/share/applications/brave-browser-beta.desktop
}

package() {
  cp -a --reflink=auto brave/opt "$pkgdir/opt"
  cp -a --reflink=auto brave/usr "$pkgdir/usr"

  install -Dm0755 "$pkgname.sh" "$pkgdir/usr/bin/brave-beta"
  install -Dm0644 "brave/opt/brave.com/brave-beta/product_logo_128_beta.png" "$pkgdir/usr/share/pixmaps/brave-browser-beta.png"

  install -Dm0664 -t "$pkgdir/usr/share/licenses/$pkgname" "brave/opt/brave.com/brave-beta/LICENSE"
  chmod 4755 "$pkgdir/opt/brave.com/brave-beta/chrome-sandbox"
}
