# Maintainer: aur.chaotic.cx

_pkgname="ckb-next"
pkgname="$_pkgname-qt6-git"
pkgver=0.6.0.r62.g22cdb6d
pkgrel=1
pkgdesc="Corsair Keyboard and Mouse Input Driver"
url="https://github.com/ckb-next/ckb-next"
license=('GPL-2.0-or-later')
arch=('x86_64')

depends=(
  'hicolor-icon-theme'
  'quazip-qt6'
)
makedepends=(
  'cmake'
  'git'
  'ninja'
  'qt6-tools'
  'libpulse'
)
optdepends=(
  'libpulse: for mviz'
)

provides=("$_pkgname")
conflicts=("$_pkgname")

install="$_pkgname.install"

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgsrc"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DCMAKE_INSTALL_LIBDIR='lib'
    -DCMAKE_INSTALL_LIBEXECDIR='lib'
    -DDISABLE_UPDATER=ON
    -DFORCE_INIT_SYSTEM="systemd"
    -DPREFER_QT6=ON
    -DUDEV_RULE_DIRECTORY="/usr/lib/udev/rules.d"
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
