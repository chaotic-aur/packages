# Maintainer: Jamie Magee <jamie dot magee at gmail dot com>
_name=azure-functions-core-tools
pkgname=$_name-bin
pkgver=4.1.2 # renovate: datasource=github-tags depName=Azure/azure-functions-core-tools
pkgrel=1
pkgdesc="Command line tools for Azure Functions"
arch=('x86_64')
url="https://github.com/Azure/$_name"
license=('MIT')
provides=($_name)
conflicts=($_name)
optdepends=('dotnet-runtime: to install extensions') # https://github.com/Azure/azure-functions-core-tools/issues/367
options=('staticlibs')
source=("https://github.com/Azure/${_name}/releases/download/${pkgver}/Azure.Functions.Cli.linux-x64.${pkgver}.zip")
b2sums=('d78c07992904399a4892b30671e59de030d4e6eb73d0866192e42b7d671fee10b4b7f3264d40acab2c0cd606f838672551530e900c85a6c1d612affb7b19260e')

package() {
  install -dm 755 "${pkgdir}/usr/lib/${_name}/"
  cp -r "${srcdir}/"* "${pkgdir}/usr/lib/${_name}"
  install -m 755 "func" "${pkgdir}/usr/lib/${_name}/"
  chmod a+x "${pkgdir}/usr/lib/${_name}/gozip" # https://github.com/Azure/azure-functions-core-tools/issues/1850

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${_name}/func" "${pkgdir}/usr/bin/func"
}
