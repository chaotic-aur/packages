# Maintainer: Jamie Magee <jamie dot magee at gmail dot com>
_name=azure-functions-core-tools
pkgname=$_name-bin
pkgver=4.0.7030 # renovate: datasource=github-tags depName=Azure/azure-functions-core-tools
pkgrel=1
pkgdesc="Command line tools for Azure Functions"
arch=('x86_64')
url="https://github.com/Azure/$_name"
license=('MIT')
provides=($_name)
conflicts=($_name)
optdepends=('dotnet-runtime: to install extensions') # https://github.com/Azure/azure-functions-core-tools/issues/367
options=('staticlibs')
source=("https://github.com/Azure/${_name}/releases/download/${pkgver}/Azure.Functions.Cli.linux-x64.${pkgver}.zip")
b2sums=('240eacbb0b2ec44fce62aab388d7e43763200c8a5d52791a55ce706015fc9aed239f686d427bdb5d3272d4852aa0f99e293d0bdd9f7208490d8dcabc50a578a5')

package() {
  install -dm 755 "${pkgdir}/usr/lib/${_name}/"
  cp -r "${srcdir}/"* "${pkgdir}/usr/lib/${_name}"
  install -m 755 "func" "${pkgdir}/usr/lib/${_name}/"
  chmod a+x "${pkgdir}/usr/lib/${_name}/gozip" # https://github.com/Azure/azure-functions-core-tools/issues/1850

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${_name}/func" "${pkgdir}/usr/bin/func"
}
