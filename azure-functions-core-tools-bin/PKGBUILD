# Maintainer: Jamie Magee <jamie dot magee at gmail dot com>
_name=azure-functions-core-tools
pkgname=$_name-bin
pkgver=4.0.5907 # renovate: datasource=github-tags depName=Azure/azure-functions-core-tools
pkgrel=1
pkgdesc="Command line tools for Azure Functions"
arch=('x86_64')
url="https://github.com/Azure/$_name"
license=('MIT')
provides=($_name)
conflicts=($_name)
optdepends=('dotnet-runtime: to install extensions') # https://github.com/Azure/azure-functions-core-tools/issues/367
options=('staticlibs')
source=("https://github.com/Azure/${_name}/releases/download/${pkgver}/Azure.Functions.Cli.linux-x64.${pkgver}.zip")
b2sums=('fe86231d51ae3c6e221a2765915e085dcbad033f05b8ffba2db1a6de0983fa66fff689a9cea71af22400bcf11b624b376ab36c2c5c89c6fa567990f2180d9dfa')

package() {
  install -dm 755 "${pkgdir}/usr/lib/${_name}/"
  cp -r "${srcdir}/"* "${pkgdir}/usr/lib/${_name}"
  install -m 755 "func" "${pkgdir}/usr/lib/${_name}/"
  chmod a+x "${pkgdir}/usr/lib/${_name}/gozip" # https://github.com/Azure/azure-functions-core-tools/issues/1850

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${_name}/func" "${pkgdir}/usr/bin/func"
}
